
pkgname=nodejs
pkgver=5.1.0
pkgrel=1
pkgdesc="Platform built on Chrome's JavaScript runtime for easily building fast, scalable network applications."
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('python2' 'openssl')
checkdepends=('curl') 
options=('!emptydirs')
source=("http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.xz")
md5sums=('d952bd87d951906936eb9b126efa4ac6')

build() {
  cd "node-v${pkgver}"
  
  ./configure --prefix=/usr
  make
}

check() {
  cd "node-v${pkgver}"
  
  make test 
}

package() {
  cd "node-v${pkgver}"
  make install DESTDIR="${pkgdir}"


  install -d "${pkgdir}/usr/share/doc/nodejs"
  cp -r doc/api/* "${pkgdir}/usr/share/doc/nodejs"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/nodejs/LICENSE"
}

