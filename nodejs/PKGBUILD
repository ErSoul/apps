
pkgname=nodejs
pkgver=5.2.0
pkgrel=1
pkgdesc="Platform built on Chrome's JavaScript runtime for easily building fast, scalable network applications."
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('python2' 'openssl' 'zlib' 'icu')
makedepends=('procps-ng')
checkdepends=('curl') 
options=('!emptydirs')
source=("http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.xz")
md5sums=('7fec59e23add5f6f68b89029f7bb8b89')

build() {
  cd "node-v${pkgver}"
  
  ./configure --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib 
  make
}

check() {
  cd "node-v${pkgver}"
  
  make test 
}

package() {
  cd "node-v${pkgver}"
  make install DESTDIR="${pkgdir}"


  install -d "${pkgdir}/usr/share/doc/nodejs"
  cp -r doc/api/* "${pkgdir}/usr/share/doc/nodejs"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/nodejs/LICENSE"
}

