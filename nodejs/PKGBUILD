
pkgname=nodejs
pkgver=8.1.1
pkgrel=1
pkgdesc="Platform built on Chrome's JavaScript runtime for easily building fast, scalable network applications."
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('python2' 'openssl' 'zlib' 'icu' 'libuv' 'c-ares')
makedepends=('procps-ng')
checkdepends=('curl') 
options=('!emptydirs')
source=("https://github.com/nodejs/node/archive/v${pkgver}.tar.gz")
md5sums=('6dc8d9353081f857773d962156a7f160')

build() {
  cd node-${pkgver}
  
  ./configure --prefix=/usr \
    --with-intl=system-icu \
    --shared-openssl \
    --shared-cares \
    --shared-zlib 
  make
}

check() {
  cd node-${pkgver}
  # 1 test fails bc of system-icu use
  make -j1 test
}

package() {
  cd node-${pkgver}
  make install DESTDIR=${pkgdir}


  install -d ${pkgdir}/usr/share/doc/nodejs
  cp -r doc/api/* ${pkgdir}/usr/share/doc/nodejs
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/nodejs/LICENSE
}

