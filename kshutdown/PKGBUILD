
pkgname=kshutdown
pkgver=4.0.82
_pkgver=4.1.1beta
pkgrel=1
pkgdesc="Graphical shutdown utility that allows you to turn off or suspend a computer at a specified time."
url="http://kshutdown.sourceforge.net/"
arch=('x86_64')
license=('GPL')
depends=('kconfig' 'kconfigwidgets' 'kdbusaddons' 'kglobalaccel' 'ki18n' 'kidletime' 'knotifications'
         'knotifyconfig' 'kxmlgui')
makedepends=('extra-cmake-modules')
groups=('utilities') 
source=("https://sourceforge.net/projects/kshutdown/files/KShutdown/4.1.1%20Beta/kshutdown-source-${_pkgver}.zip")
sha256sums=('433bffaf37c7d60203dce3933a975dbb5249a681cd124cdfb426e4e77c86df1f')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKS_KF5=TRUE
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
