
pkgname=k3b
pkgver=2.0.2
pkgrel=8
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=('x86_64')
url="http://k3b.sourceforge.net/"
depends=('kde-runtime' 'libkcddb' 'libkcompactdisc' 'libsamplerate' 'libmad'
         'ffmpeg' 'taglib' 'libmpcdec' 'libdvdread'
         'cdrdao' 'cdrtools' 'shared-mime-info' 'libxft' 'docbook-xsl')
makedepends=('cmake' 'pkgconfig' 'automoc4' 'dvd+rw-tools')
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager:    for vcd burning support'
            'transcode:    for advanced mpeg conversion support'
            'emovix:       for bootable multimedia cd/dvd support'
            'cdparanoia: for cd ripping support')
license=('GPL')
install=$pkgname.install
groups=('multimedia')
options=(!libtool)
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'k3b-2.0.2-ffmpeg.patch'
        'libavformat.patch'
        'k3bffmpegwrapper.patch')
md5sums=('c86113af31a2032e57fd2f302b5f637a'
         '7b57e59a4a614d9aa0602ac9f97d0b25'
         '7aae3d8298d8372dce5ee1ac09628eae'
         'ba49bcbe63812545b9ef54978a6251c4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/k3b-2.0.2-ffmpeg.patch"
  patch -p1 -i "${srcdir}/libavformat.patch"
  patch -p1 -i "${srcdir}/k3bffmpegwrapper.patch"
  cd ..
  
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/build"
  
  make DESTDIR="${pkgdir}" install
}

