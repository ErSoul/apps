
pkgname=acetoneiso
pkgver=2.3
pkgrel=1
pkgdesc="All in one ISO tool (bin mdf nrg img daa dmg cdi b5i bwi pdi iso)"
arch=('x86_64')
url="http://www.acetoneteam.org"
license=('GPL3')
depends=('qtwebkit' 'fuseiso' 'cdrdao' 'cdrkit' 'gnupg2' 'pinentry' 'mplayer' 'phonon' 'p7zip' 'dvd+rw-tools')
groups=('utils')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/acetoneiso/AcetoneISO/AcetoneISO%202.3/${pkgname}_${pkgver}.tar.gz")
md5sums=('225f7af687016690ff28b9771a1ce179')

prepare() {
    cd ${pkgname}_${pkgver}/${pkgname}/sources

    sed -i 's|<Phonon/|<phonon/|' erase_cd.h erase_cd.cpp erase_dvd.cpp burn_iso_2_cd.cpp burn_iso_2_cd.h burn_iso_2_dvd.cpp
}


build() {
    cd ${pkgname}_${pkgver}/${pkgname}

    qmake
    sed -i 's|-I/usr/include/QtCore|-I/usr/include/QtCore -I/usr/include/KDE|' Makefile
    make 
}

package() {
    cd ${pkgname}_${pkgver}/${pkgname}

    make INSTALL_ROOT=${pkgdir} install
}
