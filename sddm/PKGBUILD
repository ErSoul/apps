
pkgname=sddm
pkgver=20160902
_commit=ecb903e48822bd90650bdd64fe80754e3e9664cb
pkgrel=1
pkgdesc="QML based X11 display manager"
arch=('x86_64')
url="http://github.com/sddm/sddm"
license=('GPL')
depends=('qt5-declarative' 'libxcb')
makedepends=('extra-cmake-modules' 'qt5-tools' 'python2-docutils')
conflicts=('sddm-qt5-git' 'kf5-sddm')
provides=('sddm-qt5-git' 'kf5-sddm')
replaces=('sddm-qt5-git' 'kf5-sddm')
backup=('etc/sddm.conf')
install=sddm.install
source=("https://github.com/sddm/sddm/archive/${_commit}.zip"
        'sddm.conf'
        'background.png'
        'Main.qml'
        'wayland.desktop')
md5sums=('8a0f7bf5bf630eb9a12d37379a3f30b8'
         '93fa7a39377cc02494f652ffd2018554'
         'f6f2ba7460eaf337b9e8e4bce35b03b0'
         '61fa003d4721c4d3cad2ebbecb529898'
         '177ad40d119220786a32f1bf2ac44486')
  
build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
    -DBUILD_MAN_PAGES=ON 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -m 644 -p ${srcdir}/sddm.conf ${pkgdir}/etc/
  install -d -g 136 -o 136 ${pkgdir}/var/lib/sddm
  #install -Dm644 $srcdir/wayland.desktop $pkgdir/usr/share/wayland-sessions/wayland.desktop
  # fix systemd 230 double mounting of /
  #sed -i 's|PrivateTmp=yes|PrivateTmp=no|' ${pkgdir}/usr/lib/systemd/system/sddm.service
}
