
pkgname=knights
pkgver=2.5.87
pkgrel=1
_commit=6e58843955d197ae45a05f21edbebb3761c35e42
pkgdesc="Chess board for KDE with XBoard protocol support"
arch=('x86_64')
url="https://www.linux-apps.com/content/show.php/Knights?content=122046"
license=('GPL')
depends=('kcoreaddons' 'kconfig' 'kwidgetsaddons' 'kdbusaddons' 'ki18n' 'kconfigwidgets'
         'ktextwidgets' 'kxmlgui' 'kio' 'plasma-framework' 'kplotting' 'knotifyconfig'
         'kwallet' 'kcompletion' 'libkdegames' 'gnuchess' 'qt5-speech')
makedepends=('extra-cmake-modules' 'kdoctools' 'clang')
source=("https://github.com/KDE/knights/archive/${_commit}.zip"
        'historywidget.diff')
md5sums=('2f4a56a6f2dc8bbc242e4d8fd38c324a'
         '4ad894eb4ab4f0a362b2a0fd77d89c31')

prepare() {
  cd $pkgname-${_commit}
  
  patch -p1 -i $srcdir/historywidget.diff
}

build() {
  mkdir -p build
  cd build 
  
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
