pkgname=otter-browser
pkgver=1.0.01
pkgrel=1
pkgdesc='Browser aiming to recreate the best aspects of the classic Opera (12.x) UI using Qt5.'
arch=('x86_64')
url="http://www.otter-browser.org"
license=('GPL3')
depends=('qtwebkit-tp' 'qt5-multimedia' 'desktop-file-utils' 'hicolor-icon-theme' 'hunspell' 'qt5-svg' 'qt5-xmlpatterns') 
makedepends=('cmake' 'qt5-tools')
groups=('network-web')
source=("https://sourceforge.net/projects/otter-browser/files/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('2867c80887f6adcc3fe3a58818e29e05')

prepare() {
  cd ${pkgname}-${pkgver}
  
  sed -i -e 's|hunspell-1.5|hunspell-1.7|' cmake/FindHunspell.cmake
}

build() {
  cd ${pkgname}-${pkgver}
  /usr/lib/qt5/bin/lrelease resources/translations/*.ts

  mkdir -p build
  cd build
  
  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QTWEBENGINE=OFF \
    -DENABLE_QTWEBKIT=ON 
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make DESTDIR=${pkgdir} install
  
  install -D -m0644 ../../${pkgname}-${pkgver}/COPYING  ${pkgdir}/usr/share/licenses/otter-browser/COPYING
}

