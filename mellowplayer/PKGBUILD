
pkgname=mellowplayer
_pkgname=MellowPlayer
pkgver=3.4.0
_commit=0f919a1
pkgrel=1
pkgdesc="Open source and cross-platform desktop application that runs web-based music streaming 
         services in its own window and provides integration with your desktop, specifically crafted for KaOS"
url='https://colinduquesnoy.github.io/MellowPlayer/'
license=('GPL')
arch=('x86_64')
depends=('kglobalaccel' 'knotifications' 'qtwebengine' 'snorenotify' 'pepper-flash' 'widevine'
         'qt5-quickcontrols2' 'qt5-quickcontrols' 'libnotify')
makedepends=('qt5-tools' 'qtcreator')
#source=("https://github.com/ColinDuquesnoy/MellowPlayer/tarball/qbs/MellowPlayer-${pkgver}.tar.gz")
source=("https://github.com/ColinDuquesnoy/MellowPlayer/archive/${pkgver}.tar.gz")
md5sums=('60aac9e1f5f32d699920e44e2f3af790')

prepare() {
  cd ${_pkgname}-${pkgver}

  sed -i 's|Exec=MellowPlayer|Exec=MellowPlayer --ppapi-widevine-path=/usr/lib/chromium/libwidevinecdmadapter.so|' src/main/share/applications/mellowplayer.desktop
  
  qbs-setup-toolchains --detect
  qbs-setup-qt /usr/lib/qt5/bin/qmake qt5
  qbs-config defaultProfile qt5
}


build() {
  cd ${_pkgname}-${pkgver}
  
  qbs build --no-install -d build profile:qt5 config:release qbs.installRoot:/usr projects.MellowPlayer.defaultTheme:Midna modules.cpp.linkerFlags:-fuse-ld=gold
}

package() {
  cd ${_pkgname}-${pkgver}

  qbs install -d build --no-build -v --install-root ${pkgdir}/usr profile:qt5 config:release
}
