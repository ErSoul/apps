
pkgname=mellowplayer
_pkgname=MellowPlayer
pkgver=3.2.0
_commit=8d349c7
pkgrel=1
pkgdesc="Open source and cross-platform desktop application that runs web-based music streaming 
         services in its own window and provides integration with your desktop, specifically crafted for KaOS"
url='https://colinduquesnoy.github.io/MellowPlayer/'
license=('GPL')
arch=('x86_64')
depends=('kglobalaccel' 'knotifications' 'qtwebengine' 'snorenotify' 'pepper-flash' 'widevine'
         'qt5-quickcontrols2' 'qt5-quickcontrols' 'libnotify')
makedepends=('qt5-tools')
source=("https://github.com/ColinDuquesnoy/MellowPlayer/tarball/develop/MellowPlayer-${pkgver}.tar.gz")
#source=("https://github.com/ColinDuquesnoy/MellowPlayer/archive/${pkgver}.tar.gz")
md5sums=('6457e188289b9883875377bfc9d0b5db')

prepare() {
  cd ColinDuquesnoy-${_pkgname}-${_commit}

  sed -i 's|Exec=MellowPlayer|Exec=MellowPlayer --ppapi-widevine-path=/usr/lib/chromium/libwidevinecdmadapter.so|' src/share/applications/mellowplayer.desktop
}


build() {
  mkdir -p build
  cd build
  
  cmake ../ColinDuquesnoy-${_pkgname}-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTS=OFF \
    -Wno-dev \
    -DDEFAULT_THEME="Midna"
  make 
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
