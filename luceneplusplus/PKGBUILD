
pkgname=luceneplusplus
pkgver=3.0.7
_commit=6aec0704f42ab69dc7b286ceeaf9ee1020843084
pkgrel=3
pkgdesc="Up to date C++ port of the popular Java Lucene library, a high-performance, full-featured text search engine."
arch=('x86_64')
url='https://github.com/luceneplusplus/LucenePlusPlus'
license=('GPL' 'Apache')
depends=('boost-libs')
makedepends=('boost' 'cmake')
source=("https://github.com/luceneplusplus/LucenePlusPlus/archive/${_commit}.zip")
md5sums=('4be18325ab4394b7c8e5ccebc062e6ed')

build() {
  mkdir -p build
  cd build

  cmake ../LucenePlusPlus-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib \
    -DENABLE_DEMO=OFF \
    -DENABLE_TEST=OFF

  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
