
pkgname=avogadrolibs
pkgver=1.90.0
pkgrel=9
pkgdesc="Libraries providing 3D rendering, visualization, analysis and data processing useful in computational chemistry and related areas."
arch=('x86_64')
url="http://www.openchemistry.org/projects/avogadro2/"
license=('GPL2')
depends=('qt5-base' 'glew' 'hdf5' 'spglib') #'jsoncpp' 
makedepends=('cmake' 'boost' 'eigen3')
groups=('education')
conflicts=('avogadro')
replaces=('avogadro')
source=("https://github.com/OpenChemistry/avogadrolibs/archive/${pkgver}.tar.gz")
md5sums=('f2b7014f48da5b2a72d49c01dafaf225')

prepare() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i ${srcdir}/boost.patch
  sed -i 's|NAMES spglib|NAMES symspg|' cmake/FindSpglib.cmake
}

build() {
  mkdir build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_BOOST_PYTHON=OFF \
    -DUSE_MOLEQUEUE=OFF \
    -DUSE_OPENGL=ON \
    -DUSE_QT=ON \
    -DUSE_HDF5=ON \
    -DUSE_VTK=OFF
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  # use system jsoncpp
  #rm ${pkgdir}/usr/lib/libjsoncpp.a
  
  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
