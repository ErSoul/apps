
pkgname=plasma-nm
pkgver=0.9.3.2
pkgrel=1
pkgdesc="Plasma applet written in QML for managing network connections"
arch=('x86_64')
url="https://projects.kde.org/projects/playground/network/plasma-nm"
license=('GPL')
depends=('kde-workspace' 'libnm-qt' 'mobile-broadband-provider-info')
makedepends=('cmake' 'automoc4')
provides=('kdeplasma-applets-networkmanagement')
replaces=('kdeplasma-applets-networkmanagement')
conflicts=('kdeplasma-applets-networkmanagement')
source=("http://download.kde.org/unstable/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('67dde011a859f2d72633cf3af5c9f847')

build() {
  cd $pkgname-$pkgver
  mkdir build
  
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd $pkgname-$pkgver/build
  
  make DESTDIR="$pkgdir" install
}
