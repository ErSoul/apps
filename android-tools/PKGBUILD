
pkgname=android-tools
pkgver=5.1.1_r34
pkgrel=2
pkgdesc="Android platform tools (adb, fastboot)"
arch=('x86_64')
url="http://tools.android.com/"
license=('Apache')
depends=('openssl')
makedepends=('git')
# tags at 
source=("git+https://android.googlesource.com/platform/system/core#tag=android-${pkgver}"
        "git+https://android.googlesource.com/platform/system/extras#tag=android-${pkgver}"
        "git+https://android.googlesource.com/platform/external/libselinux#tag=android-${pkgver}"
        "git+https://android.googlesource.com/platform/external/f2fs-tools#tag=android-$pkgver"
        'adbMakefile'
        'fastbootMakefile')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'bc9aba92631da8729d65acd2d1a3602b'
         'bddc9b6c4ebdb3a63c8fab33e19a5c4e')

prepare() {
  sed -e 's/f2fs_sparse_file = NULL/f2fs_sparse_file = 0/g' -i extras/f2fs_utils/f2fs_utils.c 
}         
         
build() {
  cd "${srcdir}"/core/adb
  mv ../../adbMakefile makefile
  make

  cd "${srcdir}"/core/fastboot
  mv ../../fastbootMakefile makefile
  make
}

package(){
  cd "${srcdir}"/core

  install -Dm 755 adb/adb "${pkgdir}"/usr/bin/adb
  install -Dm 755 fastboot/fastboot "${pkgdir}"/usr/bin/fastboot
}

