
pkgname=android-tools
pkgver=5.0.0_r2
pkgrel=1
pkgdesc="Android platform tools (adb, fastboot)"
arch=('x86_64')
url="http://tools.android.com/"
license=('Apache')
depends=('openssl')
makedepends=('git')
source=("git+https://android.googlesource.com/platform/system/core#tag=android-${pkgver}"
        "git+https://android.googlesource.com/platform/system/extras#tag=android-${pkgver}"
        "git+https://android.googlesource.com/platform/external/libselinux#tag=android-${pkgver}"
        "git+https://android.googlesource.com/platform/external/f2fs-tools#tag=android-$pkgver"
        'adbMakefile'
        'fastbootMakefile')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         '78e6236c2d37468c2c0102d89be48fa5'
         'f99cbcef4c2289399060eba2e648f511')

prepare() {
  sed -e 's/f2fs_sparse_file = NULL/f2fs_sparse_file = 0/g' -i extras/f2fs_utils/f2fs_utils.c 
}         
         
build() {
  cd "${srcdir}"/core/adb
  mv ../../adbMakefile makefile
  make

  cd "${srcdir}"/core/fastboot
  mv ../../fastbootMakefile makefile
  make
}

package(){
  cd "${srcdir}"/core

  install -Dm 755 adb/adb "${pkgdir}"/usr/bin/adb
  install -Dm 755 fastboot/fastboot "${pkgdir}"/usr/bin/fastboot
}

