
pkgname=plasma-browser-integration
pkgver=5.12.81
_commit=0467813750d18b66c1f5f3f2c795381e3cc7b26e
pkgrel=1
arch=('x86_64')
pkgdesc="Plasma5 browser integration"
url="https://github.com/KDE/plasma-browser-integration"
license=('GPL')
depends=('kio' 'ki18n' 'knotifications' 'krunner' 'kwindowsystem' 'kactivities')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("https://github.com/KDE/plasma-browser-integration/archive/${_commit}.zip")
md5sums=('2f957b78ee7a0e35bbfb619dd41a3aa8')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DINSTALL_CHROME_MANIFEST=ON
    
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
  
  mkdir -p ${pkgdir}/opt/google/chrome-unstable/native-messaging-hosts
  mv ${pkgdir}/etc/opt/chrome/native-messaging-hosts/org.kde.plasma.browser_integration.json ${pkgdir}/opt/google/chrome-unstable/native-messaging-hosts/org.kde.plasma.browser_integration.json
  rm -r ${pkgdir}/etc/opt
}
