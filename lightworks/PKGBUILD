 
pkgname=lightworks
pkgver=11.5
pkgrel=4
pkgdesc="A professional video editing software"
arch=('x86_64')
url="http://www.lwks.com/"
license=('custom')
install="${pkgname}.install"
depends=('gtk3' 'portaudio-svn' 'libgl' 'glu' 'ffmpeg' 'libedit' 'libtiff' 'nvidia-cg-toolkit' 'libgsf')
source=("lwks-${pkgver}-amd64.deb")
md5sums=('748a8f916121e282f95ba01020565cd6')

noextract=("lwks-${pkgver}-amd64.deb")
 
package() {
  bsdtar -xf "lwks-${pkgver}-amd64.deb" data.tar.gz  
  bsdtar -xf data.tar.gz -C "${pkgdir}"
	rm data.tar.gz
  
  mv -f "${pkgdir}/lib/udev" "${pkgdir}/usr/lib"
  rmdir "${pkgdir}/lib"
  #fix icon path in .desktop file
  sed -i 's|Icon=/usr/share/lightworks/Icons/App.png|Icon=lightworks|' \
   "${pkgdir}/usr/share/applications/lightworks.desktop"
  
  mkdir -p "${pkgdir}/usr/share/licenses/lightworks/"
  mv -f "${pkgdir}/usr/share/doc/lightworks/copyright" "${pkgdir}/usr/share/licenses/lightworks/"
  rm -rf "${pkgdir}/usr/share/doc"
}
