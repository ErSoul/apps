 
pkgname=lightworks
pkgver=14.0.0.2
_pkgver=14.0.0.0
pkgrel=1
pkgdesc="A professional video editing software"
arch=('x86_64')
url="http://www.lwks.com/"
license=('custom')
install="${pkgname}.install"
depends=('gtk3' 'portaudio-svn' 'libgl' 'glu' 'ffmpeg' 'libedit' 'libtiff' 'nvidia-cg-toolkit' 'libgsf')
source=("http://downloads.lwks.com/Lightworks-Beta-91065-${_pkgver}.deb"
        'libportaudio.so.2.0.0'
        'libportaudiocpp.so.0.0.12')
md5sums=('e6d3b68e2497a95aaf44d2c2f03c9da7'
         'acfd08eb31cadb75a3a0e86cce54220b'
         'e9e13d426aab9dcf15a7dc81c21e6396')

noextract=("lwks-${pkgver}-amd64.deb")
 
package() {
  bsdtar -xf "Lightworks-Beta-91065-${_pkgver}.deb" data.tar.xz  
  bsdtar -xf data.tar.xz -C "${pkgdir}"
	rm data.tar.xz
  
  mv -f ${pkgdir}/lib/udev ${pkgdir}/usr/lib
  rmdir ${pkgdir}/lib
  #fix icon path in .desktop file
  sed -i 's|Icon=/usr/share/lightworks/Icons/App.png|Icon=lightworks|' \
   ${pkgdir}/usr/share/applications/lightworks.desktop
  
  mkdir -p ${pkgdir}/usr/share/licenses/lightworks/
  mv -f ${pkgdir}/usr/share/doc/lightworks/copyright ${pkgdir}/usr/share/licenses/lightworks/
  rm -rf ${pkgdir}/usr/share/doc
  
  # add portaudio 19.1 libs until upstream ready for newer
  cp ${srcdir}/libportaudio.so.2.0.0 ${pkgdir}/usr/lib/lightworks/
  cp ${srcdir}/libportaudiocpp.so.0.0.12 ${pkgdir}/usr/lib/lightworks/
  
  cd ${pkgdir}/usr/lib/lightworks/
  ln -s libportaudio.so.2.0.0 libportaudio.so
  ln -s libportaudio.so.2.0.0 libportaudio.so.2
  ln -s libportaudiocpp.so.0.0.12 libportaudiocpp.so
  ln -s libportaudiocpp.so.0.0.12 libportaudiocpp.so.0
}
