
pkgname=kjots
pkgver=5.0.1
pkgrel=2
arch=('x86_64')
pkgdesc="Note taking application for KDE using Akonadi"
url="https://projects.kde.org/projects/playground/pim/kjots"
license=('GPL' 'LGPL' 'FDL')
depends=('kcmutils' 'kio' 'kconfig' 'kconfigwidgets' 'kparts' 'kbookmarks' 'kxmlgui' 'ktextwidgets'
         'akonadi' 'kmime' 'kdepimlibs' 'kpimtextedit' 'kontactinterface' 
         'grantlee')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
source=("http://download.kde.org/stable/kjots/${pkgver}/src/kjots-${pkgver}.tar.xz"
        "https://github.com/KDE/kjots/commit/d536dbdf606d18baa437d03a9852fa6bb7289953.diff")
md5sums=('edb0a82781dbb75d1fe69f2eb6f7cf28'
         'c425116b8dbdcec2f6860f1c691c3026')

prepare() {
  cd ${pkgname}-${pkgver}
  
  # fix .desktop file
  patch -p1 -i ${srcdir}/d536dbdf606d18baa437d03a9852fa6bb7289953.diff
}
  
build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=FALSE 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
