
pkgname=grass
pkgver=6.4.4
pkgrel=1
pkgdesc='Geographic Information System (GIS) used for geospatial data management and analysis, image processing, graphics/maps production, spatial modeling, and visualization.'
arch=('x86_64')
url='http://grass.itc.it/index.php'
license=('GPL')
depends=('gdal' 'tk' 'sqlite3' 'python' 'mesa' 'proj' 'libjpeg-turbo' 'libpng' 'libtiff'
         'cfitsio' 'fftw' 'libxmu' 'postgresql' 'xorg-server' 'glu' 'tcl' 'zlib' 'cairo' 'geos')
makedepends=('mariadb' 'freetype2' 'r')
optdepends=('fftw: required for i.fft and i.ifft modules'
            'postgresql: PostgreSQL database interface'
            'r: R language interface'
            'lapack: required for GMATH library'
            'blas: required for GMATH library'
            'xorg-server: required for the graphical interface')
options=('!libtool' '!makeflags')
groups=('graphics')
install='grass.install'
source=("http://grass.osgeo.org/grass64/source/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.sh"
        "${pkgname}.conf")
md5sums=('4b3e0caaeb1567e15c78b523e3674170'
         '23da2e9399b3c5504851dec37821abe1'
         '6103480c2a1adc19a50b9e925e5e6d4c')

build() {
  cd ${pkgname}-${pkgver}

  # see ${srcdir}/grass-6.4.0/REQUIREMENTS.html for options
  ./configure --enable-64bit \
    --prefix=/opt \
    --with-mysql-includes=/usr/include/mysql \
    --with-mysql \
    --with-sqlite \
    --with-postgres \
    --with-fftw \
    --with-gdal=/usr/bin/gdal-config \
    --with-python \
    --with-blas \
    --with-lapack \
    --with-geos \
    --with-cairo \
    --with-proj-libs=/usr/lib \
    --with-proj-includes=/usr/include \
    --with-proj-share=/usr/share/proj \
    --with-fftw-includes=/usr/include \
    --with-fftw-libs=/usr/lib \
    --with-freetype \
    --with-freetype-includes=/usr/include/freetype2 \
    --without-glw \
    --without-wxwidgets \
    --without-htmldocs
   
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make \
    INST_DIR=${pkgdir}/opt/grass-${pkgver} \
    BINDIR=${pkgdir}/usr/bin \
    install

  # fix $GISBASE path
  sed -i "s|GISBASE=${pkgdir}/opt/grass-${pkgver}|GISBASE=/opt/grass-${pkgver}|g" ${pkgdir}/usr/bin/grass64

  # strip html docs
  #rm -r ${pkgdir}/opt/grass-${pkgver}/docs/html

  # install profile.d file
  install -D ${srcdir}/grass.sh ${pkgdir}/etc/profile.d/grass.sh

  # install some freedesktop.org compatibility
   install -D -m644 gui/icons/grass.desktop ${pkgdir}/usr/share/applications/grass.desktop

  sed -i -e 's/grass65/grass64/' -e 's_/usr/share/icons_/usr/share/pixmaps_' \
    ${pkgdir}/usr/share/applications/grass.desktop

  install -D -m644 gui/icons/grass-48x48.png ${pkgdir}/usr/share/pixmaps/grass-48x48.png

  install -D -m644 ${srcdir}/grass.conf ${pkgdir}/etc/ld.so.conf.d/grass.conf
}
