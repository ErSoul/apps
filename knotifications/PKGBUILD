
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=knotifications
pkgver=${_kf5ver}
pkgrel=2
pkgdesc='KNotifications'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/knotifications'
license=('LGPL')
depends=('kiconthemes' 'kservice' 'kwindowsystem' 'libdbusmenu-qt5' 'libxslt' 'phonon-qt5' 'qt5-speech')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/5.37/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/KDE/knotifications/commit/1c97e1d9741fd15962474f47932dd09728dae76b.diff")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         'bb0b33849b5b5a2c1800fa1af88003d5')

prepare() {
  cd ${pkgname}-${pkgver}
  # https://bugs.kde.org/show_bug.cgi?id=382444
  patch -p1 -i ${srcdir}/1c97e1d9741fd15962474f47932dd09728dae76b.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR="${pkgdir}" install
}
