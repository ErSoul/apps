
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kopete
#pkgver=${_kdever}
pkgver=17.03.71
_commit=285c9aadaa5f584bf52899fc7233557cae67f62c
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
#groups=('kde' 'kdenetwork' 'kde-uninstall' 'network')
depends=('kcoreaddons' 'ki18n' 'kcmutils' 'khtml' 'knotifyconfig' 'kparts' 'kwallet'
         'kdelibs4support' 'ktexteditor' 'kcrash' 'kconfig' 'kemoticons' 'kdbusaddons' 'kdnssd'
         'kcontacts' 'kidentitymanagement' 'libkleo' 'gpgme'        
         'libotr' 'libidn' 'qimageblitz' 'libxss' 'v4l-utils' # 'mediastreamer' 'ortp' 'libgadu' 
         'jsoncpp' 'qca-qt5' 'sqlite' 'speex' 'phonon-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
#source=("$_mirror/${pkgname}-$_kdever.tar.xz")
#md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)
source=("https://github.com/KDE/kopete/archive/${_commit}.zip")
md5sums=('f34fcb9bdb3031e7b761dfab0cee53d2')

prepare() {
  cd ${pkgname}-${_commit}
  sed -i -e 's|NAMES kleo/kleo_export.h|NAMES KF5/libkleo/kleo_export.h|' cmake/modules/FindKleopatra.cmake
  #patch -p1 -i "$srcdir/kde_bug309119.diff" 
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_SKIP_RPATH=ON \
    -DDWITH_jabber=OFF \
    -DWITH_skype=ON
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
