
pkgname=handbrake
pkgver=0.9.9
pkgrel=2
arch=('x86_64')
pkgdesc="Multithreaded video transcoder"
url="http://handbrake.fr/"
license=('GPL')
depends=('bzip2' 'gcc-libs' 'gst-plugins-base' 'libnotify' 'dbus-glib' 'fribidi' 'libass' 'gtk3'
         'fontconfig' 'freetype2' 'libxml2' 'libogg' 'libvorbis' 'libtheora' 'libsamplerate'
         'libbluray' 'x264' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'python2' 'yasm' 'wget')
source=("http://downloads.sourceforge.net/project/handbrake/$pkgver/HandBrake-$pkgver.tar.bz2")
md5sums=('ee81337a46f8af76599d6038f193a2c1')

prepare() {
  cd "$srcdir/HandBrake-$pkgver"
  sed -i 's/python /python2 /' gtk/src/Makefile.am
  # https://forum.handbrake.fr/viewtopic.php?f=13&t=27581
  sed -i \
    -e '/MODULES += contrib\/libbluray/d' \
    -e '/MODULES += contrib\/x264/d' \
    make/include/main.defs

  # For automake 1.13
  cd gtk
  sed -i \
    -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' \
    -e '/AM_PROG_CC_STDC/d' \
    configure.ac

  autoreconf -vi
}

build() {
  cd "$srcdir/HandBrake-$pkgver"

  LDFLAGS='-lgmodule-2.0' 
  ./configure --prefix=/usr --force --disable-gtk-update-checks
  cd build
  make
}

package() {
  cd "$srcdir/HandBrake-$pkgver/build"

  make DESTDIR="$pkgdir" install
}

