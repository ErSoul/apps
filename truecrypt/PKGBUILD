
pkgname=truecrypt
pkgver=7.1a
pkgrel=1
pkgdesc="Free open-source cross-platform disk encryption software"
url="http://www.truecrypt.org/"
arch=('i686' 'x86_64')
license=('custom') 
depends=('fuse>=2.8.0' 'wxgtk>=2.8.9' 'libsm' 'device-mapper' 'libjpeg')
makedepends=('nasm')
source=("http://sourceforge.net/projects/kaosx/files/sources/truecrypt/${pkgname}-${pkgver}-source.tar.xz"
        "http://sourceforge.net/projects/kaosx/files/sources/truecrypt/pkcs-2.20.tar.gz"
        'truecrypt.desktop'
        'detection.patch') 
md5sums=('e333be7fed8c30f7e2ccef2d5e5f08c9'
         'ce6a707b79411e82e8e558aa03e764b0'
         '96cdc0f14d25de813437ad3d825e333b'
         '00ea70fa8437de877b31b027ac2b4060')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}-source"
  patch -p1 -i "${srcdir}/detection.patch"
  
  make PKCS11_INC="${srcdir}/pkcs-2.20" LFLAGS+="-ldl"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-source"

  install -D -m755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 Resources/Icons/TrueCrypt-48x48.xpm "${pkgdir}/usr/share/pixmaps/truecrypt.xpm"
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/License.txt"
}

