
pkgname=libktorrent
pkgver=2.1
pkgrel=1
ktorver=5.1
pkgdesc="A BitTorrent program for KDE"
arch=('x86_64')
url="http://ktorrent.pwsp.net/"
license=('GPL2')
depends=('karchive' 'kcrash' 'ki18n' 'kio' 'solid' 'qca-qt5')
makedepends=('extra-cmake-modules' 'boost' 'doxygen' 'kdoctools')
options=('libtool')
source=("https://download.kde.org/stable/ktorrent/${ktorver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('3eefdcceec73410c558e82d7be22eb2ed18679e9ab64f34cc100b54e186fe26f')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
