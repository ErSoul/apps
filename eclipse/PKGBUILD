
pkgname=eclipse
pkgver=4.4.1
pkgrel=1
_date=201409250400
pkgdesc="An IDE for Java and other languages"
arch=('x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'webkitgtk2' 'libxtst')
install=${pkgname}.install
makedepends=('zip')
license=("EPL/1.0")
source=("http://www.eclipse.org/downloads/download.php?r=1&file=/eclipse/downloads/drops4/R-$pkgver-${_date}/$pkgname-SDK-$pkgver-linux-gtk-x86_64.tar.gz"
	 'eclipse.sh'
	 'eclipse.desktop'
	 'eclipse.svg')
md5sums=('e7712ce2eaf215bebbd1e3a272e2d2b6'
         '7ea99a30fbaf06ec29261541b8eb1e23'
         'ba2cf02c48e6e35bfe3685401c26bb5b'
         '77cff7543ccf7e177cb6667f5a20ce19')

package() {
  install -m755 -d "$pkgdir/usr/share/licenses/eclipse/"
  mv eclipse "$pkgdir/usr/share/"

  install -d $pkgdir/usr/bin $pkgdir/usr/share/applications $pkgdir/usr/share/icons/hicolor/apps
  install -m755 eclipse.sh "$pkgdir/usr/bin/eclipse"
  install -m644 eclipse.desktop "$pkgdir/usr/share/applications/"

  install -Dm644 "$srcdir"/eclipse.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/eclipse.svg
  sed -i "s|#!/usr/bin/python|#!/usr/bin/python2|" "$pkgdir"/usr/share/eclipse/plugins/org.apache.ant_1.9.2.v201404171502/bin/runant.py

  mv $pkgdir/usr/share/eclipse/epl-v10.html "$pkgdir/usr/share/licenses/eclipse/epl-v10.html"
}
