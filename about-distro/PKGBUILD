
pkgname=about-distro
pkgver=2.0.0
pkgrel=1
pkgdesc="KCM displaying distribution and system information"
arch=('x86_64')
url='https://projects.kde.org/about-distro'
license=('GPL3')
depends=('kconfigwidgets' 'solid')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('kf5-about-distro')
replaces=('kf5-about-distro')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"
        'kcm-about-distrorc'
        'KaOS.png')
md5sums=('9614ed30b107f3578a36c86f3c3e4925'
         '1bc1260a89d137cd331459d510526254'
         'f4049305cce2be768eabc0ab29239c0b')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  install -Dm644 "$srcdir/kcm-about-distrorc" "$pkgdir/etc/xdg/kcm-about-distrorc"
  install -Dm644 "$srcdir/KaOS.png" "$pkgdir/usr/share/$pkgname/KaOS.png"
}
