
pkgname=tribe
pkgver=2013.08.28
_rls_notes=20130828
pkgrel=1
pkgdesc="Tribe - KaOS LiveCD Installer, forked from chakra-project"
url="https://github.com/KaOSx/Tribe"
license="GPL"
arch=('x86_64')
depends=('kdelibs' 'kdeedu-marble>=4.11.0' 'squashfs-tools' 'tribe-partitionmanager'
          'rsync' 'mkinitcpio-nfs-utils' 'ntfs-3g' 'libpwquality')
makedepends=('cmake' 'kde-workspace' 'automoc4')
groups=('kde-uninstall' 'system')
install=tools-tribe.install
source=("git://github.com/KaOSx/Tribe.git"
        "RELEASE_NOTES_${_rls_notes}.html"
        "launch-tribe.sh")
md5sums=('SKIP'
         'f0c4b5d257a5cdc6fa7c57f5cfc68217'
         'e31855da237a2c8029774831b3c04685')

build() {        
 
        cd "${srcdir}/Tribe"

	# proper bootmnt folder
	sed -i -e "s/@ARCH@/${CARCH}/g" config-tribe.h.cmake

	# add pkgver to tribe
	sed -i -e "s~source build/from git~Build: ${pkgver}~g" config-tribe.h.cmake
	sed -i -e "s~kaos-version~${pkgver}~g" "scripts/postinstall-functions/01-job-initialize-target"
	cmake . -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' 

	make 
}

package() {
        cd "${srcdir}/Tribe"
	make DESTDIR=${pkgdir} install 
	
        install -Dm755 ${srcdir}/launch-tribe.sh \
        ${pkgdir}/usr/bin/launch-tribe.sh
	cp -vf ${srcdir}/RELEASE_NOTES_${_rls_notes}.html $pkgdir/usr/share/tribe/config/RELEASE_NOTES.html

	# remove nasty files
	find $srcdir/ -name ".git" -type d -exec rm -fr {} +
}
