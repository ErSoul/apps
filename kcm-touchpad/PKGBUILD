
pkgname=kcm-touchpad
pkgver=1.1
pkgrel=1
pkgdesc="Touchpad configuration tool for KDE, provides a System Settings module that allows configuration of touchpads."
arch=('x86_64')
url="https://github.com/sanya-m/kde-touchpad-config"
license=('GPL')
depends=('kinit' 'kcmutils' 'kcoreaddons' 'kconfigwidgets' 'kdbusaddons' 'kcompletion' 'kwidgetsaddons'
         'ki18n' 'kservice' 'knotifyconfig' 'kxmlgui' 'knotifications' 'kglobalaccel' 'plasma-workspace'
         'xf86-input-synaptics' 'libx11' 'libxcb' 'xorg-server' 'libxi')
makedepends=('extra-cmake-modules' 'kdoctools' 'pkg-config' 'git')
conflicts=('kcm-touchpad-config')
groups=('system')
source=("git://anongit.kde.org/kcm-touchpad.git#branch=frameworks"
        'xlibbackend.diff'
        'x11.diff')
md5sums=('SKIP'
         '417d582f137c4d6c8c80501c817e9f66'
         'bb1c795439814faebfad23f71161f544')

prepare() {
  cd ${pkgname}
  patch -p0 -i ${srcdir}/xlibbackend.diff
  patch -p0 -i ${srcdir}/x11.diff
}

build() {
  cd ${srcdir}
  
  mkdir -p build
  cd build

  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}

