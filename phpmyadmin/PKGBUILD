
pkgname=phpmyadmin
pkgver=4.7.9
pkgrel=1
pkgdesc='PHP and hence web-based tool to administrate MySQL over the WWW'
arch=('x86_64')
url='https://www.phpmyadmin.net'
license=('GPL')
depends=('mariadb-clients' 'php')
backup=(etc/webapps/phpmyadmin/config.inc.php)
source=("https://files.phpmyadmin.net/phpMyAdmin/${pkgver}/phpMyAdmin-${pkgver}-all-languages.zip")
sha256sums=('2fb9f7b31ae7cb71f6398e5da8349fb4f41339386e06a851c4444fc7a938a38a')

package() {
  _instdir=${pkgdir}/usr/share/webapps/phpMyAdmin
  mkdir -p ${_instdir} ${pkgdir}/etc/webapps/phpmyadmin
  cd ${_instdir}

  cp -ra ${srcdir}/phpMyAdmin-${pkgver}-all-languages/* .

  ln -s /etc/webapps/phpmyadmin/config.inc.php ${_instdir}/config.inc.php
  cp ${_instdir}/config.sample.inc.php ${pkgdir}/etc/webapps/phpmyadmin/config.inc.php

  cat >${pkgdir}/etc/webapps/phpmyadmin/apache.example.conf <<EOF
	Alias /phpmyadmin "/usr/share/webapps/phpMyAdmin"
	<Directory "/usr/share/webapps/phpMyAdmin">
		AllowOverride All
		Options FollowSymlinks
		Order allow,deny
		Allow from all
		php_admin_value open_basedir "/srv/:/tmp/:/usr/share/webapps/:/etc/webapps:/usr/share/pear/"
	</Directory>
EOF
}
