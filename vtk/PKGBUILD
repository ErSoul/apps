
pkgname=vtk
pkgver=7.0.0
_pkgver=7.0
pkgrel=1
pkgdesc='Open-source software system for 3D computer graphics, image processing, and visualization.'
arch=('x86_64')
url='https://gitlab.kitware.com/vtk/vtk'
license=('BSD')
depends=('java-environment' 'gnuplot' 'tk' 'python2-matplotlib' 'unixodbc' 'gdal' 'openmpi' 'mariadb'
         'glew' 'gl2ps' 'ffmpeg' 'python2-six' 'qt5-tools' 'jsoncpp')
makedepends=('boost' 'cmake')
options=('!emptydirs')
source=("http://www.vtk.org/files/release/${_pkgver}/VTK-${pkgver}.tar.gz"
        "http://www.vtk.org/files/release/${_pkgver}/VTKData-${pkgver}.tar.gz"
        "http://www.vtk.org/files/release/${_pkgver}/VTKLargeData-${pkgver}.tar.gz"
        'gdal2.patch')
md5sums=('0231ca4840408e9dd60af48b314c5b6d'
         'b164200226805aeb741703a8168afdda'
         'dfcbc06a4e8f914341a0853175cb4958'
         'e85edd330937a2395a233e0cb6be0b83')

prepare() {
  cd VTK-$pkgver

  patch -p1 -i ${srcdir}/gdal2.patch
}

build() {
  mkdir build
  cd build

  #export JAVA_HOME=/usr/lib/jvm/default

  # use system libs
  local cmake_system_flags=""
  for lib in EXPAT FREETYPE JPEG PNG TIFF ZLIB OGGTHEORA SIX JSONCPP GLEW GL2PS; do
    cmake_system_flags+="-DVTK_USE_SYSTEM_${lib}:BOOL=ON "
  done

  cmake \
    -Wno-dev \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DBUILD_DOCUMENTATION:BOOL=OFF \
    -DDOCUMENTATION_HTML_HELP:BOOL=OFF \
    -DDOCUMENTATION_HTML_TARZ:BOOL=OFF \
    -DBUILD_EXAMPLES:BOOL=ON \
    -DVTK_USE_FFMPEG_ENCODER:BOOL=ON \
    -DVTK_BUILD_ALL_MODULES:BOOL=ON \
    -DVTK_USE_LARGE_DATA:BOOL=ON \
    -DVTK_QT_VERSION:STRING="5" \
    -DVTK_WRAP_JAVA:BOOL=ON \
    -DVTK_WRAP_PYTHON:BOOL=ON \
    -DVTK_WRAP_TCL:BOOL=ON \
    -DCMAKE_CXX_FLAGS="-D__STDC_CONSTANT_MACROS" \
    -DVTK_CUSTOM_LIBRARY_SUFFIX="" \
    -DVTK_INSTALL_INCLUDE_DIR:PATH=include/vtk \
    ${cmake_system_flags} \
    -DCMAKE_BUILD_TYPE=Release \
    "${srcdir}/VTK-$pkgver"

  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install

  install -dv "${pkgdir}/usr/share/java/vtk"
  mv -v "${pkgdir}/usr/lib/vtk.jar" "${pkgdir}/usr/share/java/vtk"
  #rm -rf "${pkgdir}/usr/lib/vtk-${_pkgver}/java"

  install -dv "${pkgdir}/usr/share/licenses/vtk"
  install -m644 "${srcdir}/VTK-$pkgver/Copyright.txt" "${pkgdir}/usr/share/licenses/vtk"

  install -dv "${pkgdir}/usr/lib/qt5"
  mv "$pkgdir"/usr/plugins "$pkgdir"/usr/lib/qt5/plugins
}

