# Support packages for Desktop

pkgname=qt-mobility
pkgver=1.2.0
pkgrel=2
pkgdesc="Qt APIs for mobile device functionality"
arch=('x86_64' 'i686')
url="http://qt.nokia.com"
license=('LGPL')
depends=('qt>=4.8' 'bluez')
makedepends=('perl')
optdepends=('networkmanager: Bearer Management'
            'tracker: Document Gallery'
            'gstreamer0.10-plugins>=0.10.19: Multimedia mediaservice plugins')
source=(http://get.qt.nokia.com/qt/add-ons/$pkgname-opensource-src-$pkgver.tar.gz
        mobility.patch)
md5sums=('ea5db5a8d3dd4709c2926dceda646bd8'
         'c36ebfb26a4226d3fcbeb9e0ed442d36')

build() {
	cd "$srcdir/${pkgname}-opensource-src-${pkgver}"
	patch -p1 -i ${srcdir}/mobility.patch

	./configure -prefix /usr \
		-plugindir /usr/lib/qt/plugins \
		-silent

	make
}

package() {
	cd "$srcdir/${pkgname}-opensource-src-${pkgver}"

	make INSTALL_ROOT="$pkgdir/" install
  
	# Install docs
	install -d ${pkgdir}/usr/share/doc/qt
	cp -r doc/qch ${pkgdir}/usr/share/doc/qt/
}
