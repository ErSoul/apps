
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kio-extras
pkgver=${_kdever}
pkgrel=2
pkgdesc='Additional components to increase the functionality of KIO'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kio-extras'
license=('LGPL')
depends=('exiv2' 'kdelibs4support' 'kdnssd' 'kemoticons' 'khtml' 'kinit' 'kitemmodels' 'kpty'
         'libmtp' 'libssh' 'openexr' 'samba')
makedepends=('extra-cmake-modules' 'kdoctools' 'openslp')
conflicts=('kfileaudiopreview' 'kio-mtp')
provides=('kfileaudiopreview' 'kio-mtp' )
replaces=('kfileaudiopreview' 'kio-mtp' )
groups=('plasma5')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'info2html.diff')
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '0d075bf99b0a0154da15b6ce7e5140b8')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/info2html.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
