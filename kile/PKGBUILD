
pkgname=kile
pkgver=2.9.72
_commit=3bf485d21fa721723fce6bc600f86c73c818ce69
pkgrel=1
pkgdesc="User friendly TeX/LaTeX frontend for KDE."
arch=('x86_64')
license=('GPL2')
url="http://kile.sourceforge.net/"
depends=('kconfig' 'kcoreaddons' 'kcrash' 'kdbusaddons' 'kguiaddons' 'ki18n' 'kiconthemes' 'kinit' 'kio' 
         'kparts' 'ktexteditor' 'kwindowsystem' 'kxmlgui' 'texlive-core' 'docbook-xsl' 'okular')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kf5-kile')
replaces=('kf5-kile')
groups=('office')
source=("https://github.com/KDE/kile/archive/${_commit}.zip")
md5sums=('96a98eae1eef33cb63896d73e7466902')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  # conflict with kmymony
  rm ${pkgdir}/usr/share/icons/hicolor/16x16/actions/delete.png
}
