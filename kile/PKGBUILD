
pkgname=kile
pkgver=2.9.91
_commit=6c824b8174b1609187a7981d820cd81b5a532cd5
pkgrel=1
pkgdesc="User friendly TeX/LaTeX frontend for KDE."
arch=('x86_64')
license=('GPL2')
url="http://kile.sourceforge.net/"
depends=('kconfig' 'kcoreaddons' 'kcrash' 'kdbusaddons' 'kguiaddons' 'ki18n' 'kiconthemes' 'kinit' 'kio' 
         'kparts' 'ktexteditor' 'kwindowsystem' 'kxmlgui' 'texlive-core' 'docbook-xsl' 'okular')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('office')
#source=("https://github.com/KDE/kile/archive/${_commit}.zip")
source=("https://sourceforge.net/projects/kile/files/unstable/kile-3.0b1/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('08cb54cddc54c851d98b339f386d9aa20b3d71ff98ef078242fd52f684712d93')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
