
pkgname=keepassxc
pkgver=2.3.4
pkgrel=1
_commit=d4ed4f93255e51ed7a7986e6040f240ac6488670
pkgdesc="Cross-platform community-driven port of the Windows application Keepass Password Safe"
arch=('x86_64')
url="http://www.keepassxc.org/"
license=('GPL2')
depends=('qt5-x11extras' 'libxtst' 'shared-mime-info' 'libsodium' 'argon2')
makedepends=('cmake' 'intltool' 'qt5-tools')
groups=('accessories')
replaces=('keepassx')
provides=('keepassx')
conflicts=('keepassx')
#source=("https://www.keepassx.org/releases/${pkgver}/${pkgname}-${pkgver}.tar.gz")
source=("https://github.com/keepassxreboot/keepassxc/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.xz")
sha256sums=('0c24a78895185b9b6b098f08a15b332c600f80aa570d608940fcc2a38e02780a')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_GUI_TESTS=OFF \
    -DWITH_XC_BROWSER=ON \
    -DWITH_XC_NETWORKING=ON \
    -DWITH_XC_AUTOTYPE=ON \
    -DWITH_XC_SSHAGENT=ON
  make 
}

check() {
    cd build
    # testcsvparser fails
    make test
}

package() {
    cd build
    
    make DESTDIR=${pkgdir} install 
}
