
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdebindings-pykde4
_pkgname=pykde4
pkgver=${_kdever}
pkgrel=2
pkgdesc="KDE bindings for python"
url="http://kde.org/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('pyqt' 'qscintilla' 'kdepim-runtime')
makedepends=('cmake' 'automoc4' 'polkit-qt' 'boost')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz"
        'pyqt4.10.4.patch'
        'pyqt4.11.diff')
md5sums=(`grep ${_pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'd9d0f4acb0955d1d50e45a254cd87b29'
         'fcd00f9085b3f1563fafe233a8279d1a')  
groups=("kde" "kdebindings" "kde-uninstall")
provides=('kdebindings-python' 'pykde4')
replaces=('kdebindings-python')
conflicts=('kdebindings-python')
options=('docs' 'debug')

build() {
	cd "${srcdir}" 
	patch -p0 -i ${srcdir}/pyqt4.10.4.patch
	patch -p1 -i ${srcdir}/pyqt4.11.diff
	
        rm -rf build 
	mkdir build && cd build
	
	cmake ../pykde4-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DPYTHON_EXECUTABLE=/usr/bin/python2 \
		-DPYKDEUIC4_ALTINSTALL=TRUE
	make
}

package() {
	cd "${srcdir}"/build
	make DESTDIR="${pkgdir}" install

	# Use the python2 executable
	find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
