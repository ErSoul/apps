
pkgname=audacity
pkgver=2.3.2
pkgrel=2
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('x86_64')
url="https://web.audacityteam.org/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'lame' 'hicolor-icon-theme' 'ffmpeg'
         'shared-mime-info' 'desktop-file-utils' 'redland' 'jack' 'soundtouch' 'libsoxr'
         'lilv' 'flac' 'libsndfile' 'libsamplerate' 'suil')
makedepends=('cmake' 'zip' 'python2' 'pkgconfig')
options=('!makeflags')
source=("https://sourceforge.net/projects/kaosx/files/sources/audacity/${pkgname}-minsrc-${pkgver}.tar.xz")
sha256sums=('df1f88984a6486094157e061024244b39c4a5875394ec9e5ae404c783cd8fee2')

build() {
  cd ${pkgname}-minsrc-${pkgver}
  #patch -p1 -i ${srcdir}/ffmpeg.patch

  # no system portaudio, see http://portaudio.music.columbia.narkive.com/rju8OHIE/pa-getstreamhostapitype
  #autoreconf -vi
  ./configure --prefix=/usr \
    --enable-unicode \
    --with-ffmpeg=system \
    --with-libsamplerate=system \
    --with-expat=system \
    --with-libsndfile=system \
    --with-libsoxr=system \
    --with-portaudio=local \
    --with-lv2=system \
    --with-lilv=system \
    --with-sord=system \
    --with-suil=system
  make
}

package() {
  cd ${pkgname}-minsrc-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

