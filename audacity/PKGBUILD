
pkgname=audacity
pkgver=2.2.1
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('x86_64')
url="http://web.audacityteam.org/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'lame' 'hicolor-icon-theme' 'ffmpeg'
         'shared-mime-info' 'desktop-file-utils' 'redland' 'jack' 'soundtouch' 'libsoxr'
         'lilv' 'portaudio' 'flac' 'libsndfile' 'libsamplerate')
makedepends=('cmake' 'zip' 'python2' 'pkgconfig')
options=('!makeflags')
source=("https://github.com/audacity/audacity/archive/Audacity-${pkgver}.tar.gz")
md5sums=('0b07c3bf0eb41072704f03b291f08796')

build() {
  cd ${pkgname}-Audacity-${pkgver}
  #patch -p1 -i ${srcdir}/ffmpeg.patch

  autoreconf -vi
  ./configure --prefix=/usr \
    --enable-unicode \
    --with-ffmpeg=system \
    --with-libsamplerate=system \
    --with-expat=system
  make
}

package() {
  cd ${pkgname}-Audacity-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

