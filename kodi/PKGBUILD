
_prefix=/usr

pkgname=kodi
pkgver=16.0
_pkgver=16.0-Jarvis
pkgrel=1
pkgdesc="Open source (GPL) software media player and entertainment hub"
arch=('x86_64')
url="http://kodi.tv/"
license=('GPL' 'custom')
depends=( 'bzip2' 'curl' 'enca' 'faac' 'faad2' 'fontconfig' 'fribidi' 'python2' 'python-pysqlite'
          'glew' 'jasper' 'libcdio' 'libgl' 'libmad' 'libmms' 'libass' 'yajl' 'x264'
          'libmpeg2' 'libmariadbclient' 'libsamplerate' 'libxinerama' 'libpng' 'bluez-qt'
          'libxrandr' 'libxtst' 'lzo2' 'sdl2' 'libvorbis' 'pulseaudio' 'libdvdcss'
          'samba' 'wavpack' 'mesa-demos' 'xorg-utils' 'libmicrohttpd' 'libjpeg-turbo' 'mesa' 'swig'
          'libssh' 'libva' 'libmodplug' 'rtmpdump' 'lsb-release' 'sqlite3' 'libbluray' 'java-runtime'
          'tinyxml' 'taglib' 'libcap' 'glu' 'libusbx' 'hicolor-icon-theme' 'libtiff' 'avahi'
          'gnutls' 'nettle' 'libnfs' 'dcadec' 'crossguid')
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'libvdpau' 'zip' 'unzip' 'autoconf' 'automake' 'doxygen'
             'udisks' 'upower')
optdepends=('unrar: access compressed files without unpacking them'
            'udisks: automount external drives'
            'upower: used to trigger suspend functionality'
            'libvdpau: for nvidia hardware acceleration'
            'lcms'
            'tk')
groups=('multimedia')
install='kodi.install'
options=('makeflags')
conflicts=('xbmc')
provides=('xbmc')
replaces=('xbmc')
#source=("http://mirrors.xbmc.org/releases/source/xbmc-$pkgver.tar.gz"
source=("https://github.com/xbmc/xbmc/archive/${_pkgver}.tar.gz")
md5sums=('ce887b7e831bca8843c5262313f28913')

build() {
    cd xbmc-${_pkgver}
  
    ./bootstrap
    
    ./configure --prefix=${_prefix} --exec-prefix=${_prefix} \
        --disable-debug \
        --enable-optimizations \
        --enable-gl \
        --enable-vaapi \
	--enable-vdpau \
	--enable-pulse \
	--enable-avahi \
	--enable-samba \
	--enable-rtmp \
	--enable-libbluray \
	--enable-dvdcss \
	--enable-rtmp 

    make 
}

package() {
    cd xbmc-${_pkgver}

    make DESTDIR=${pkgdir} install

    # various fixes
    sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/KaOS-release/g" ${pkgdir}${_prefix}/bin/kodi

    # Licenses
    install -d -m 0755 ${pkgdir}${_prefix}/share/licenses/${pkgname}
    for licensef in LICENSE.GPL copying.txt; do
        mv ${pkgdir}${_prefix}/share/doc/kodi/${licensef} ${pkgdir}${_prefix}/share/licenses/${pkgname}
    done
    
    # move from xbmc to kodi leaves conflicting symlinks in place of real files, pacman not handling it
    rm -r ${pkgdir}${_prefix}/share/xbmc
    rm -r ${pkgdir}${_prefix}/lib/xbmc
}
