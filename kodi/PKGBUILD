
pkgname=kodi
pkgver=17.0rc2
_pkgver=17.0rc2-Krypton
pkgrel=1
pkgdesc="Open source (GPL) software media player and entertainment hub"
arch=('x86_64')
url="http://kodi.tv/"
license=('GPL' 'custom')
depends=( 'bzip2' 'curl' 'enca' 'faac' 'faad2' 'fontconfig' 'fribidi' 'python2' 'python-pysqlite'
          'glew' 'libcdio' 'libgl' 'libmad' 'libmms' 'libass' 'yajl' 'x264'
          'libmpeg2' 'libmariadbclient' 'libsamplerate' 'libpng' 'bluez'
          'libxrandr' 'libxtst' 'lzo2' 'sdl2' 'pulseaudio' 'libdvdcss'
          'samba' 'wavpack' 'mesa-demos' 'xorg-utils' 'libmicrohttpd' 'libjpeg-turbo' 'mesa' 'swig'
          'libssh' 'libva' 'libmodplug' 'lsb-release' 'sqlite3' 'libbluray' 'java-runtime'
          'tinyxml' 'taglib' 'libcap' 'glu' 'libusb' 'hicolor-icon-theme' 'libtiff' 'avahi'
          'gnutls' 'nettle' 'libnfs' 'dcadec' 'crossguid' 'python2-pillow' 'python2-simplejson' 'lcms2')
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'libvdpau' 'zip' 'unzip' 'autoconf' 'automake'
             'doxygen' 'upower')
optdepends=('unrar: access compressed files without unpacking them'
            'upower: used to trigger suspend functionality'
            'libvdpau: for nvidia hardware acceleration'
            'tk')
groups=('multimedia')
options=('makeflags')
#source=("http://mirrors.xbmc.org/releases/source/xbmc-$pkgver.tar.gz"
source=("https://github.com/xbmc/xbmc/archive/${_pkgver}.tar.gz")
md5sums=('5a6bf08196aff4297a9ccd3c483e9a33')

build() {
    cd xbmc-${_pkgver}
  
    ./bootstrap
    
    ./configure --prefix=/usr --exec-prefix=/usr \
        --disable-debug \
        --enable-optimizations \
        --enable-gl \
        --enable-vaapi \
        --enable-vdpau \
        --enable-pulse \
        --enable-avahi \
        --enable-samba \
        --enable-libbluray 
    make
    # use when switched to cmake build
    #make -C tools/depends/target/binary-addons PREFIX=/usr
}

package() {
    cd xbmc-${_pkgver}

    make DESTDIR=${pkgdir} install

    # various fixes
    sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/KaOS-release/g" ${pkgdir}/usr/bin/kodi

    # Licenses
    install -d -m 0755 ${pkgdir}/usr/share/licenses/${pkgname}
    for licensef in LICENSE.GPL copying.txt; do
        mv ${pkgdir}/usr/share/doc/kodi/${licensef} ${pkgdir}/usr/share/licenses/${pkgname}
    done
    
    # move from xbmc to kodi leaves conflicting symlinks in place of real files, pacman not handling it
    rm -r ${pkgdir}/usr/share/xbmc
    rm -r ${pkgdir}/usr/lib/xbmc
}
