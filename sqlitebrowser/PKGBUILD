
pkgname=sqlitebrowser
pkgver=3.9.1
pkgrel=1
pkgdesc="High quality, visual, open source tool to create, design, and edit database files compatible with SQLite"
arch=('x86_64')
url="http://sqlitebrowser.org/"
license=('GPL')
depends=('qt5-base' 'sqlite' 'sqlcipher')
makedepends=('cmake' 'qt5-tools')
source=("https://github.com/sqlitebrowser/sqlitebrowser/archive/v${pkgver}.tar.gz")
md5sums=('edf47a4baa8c53e9151032152e3c2991')

prepare() {
  cd ${pkgname}-${pkgver}
  
  sed -e "s|DB Browser for SQLite|sqlitebrowser|" -i CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_QT5=TRUE
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
