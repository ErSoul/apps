
pkgname=google-chrome
pkgver=48.0.2535.0
pkgrel=1
pkgdesc="Google Chrome Browser, developer preview channel for Linux"
arch=('x86_64')
url="http://dev.chromium.org/getting-involved/dev-channel"
license=('custom:chrome')
groups=('network')
options=('!strip' '!emptydirs')
install=$pkgname.install
depends=('gcc-libs' 'gtk2' 'nss' 'gconf' 'libjpeg-turbo' 'freetype2' 'cairo' 'libxslt'
         'libpng' 'alsa-lib' 'libxss' 'hicolor-icon-theme' 'xdg-utils')
source=("google-chrome-unstable_${pkgver}_amd64.deb::https://dl.google.com/linux/direct/google-chrome-unstable_current_amd64.deb"
         "https://www.google.com/chrome/intl/en/eula_text.html"
         'google-chrome-unstable.desktop')
md5sums=('1a4ffc277cf0e48487aac482bddeb99a'
         'da54da9383ddffa44c5db45513239da2'
         'dc490bcda18c1379056700057800cc8f')


package() {
    msg "Extracting Google Chrome"
    bsdtar -xf data.tar.xz -C "$pkgdir/" 
    msg2 "Done extracting!"

    # Adding man page
    gzip "$pkgdir"/usr/share/man/man1/google-chrome-unstable.1
    
    # Symlinking icons to /usr/share/icons/hicolor/
    for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 "$pkgdir"/opt/google/chrome-unstable/product_logo_${i}.png "$pkgdir"/usr/share/icons/hicolor/${i}x${i}/apps/google-chrome.png
    done

    # License
    install -Dm644 "$srcdir"/eula_text.html "$pkgdir"/usr/share/licenses/google-chrome/eula_text.html
    
    msg2 "Removing cron job and duplicated images"
    rm -r "$pkgdir"/etc/cron.daily/ "$pkgdir"/opt/google/chrome-unstable/cron/
    rm "$pkgdir"/opt/google/chrome-unstable/product_logo_*.png
    
    rm "$pkgdir"/usr/share/applications/google-chrome-unstable.desktop
    install -m755 "$srcdir"/google-chrome-unstable.desktop "$pkgdir"/usr/share/applications/google-chrome-unstable.desktop

    msg2 "Installation finished!"
}
