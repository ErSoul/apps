
pkgbase=google-chrome
pkgname=('google-chrome' 'pepper-flash')
pkgver=54.0.2816.0
pkgrel=1
arch=('x86_64')
url="http://dev.chromium.org/getting-involved/dev-channel"
license=('custom:chrome')
options=('!strip' '!emptydirs')
makedepends=('gcc-libs' 'gtk2' 'nss' 'gconf' 'libjpeg-turbo' 'freetype2' 'cairo' 'libxslt'
            'libpng' 'alsa-lib' 'libxss' 'hicolor-icon-theme' 'xdg-utils')
# version:  curl -s https://dl.google.com/linux/chrome/rpm/stable/x86_64/repodata/other.xml.gz | gzip -df | awk -F\" '/pkgid/{ sub(".*-","",$4); print $4": "$10 }'
source=("google-chrome-unstable_${pkgver}_amd64.deb::https://dl.google.com/linux/direct/google-chrome-unstable_current_amd64.deb"
         "https://www.google.com/chrome/intl/en/eula_text.html"
         'google-chrome-unstable.desktop')
md5sums=('73a6fb6f165f47630a5533a19d16746e'
         'da54da9383ddffa44c5db45513239da2'
         'dc490bcda18c1379056700057800cc8f')


package_google-chrome() {
    pkgdesc="Google Chrome Browser, developer preview channel for Linux"
    depends=('gcc-libs' 'gtk2' 'nss' 'gconf' 'libjpeg-turbo' 'freetype2' 'cairo' 'libxslt'
             'libpng' 'alsa-lib' 'libxss' 'hicolor-icon-theme' 'xdg-utils' 'pepper-flash')
    groups=('network')
    install=$pkgname.install
    
    bsdtar -xf data.tar.xz -C ${pkgdir}/ 

    # Adding man page
    gzip ${pkgdir}/usr/share/man/man1/google-chrome-unstable.1
    
    # icons to /usr/share/icons/hicolor/ too
    for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 ${pkgdir}/opt/google/chrome-unstable/product_logo_${i}.png ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/google-chrome.png
    done

    # License
    install -Dm644 $srcdir/eula_text.html ${pkgdir}/usr/share/licenses/google-chrome/eula_text.html
    
    rm -r ${pkgdir}/etc/cron.daily/ ${pkgdir}/opt/google/chrome-unstable/cron/
    rm ${pkgdir}/opt/google/chrome-unstable/product_logo_*.png
    # use in pepper-flash package
    rm ${pkgdir}/opt/google/chrome-unstable/PepperFlash/manifest.json
    rm ${pkgdir}/opt/google/chrome-unstable/PepperFlash/libpepflashplayer.so
    
    rm ${pkgdir}/usr/share/applications/google-chrome-unstable.desktop
    install -m755 $srcdir/google-chrome-unstable.desktop ${pkgdir}/usr/share/applications/google-chrome-unstable.desktop
    # symlink no longer provided
    ln -s /opt/google/chrome-unstable/google-chrome-unstable ${pkgdir}/usr/bin/google-chrome-unstable
}

package_pepper-flash() {
    pkgdesc="Google Chrome's Pepper Flash plugin"
    depends=('pulseaudio')
    optdepends=('freshplayerplugin: required for flash support in Firefox')
    provides=('flashplugin')
    replaces=('flashplugin')
    conflicts=('flashplugin')
    install=$pkgname.install
    
    bsdtar -xf data.tar.xz
    
    install -Dm644 opt/google/chrome-unstable/PepperFlash/manifest.json ${pkgdir}/opt/google/chrome-unstable/PepperFlash/manifest.json
    install -Dm755 opt/google/chrome-unstable/PepperFlash/libpepflashplayer.so ${pkgdir}/opt/google/chrome-unstable/PepperFlash/libpepflashplayer.so
    
    mkdir -p ${pkgdir}/usr/lib/PepperFlash
    cd ${pkgdir}
    ln -s /opt/google/chrome-unstable/PepperFlash/manifest.json ${pkgdir}/usr/lib/PepperFlash/manifest.json
    ln -s /opt/google/chrome-unstable/PepperFlash/libpepflashplayer.so ${pkgdir}/usr/lib/PepperFlash/libpepflashplayer.so
    
    install -Dm644 $srcdir/eula_text.html ${pkgdir}/usr/share/licenses/pepper-flash/eula_text.html
}
