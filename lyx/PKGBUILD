
pkgname=lyx
pkgver=2.2.0
_pkgver=2.2.0
pkgrel=1
pkgdesc="An advanced open-source document processor."
arch=('x86_64')
url="http://www.lyx.org"
depends=('qt5-base' 'qt5-svg' 'qt5-x11extras' 'texlive-core' 'python2' 'perl' 'imagemagick' # 'enchant' 
         'boost-libs' 'icu' 'libmythes' 'libxtst' 'hunspell' 'aspell')
makedepends=('boost' 'bc' 'cmake' 'qt5-tools')
groups=('office')
license=('GPL')
source=("ftp://ftp.lyx.org/pub/lyx/stable/2.2.x/$pkgname-$pkgver.tar.xz" 
#source=("ftp://ftp.lyx.org/pub/lyx/devel/lyx-2.2/lyx-${_pkgver}/lyx-${_pkgver}.tar.xz"
        'lyx.desktop')
md5sums=('cb8032fb9f850d3e3e2f8af81e04656f'
         '0ea910b9b881ebaa0f79ebaa618930fd')
         
prepare() {
  cd ${pkgname}-${_pkgver}
  
  #sed -i 's|set(HUNSPELL_VERSION 1.3.3)|set(HUNSPELL_VERSION 1.4.1)|' 3rdparty/hunspell/CMakeLists.txt
}
         
build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_pkgver} \
    -DLYX_RELEASE=ON \
    -DLYX_INSTALL_PREFIX=/usr \
    -DLYX_DATA_SUBDIR=share/lyx2.2/ \
    -DLYX_MAN_DIR=/usr/share/man/man1 \
    -DLYX_USE_QT=QT5 \
    -DLYX_ASPELL=ON \
    -DLYX_ENCHANT=OFF \
    -DLYX_HUNSPELL=OFF \
    -DLYX_EXTERNAL_BOOST=ON \
    -DLYX_INSTALL=ON
    
  make
  make translations
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  mv ${pkgdir}/usr/share/lyx2.2/locale ${pkgdir}/usr/share/locale
  mv ${pkgdir}/usr/applications ${pkgdir}/usr/share/applications
  mv ${pkgdir}/usr/icons ${pkgdir}/usr/share/icons
}

