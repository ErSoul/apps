
_prefix=/usr

pkgname=kodi
pkgver=14.0a4
_pkgver=14.0a4-Helix
pkgrel=1
pkgdesc="Open source (GPL) software media player and entertainment hub"
arch=('x86_64')
url="http://kodi.tv/"
license=('GPL' 'custom')
depends=( 'bzip2' 'curl' 'enca' 'faac' 'faad2' 'fontconfig' 'fribidi' 'python2' 'python-pysqlite'
          'glew' 'jasper' 'libcdio' 'libgl' 'libmad' 'libmms' 'libass' 'yajl' 'libpng12' 'x264'
          'libmpeg2' 'libmariadbclient' 'libsamplerate' 'libxinerama' 'libpng' 'libbluedevil'
          'libxrandr' 'libxtst' 'lzo2' 'sdl_image' 'sdl_mixer' 'libvorbis' 'pulseaudio' 'libdvdcss'
          'samba' 'wavpack' 'mesa-demos' 'xorg-utils' 'libmicrohttpd' 'libjpeg-turbo' 'mesa' 'swig'
          'libssh' 'libva' 'libmodplug' 'rtmpdump' 'lsb-release' 'sqlite3' 'libbluray' 'java-runtime'
          'tinyxml' 'taglib' 'libcap' 'glu' 'libusbx' 'hicolor-icon-theme' 'libtiff' 'avahi')
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'libvdpau' 'zip' 'unzip' 'autoconf' 'automake' 'doxygen'
             'udisks' 'upower')
optdepends=('unrar: access compressed files without unpacking them'
            'udisks: automount external drives'
            'upower: used to trigger suspend functionality'
            'libvdpau: for nvidia hardware acceleration'
            'lcms'
            'tk')
groups=('multimedia')
install='kodi.install'
options=('makeflags')
conflicts=('xbmc')
provides=('xbmc')
replaces=('xbmc')
#source=("http://mirrors.xbmc.org/releases/source/xbmc-$pkgver.tar.gz"
source=("https://github.com/xbmc/xbmc/archive/$_pkgver.tar.gz")
md5sums=('7b93dafe8ef385fb65c8e12e962745a7')

build() {
    cd "${srcdir}/xbmc-$_pkgver"
    sed -i -e 's:/usr/bin/lsb_release -d:cat /etc/KaOS-release:' xbmc/utils/SystemInfo.cpp
  
    msg "Bootstrapping XBMC"
    ./bootstrap
    
    export PYTHON_VERSION=2 
    msg "Configuring Kodi"
    ./configure --prefix=${_prefix} --exec-prefix=$_prefix \
        --disable-debug \
        --enable-optimizations \
        --enable-gl \
        --enable-sdl \
        --enable-vaapi \
	--enable-vdpau \
	--enable-xrandr \
	--enable-x11 \
	--enable-udev \
	--enable-pulse \
	--enable-avahi \
	--enable-samba \
	--enable-rtmp \
	--enable-libbluray \
	--enable-dvdcss \
	--enable-libusb \
	--enable-rtmp \
	--disable-texturepacker 

    msg "Running make"
    make 
}

package() {
    cd "${srcdir}/xbmc-$_pkgver"
    msg "Running make install"
    make DESTDIR="${pkgdir}" install

    # various fixes
    sed -i -e 's/which lsb_release &> \/dev\/null/\[ -f \/etc\/KaOS-release ]/g' "${pkgdir}${_prefix}/bin/xbmc"
    sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/KaOS-release/g" "${pkgdir}${_prefix}/bin/xbmc"
    sed -i -e 's/python/python2/g' ${pkgdir}${_prefix}/bin/xbmc

    # Tools
    install -D -m 0755 "${srcdir}/xbmc-$_pkgver/xbmc-xrandr" "${pkgdir}${_prefix}/share/xbmc/xbmc-xrandr"
    #install -D -m 0755 "${srcdir}/xbmc-$pkgver/tools/TexturePacker/TexturePacker" "${pkgdir}${_prefix}/share/xbmc/"

    # Licenses
    install -d -m 0755 "${pkgdir}${_prefix}/share/licenses/${pkgname}"
    for licensef in LICENSE.GPL copying.txt; do
        mv "${pkgdir}${_prefix}/share/doc/xbmc/${licensef}" "${pkgdir}${_prefix}/share/licenses/${pkgname}"
    done
}
