
# build version at https://dolphin-emu.org/download/
pkgname=dolphin-emu
pkgver=5.0.8993
pkgrel=1
_commit=710b893b914bcc1e812f1a63fdb9a143ff682561
pkgdesc='GameCube, Wii and Triforce emulator'
arch=('x86_64')
url='http://dolphin-emu.org'
license=('GPL2')
depends=('bluez' 'ffmpeg' 'libao' 'miniupnpc' 'openal' 'portaudio'
         'libevdev' 'soundtouch' 'qt5-base' 'pulseaudio')
makedepends=('cmake')
source=("https://github.com/dolphin-emu/dolphin/archive/${_commit}.zip")
#source=("https://github.com/dolphin-emu/dolphin/archive/${pkgver}.tar.gz")
md5sums=('82159ebf4683ff93ffafaea4a332458c')
         
build() {
  mkdir -p build 
  cd build
  
  cmake ../dolphin-${_commit}\
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT2=ON \
    -DENABLE_WX=OFF \
    -DUSE_EGL=ON
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
  
  install -D -m644 ../dolphin-${_commit}/Data/51-usb-device.rules -t ${pkgdir}/usr/lib/udev/rules.d/
}


