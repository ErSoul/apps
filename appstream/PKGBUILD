 
pkgname=appstream
_pkgname=AppStream
pkgver=0.11.5
pkgrel=1
pkgdesc="Provides a standard for creating app stores across distributions"
arch=('x86_64')
url="https://github.com/ximion/appstream"
license=('GPL')
depends=('libyaml' 'libxml2' 'glib2' 'qt5-base')
makedepends=('meson' 'ninja' 'gobject-introspection' 'itstool' 'python2' 'xmlto' 'gtk-doc')
source=("https://www.freedesktop.org/software/appstream/releases/${_pkgname}-${pkgver}.tar.xz"
        'appstream.hook')
md5sums=('e4a2d1e89d676846017dc7aabb1b9a4f'
         'f244ccc7b89aacfe903facce393174ca')

build() {
  mkdir -p build
  cd build

   meson ../${_pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Denable-qt=true \
    -Denable-stemming=false \
    -Denable-docs=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../appstream.hook ${pkgdir}/usr/share/libalpm/hooks/appstream.hook
}
