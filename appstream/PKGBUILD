 
pkgname=appstream
_pkgname=AppStream
pkgver=0.12.8
pkgrel=1
pkgdesc="Provides a standard for creating app stores across distributions"
arch=('x86_64')
url="https://github.com/ximion/appstream"
license=('GPL')
depends=('libyaml' 'libxml2' 'glib2' 'qt5-base' 'lmdb')
makedepends=('meson' 'ninja' 'gobject-introspection' 'itstool' 'xmlto' 'gtk-doc' 'gperf' 'qt5-tools')
source=("https://www.freedesktop.org/software/appstream/releases/${_pkgname}-${pkgver}.tar.xz"
        'appstream.hook')
md5sums=('e972ea4b2046fc764f5f2613f33bcdec'
         'f244ccc7b89aacfe903facce393174ca')

build() {
  mkdir -p build
  cd build

   meson ../${_pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dqt=true \
    -Dstemming=false \
    -Ddocs=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../appstream.hook ${pkgdir}/usr/share/libalpm/hooks/appstream.hook
}
