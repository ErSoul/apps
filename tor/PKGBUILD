
pkgname=tor
pkgver=0.2.8.12
pkgrel=1
pkgdesc='Open network that helps you defend against traffic analysis, a form of network surveillance that threatens personal freedom and privacy.'
arch=('x86_64')
url='http://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'bash' 'libseccomp')
makedepends=('ca-certificates')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist')
install='tor.install'
source=("https://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"
        'torrc'
        'tor.service')
md5sums=('476f5074c2f77350010dcb52f6f8651d'
         'a63a553384d825a05ca052fdfadc7d08'
         '64114b508cbe987ade03e8814540a9d4')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -dm0700 -o 43 -g 43 ${pkgdir}/var/lib/tor

  mv ${pkgdir}/etc/tor/torrc.sample ${pkgdir}/etc/tor/torrc-dist
  install -Dm0644 ${srcdir}/torrc ${pkgdir}/etc/tor/torrc
  install -Dm0644 ${srcdir}/tor.service ${pkgdir}/usr/lib/systemd/system/tor.service

  install -Dm0644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
