
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kauth
pkgver=${_kf5ver}
pkgrel=1
pkgdesc='KAuth provides a convenient, system-integrated way to offload actions that need to be performed as a privileged user.'
arch=('x86_64')
url='https://github.com/KDE/kauth'
license=('LGPL')
depends=('kcoreaddons' 'polkit-qt5')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'deadlock.patch')
md5sums=(`grep ${pkgname}-$_kf5ver.tar.xz ../kf5.md5 | cut -d" " -f1`
         '150cefa733d5f51d174b91c6183a638d')

prepare() {
  cd $pkgname-${pkgver}
  
  patch -p1 -i $srcdir/deadlock.patch
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR="${pkgdir}" install
}
