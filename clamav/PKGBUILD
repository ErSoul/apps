
pkgname=clamav
pkgver=0.98
pkgrel=1
pkgdesc='Anti-virus toolkit for Unix'
arch=('x86_64')
url='http://www.clamav.net/'
license=('GPL')
depends=('bzip2' 'zlib' 'libltdl')
options=('!libtool')
install="$pkgname.install"
groups=('system')
backup=('etc/logrotate.d/clamav')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"  
        'config.patch'
        'logrotate'
        'service'
        'service.fresh'
        'tmpfiles.d')

md5sums=('ca0b8c930efcb8be1d47592d268006c5'
         'd435081ffe6d815175e1aa57b09793c9'
         '8386e24561c5ce4cb87fa1d9f3b1cd87'
         '7d13bb47a3e8e5510c8105484acecadf'
         '9ea7c696979f3977120791bdb355acf9'
         '89ed33d37ca79d45b8c02418df2486e6')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i ../config.patch
	
	./configure --prefix=/usr \
	       --sysconfdir=/etc/clamav \
	       --with-dbdir=/var/lib/clamav \
	       --disable-clamav
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -D -m644 ../service.fresh "${pkgdir}/usr/lib/systemd/system/freshclamd.service"
	install -D -m644 ../service "${pkgdir}/usr/lib/systemd/system/clamd.service"
	install -D -m644 ../tmpfiles.d "${pkgdir}/usr/lib/tmpfiles.d/clamav.conf"
	install -D -m644 ../logrotate "$pkgdir/etc/logrotate.d/clamav"
	install -d -o 64 -g 64 "${pkgdir}"/run/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/log/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/lib/clamav
}
