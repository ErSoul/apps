
pkgbase=transmission
pkgname=('transmission-cli' 'transmission-qt')
pkgver=2.90
_pkgver=2.84+
pkgrel=1
arch=('x86_64')
url="http://www.transmissionbt.com/"
license=('GNU GPLv2')
makedepends=('intltool' 'curl' 'pkgconfig' 'desktop-file-utils' 'qt5-declarative' 'libevent'
             'gettext' 'cmake' 'qt5-tools' 'miniupnpc')
source=("https://download.transmissionbt.com/files/${pkgbase}-${pkgver}.tar.xz"
#source=("https://build.transmissionbt.com/job/trunk-linux/lastBuild/artifact/transmission-trunk-r14679.tar.xz"
        'transmission.systemd'
        'transmission.tmpfiles')
md5sums=('0792c67fcbf722f7823e964e09eb16bc'
         '5c289c8901221a94be74665368ab5c2c'
         '23f2320361ad54373c3a4551ef634fe8')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgbase}-${pkgver} \
        -DENABLE_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_GTK=OFF \
        -DENABLE_QT=ON \
        -DENABLE_CLI=ON \
        -DUSE_QT5=ON \
        -DINSTALL_LIB=OFF \
        -DWITH_CRYPTO="openssl" \
        -DWITH_INOTIFY=ON
     
      make
}

package_transmission-cli() {
  pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
  depends=('curl' 'libevent')
  groups=('network')

  cd build

  for dir in daemon cli utils
  do
      make -C "${dir}" DESTDIR="${pkgdir}" install
  done

  install -D -m644 "$srcdir/transmission.systemd" "$pkgdir/usr/lib/systemd/system/transmission.service"
  install -D -m644 "$srcdir/transmission.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/transmission.conf"
  install -D -m644 "$srcdir/${pkgbase}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/transmission-cli/COPYING"
}

package_transmission-qt() {
  pkgdesc="Fast, easy, and free BitTorrent client (Qt GUI)"
  depends=('curl' 'qt5-declarative' 'libevent' 'miniupnpc')
  optdepends=('transmission-cli: daemon support')
  groups=('network')
  install=transmission-qt.install

  cd build
  make -C qt DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/${pkgbase}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/transmission-qt/COPYING"
}
