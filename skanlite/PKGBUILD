
pkgname=skanlite
pkgver=2.1.0
_pkgver=2.1
pkgrel=1
pkgdesc="Image Scanning Application for KDE"
arch=('x86_64')
url='https://www.kde.org/applications/graphics/skanlite'
license=('LGPL')
depends=('kio' 'libksane' 'sane')
makedepends=('pkgconfig' 'extra-cmake-modules' 'kdoctools')
conflicts=('kf5-skanlite')
replaces=('kf5-skanlite')
groups=('graphics')
source=("https://download.kde.org/stable/skanlite/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e625db6eed8e2ca9713936ae8175fb96d30d88c0986528ee8845d198624e41bd')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
