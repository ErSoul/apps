
pkgname=libtorrent-rasterbar
_pkgname=libtorrent
pkgver=1.0.8
_pkgver=1_0_8
pkgrel=1
pkgdesc="A C++ library that aims to be a good alternative to all the other bittorrent implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('custom')
depends=('boost-libs' 'python2' 'geoip')
makedepends=('boost' 'cmake')
source=("https://github.com/arvidn/libtorrent/archive/${_pkgname}-${_pkgver}.tar.gz")
md5sums=('eaefb0ee6aca31eb3c50faef28a7f8ea')

build() {
  mkdir -p build
  cd build
  
  cmake ../${_pkgname}-${_pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Dgeoip=ON
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -D ../${_pkgname}-${_pkgname}-${_pkgver}/COPYING ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
  
