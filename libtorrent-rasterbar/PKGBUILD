
pkgname=libtorrent-rasterbar
pkgver=1.1.5
_pkgver=1_1_5
pkgrel=2
pkgdesc="A C++ library that aims to be a good alternative to all the other bittorrent implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('custom')
depends=('boost-libs' 'python3' 'geoip')
makedepends=('boost')
source=("https://github.com/arvidn/libtorrent/releases/download/libtorrent-${_pkgver}/${pkgname}-${pkgver}.tar.gz"
        'boost1.66.patch')
md5sums=('f0e26796a79c51ae8e49ab652ce00166'
         '122005c9eb11c09fac1938216aafad74')

build() {
  cd ${pkgname}-${pkgver}
  # boost 1.66 https://github.com/arvidn/libtorrent/commit/d1a63cd781e79e1e3792a258c0bc7f9455f2272f
  patch -p1 -i ${srcdir}/boost1.66.patch
  
  CXXFLAGS="-std=c++11" \
  PYTHON=/usr/bin/python3 \
  PYTHON_VERSION=3.6 \
  ./configure --prefix=/usr \
    --enable-python-binding \
    --with-boost-python="boost_python3"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -D COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
