
pkgname=krusader
pkgver=2.4.0beta3
_pkgver=2.4.0-beta3
pkgrel=2
pkgdesc="An alternative file manager for KDE resembling Midnight Commander"
url="http://krusader.sourceforge.net/"
arch=('x86_64')
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
groups=('system')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.bz2"
        'kde_bug309119.diff'
        'kde_bug325801.diff')
md5sums=('62a02db3f213bb76805ca6c958e9e263'
         '5e9952f6ac38e5ce323eb95ca6f20db6'
         '55fd27e4888e15e8b3bd8ab08b85aa9e')

prepare() {
  cd $srcdir/$pkgname-$_pkgver

  patch -p1 -i "$srcdir/kde_bug309119.diff" 
  patch -p1 -i "$srcdir/kde_bug325801.diff" 
}

build() {
  cd ${srcdir}
  mkdir build
  
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make 
}

package() {
  cd ${srcdir}/build
  
  make DESTDIR=${pkgdir} install 
}
