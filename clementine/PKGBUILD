
pkgname=clementine
_pkgname=Clementine
pkgver=1.2.3.86
pkgrel=1
_commit=78804b1
pkgdesc="Modern music player and library organiser that takes advantage of Qt4."
url="http://www.clementine-player.org/"
license="GPL"
arch=('x86_64')
install="${pkgname}.install"
depends=('gstreamer' 'taglib' 'glew' 'liblastfm' 'libgpod' 'libmtp' 'libplist'
         'hicolor-icon-theme' 'qtwebkit' 'libimobiledevice' 'qjson' 'protobuf'
         'libcdio-paranoia' 'qca' 'qca-ossl' 'chromaprint' 'sparsehash' 'libmygpo-qt'
         'gst-plugins-bad' 'gst-plugins-good' 'gst-plugins-ugly' 'pulseaudio')
makedepends=('cmake' 'boost' 'mesa')
groups=('multimedia')
#source=("https://github.com/clementine-player/${_pkgname}/archive/${pkgver}.tar.gz")
source=("https://github.com/clementine-player/Clementine/tarball/master/${_pkgname}-${pkgver}.tar.gz"
        'pipeline.patch'
        'playbin.patch')
md5sums=('b3edb4fa1cb7db184b7d2a493f18a58d'
         'e6d9f2c081f20c2cc570d11cd7b4a940'
         '49c7be1a2d0fdaacf222205d24e5d234')

build() {
  cd "${srcdir}/clementine-player-${_pkgname}-${_commit}"
  # https://github.com/clementine-player/Clementine/issues/4293
  patch -p1 -i $srcdir/pipeline.patch
  patch -p1 -i $srcdir/playbin.patch
  
  mkdir -p build 
  
  cd build
  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr \
            -DCMAKE_BUILD_TYPE=Release 
  make
}

package() {
  cd "${srcdir}/clementine-player-${_pkgname}-${_commit}/build"
  
  make install DESTDIR="${pkgdir}"
}

