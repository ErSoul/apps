
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdialog
pkgver=${_kdever}
pkgrel=2
pkgdesc="KDialog can be used to show nice dialog boxes from shell scripts"
arch=('x86_64')
url='https://github.com/KDE/kdialog'
license=('LGPL')
depends=('kio' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kde-baseapps')
replaces=('kde-baseapps')
provides=('kde-baseapps')
install=kdialog.install
source=("$_mirror/${pkgname}-${_kdever}.tar.xz"
        "https://github.com/KDE/kdialog/commit/da7c7c8218fc55af7706e649449684bc3d2782fb.diff"
        "https://github.com/KDE/kdialog/commit/d72ab46df8879024cda620beec9da71a674873dc.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'd256c030978485f3055799e2300f9a12'
         '72f311cd11d44c0334bdfbf7feb4af69')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/da7c7c8218fc55af7706e649449684bc3d2782fb.diff
  patch -p1 -i ${srcdir}/d72ab46df8879024cda620beec9da71a674873dc.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
