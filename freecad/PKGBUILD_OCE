
pkgname=freecad
pkgver=0.15.4671
pkgrel=4
pkgdesc='A general purpose 3D CAD modeler'
arch=('x86_64')
url='http://www.freecadweb.org/'
license=('GPL')
depends=('boost-libs' 'curl' 'oce' 'pivy-hg' 'pyqt-python2' 'xerces-c' 'libspnav'
         'shared-mime-info' 'hicolor-icon-theme' 'python2-matplotlib' 'shiboken' 
         'python2-pyside-tools' 'qtwebkit')
makedepends=('boost' 'eigen3' 'gcc-fortran' 'swig' 'desktop-file-utils' 'cmake' 'coin')
options=('!libtool' '!makeflags')
install=freecad.install
source=("http://downloads.sourceforge.net/sourceforge/free-cad/${pkgname}_${pkgver}.tar.gz"
        'freecad.desktop'
        'freecad.xml')
md5sums=('7afa95d3e8cd845bef83202e76db7f24'
         '0a4d0635dbd97d9f594ac8e927284316'
         'c2f4154c8e4678825411de8e7fa54c6b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}/"
  
    sed -i -e "s|ModDir = os.path.realpath(ModDir)|ModDir = '/usr/share/freecad/Mod'|" src/App/FreeCADInit.py
    sed -i -e "s|LibDir = os.path.realpath(LibDir)|LibDir = '/usr/lib/freecad'|" src/App/FreeCADInit.py
}
         
build() {
  #mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_INSTALL_DOCDIR:PATH=share/freecad/doc \
    -DCMAKE_INSTALL_DATADIR:PATH=share/freecad \
    -DCMAKE_INSTALL_LIBDIR:PATH=lib/freecad \
    -DCMAKE_BUILD_TYPE=Release \
    -DFREECAD_USE_EXTERNAL_PIVY:BOOL=ON
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install

  mkdir -p "${pkgdir}/usr/bin/"
  ln -s "FreeCAD" "${pkgdir}/usr/bin/freecad"
  ln -s "FreeCADCmd" "${pkgdir}/usr/bin/freecadcmd"
  
  #cp -ra "${pkgdir}/usr/Mod" "${pkgdir}/usr/share/freecad/"
  #rm -r "${pkgdir}/usr/Mod"

  # Install desktop file
  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/${pkgname}.desktop"

   install -D -m644 "${srcdir}/${pkgname}.xml" "${pkgdir}/usr/share/mime/packages/${pkgname}.xml"
}

