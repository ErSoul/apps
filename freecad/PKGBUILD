
pkgname=freecad
pkgver=0.13.1830
pkgrel=1
pkgdesc='A general purpose 3D CAD modeler'
arch=('x86_64')
url='http://www.freecadweb.org/'
license=('GPL')
depends=('boost-libs' 'curl' 'opencascade>=6.6.0' 'pivy-hg' 'pyqt' 'xerces-c' 'libspnav'
         'shared-mime-info' 'hicolor-icon-theme' 'python2-matplotlib')
makedepends=('boost' 'eigen3' 'gcc-fortran' 'swig' 'xerces-c' 'desktop-file-utils'
             'cmake' 'coin')
options=('!libtool' '!makeflags')
install=freecad.install
source=("http://downloads.sourceforge.net/sourceforge/free-cad/$pkgname-${pkgver}.tar.gz"
        'freecad-0.13.diff'
        '${pkgname}.desktop'
        '${pkgname}.xml')
md5sums=('13b8fddef12f5f8419dcf4adbab596eb'
         '493490db7844d7d5b9426fab8da5acea'
         '0a4d0635dbd97d9f594ac8e927284316'
         'c2f4154c8e4678825411de8e7fa54c6b')

build() {
  cd "${srcdir}/$pkgname-${pkgver}/"

  # compatibility issues with OCC-6.6
  for _FILE in $( grep -Rl "BRepTools::OuterShell" * )
  do
    sed -e "s|BRepTools|BRepClass3d|g" -i "$_FILE"
  done

  patch -p1 -i ${srcdir}/freecad-0.13.diff

  mkdir -p build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX:PATH=/opt/$pkgname .. \
  	-DOCC_INCLUDE_DIR:PATH=/opt/opencascade/inc/ \
	-DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2 \
	-DFREECAD_USE_EXTERNAL_PIVY:BOOL=ON
  make

  # Builds Qt plugin
  cd ../src/Tools/plugins/widget/
  qmake plugin.pro
  make
}

package() {
  cd "${srcdir}/$pkgname-${pkgver}/build"

  # Install main program
  make DESTDIR="${pkgdir}" install

  # Symlink to /usr/bin
  mkdir -p "${pkgdir}/usr/bin/"
  ln -sf "/opt/$pkgname/bin/FreeCAD" "${pkgdir}/usr/bin/freecad"
  ln -sf "/opt/$pkgname/bin/FreeCADCmd" "${pkgdir}/usr/bin/freecadcmd"

  # Installs Qt plugin
  install -Dm755 ../src/Tools/plugins/widget/libFreeCAD_widgets.so "${pkgdir}/usr/lib/qt/plugins/designer/libFreeCAD_widgets.so"

  # Install pixmaps and desktop shortcut
  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/${pkgname}.desktop"

   # Mime info
   install -D -m644 "${srcdir}/${pkgname}.xml" "${pkgdir}/usr/share/mime/packages/${pkgname}.xml"
}

