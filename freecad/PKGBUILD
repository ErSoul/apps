
pkgname=freecad
_pkgname=FreeCAD
pkgver=0.16
pkgrel=1
pkgdesc='A general purpose 3D CAD modeler'
arch=('x86_64')
url=''
license=('GPL')
depends=('boost-libs' 'curl' 'opencascade' 'pivy-hg' 'pyqt-python2' 'xerces-c' 'libspnav'
         'shared-mime-info' 'hicolor-icon-theme' 'python2-matplotlib' 'shiboken' 
         'python2-pyside-tools' 'qtwebkit')
makedepends=('boost' 'eigen3' 'gcc-fortran' 'swig' 'desktop-file-utils' 'cmake' 'coin')
options=('!libtool' '!makeflags')
install=freecad.install
source=("https://github.com/FreeCAD/FreeCAD/archive/${pkgver}.tar.gz"
        'freecad.desktop'
        'freecad.xml')
md5sums=('6e992c98ae2b8448bcf428e3e9efb02f'
         '0a4d0635dbd97d9f594ac8e927284316'
         'c2f4154c8e4678825411de8e7fa54c6b')

         
build() {
  cd ${_pkgname}-${pkgver}/
  # boost 1.60.0 fix, part of https://github.com/FreeCAD/FreeCAD/commit/e0683f4db0fb56f8139f78ca2e500b21ec2a67f0
  sed -i 's|boost::re_detail::string_out_iterator<std::string > out(newfragment);|std::back_insert_iterator<std::string> out(newfragment);|' src/Mod/Drawing/App/FeaturePage.cpp

  mkdir -p build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX:PATH=/opt/${pkgname} .. \
        -DCMAKE_BUILD_TYPE=Release \
  	-DOCC_INCLUDE_DIR:PATH=/opt/opencascade/inc/ \
  	-DOCC_LIBRARY_DIR:PATH=/opt/opencascade/lin64/gcc/lib \
	-DFREECAD_USE_EXTERNAL_PIVY:BOOL=ON
  make

  # Builds Qt plugin, seems not part of the source in 0.14
  #cd ../src/Tools/plugins/widget/
  #qmake plugin.pro
  #make
}

package() {
  cd ${_pkgname}-${pkgver}/build

  # Install main program
  make DESTDIR=${pkgdir} install

  # Symlink to /usr/bin
  mkdir -p ${pkgdir}/usr/bin/
  ln -sf /opt/${pkgname}/bin/FreeCAD ${pkgdir}/usr/bin/freecad
  ln -sf /opt/${pkgname}/bin/FreeCADCmd ${pkgdir}/usr/bin/freecadcmd

  # Installs Qt plugin
  #install -Dm755 ../src/Tools/plugins/widget/libFreeCAD_widgets.so "${pkgdir}/usr/lib/qt/plugins/designer/libFreeCAD_widgets.so"

  # Install pixmaps and desktop shortcut
  desktop-file-install \
    --dir=${pkgdir}/usr/share/applications ${srcdir}/${pkgname}.desktop

   # Mime info
   install -D -m644 ${srcdir}/${pkgname}.xml ${pkgdir}/usr/share/mime/packages/${pkgname}.xml
}

