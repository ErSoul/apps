
pkgname=opencascade
pkgver=6.9.1
pkgrel=1
epoch=1
pkgdesc="Open CASCADE Technology, 3D modeling & numerical simulation"
arch=('x86_64')
url="http://dev.opencascade.org/"
license=('custom')
depends=('tk' 'mesa' 'java-runtime' 'libxmu' 'ftgl' 'vtk')
makedepends=('java-environment')
options=('!libtool')
source=("http://files.opencascade.com/OCCT/OCC_${pkgver}_release/$pkgname-${pkgver}.tgz"
        'env.sh'
        'opencascade.sh'
        'opencascade.conf')
md5sums=('7f2e645b5010d190c9bb35dc457f9a7c'
         'a96f28ee7f4273ae1771ee033a2a3af3'
         'd9368b8d348ced3ec4462012977552d2'
         '2924ecf57c95d25888f51071fdc72ad0')

prepare() {
  cd ${pkgname}-${pkgver}
  
  sed -i -e 's|-6.1||' adm/cmake/TKIVtk/CMakeLists.txt
  sed -i -e 's|-6.1||' adm/cmake/TKIVtkDraw/CMakeLists.txt
  
  sed -i -e 's|${OS_WITH_BIT}/${COMPILER}/lib${BUILD_POSTFIX}|lib${BUILD_POSTFIX}|' adm/templates/3rdparty_macro.cmake
  sed -i -e 's|${OS_WITH_BIT}/${COMPILER}/lib${BUILD_POSTFIX}|lib${BUILD_POSTFIX}|' adm/templates/freetype.cmake
  sed -i -e 's|${OS_WITH_BIT}/${COMPILER}/lib${BUILD_POSTFIX}|lib${BUILD_POSTFIX}|' adm/templates/vtk.cmake
}
         
build() {
  mkdir -p build
  cd build
  
  CXXFLAGS+=' -DvtkFloatingPointType=double'

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/${pkgname} \
    -DINSTALL_DIR=/opt/${pkgname} \
    -DINSTALL_FREETYPE=/opt/${pkgname} \
    -DINSTALL_TCL=/opt/${pkgname} \
    -DINSTALL_VTK=/opt/${pkgname} \
    -DLIN=/opt/${pkgname}/lib \
    -DUSE_GL2PS=ON \
    -DUSE_VTK=ON \
    -D3RDPARTY_VTK_DIR=/usr/lib \
    -D3RDPARTY_VTK_INCLUDE_DIR=/usr/include/vtk/ 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  #cp -r src/UnitsAPI/ "${pkgdir}/opt/${pkgname}/src"
  install -D -m644 ${srcdir}/opencascade.conf ${pkgdir}/etc/ld.so.conf.d/opencascade.conf
  install -D -m 755 ${srcdir}/opencascade.sh ${pkgdir}/etc/profile.d/opencascade.sh
  install -m 755 ${srcdir}/env.sh ${pkgdir}/opt/${pkgname}
  
  install -dm755 $pkgdir/usr/share/licenses/${pkgname}/
  install -m644 ${srcdir}/${pkgname}-${pkgver}/LICENSE_LGPL_21.txt  ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/${pkgname}-${pkgver}/OCCT_LGPL_EXCEPTION.txt ${pkgdir}/usr/share/licenses/${pkgname}
}

