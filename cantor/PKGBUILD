
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=cantor
pkgver=${_kdever}
pkgrel=2
pkgdesc="KDE Frontend to Mathematical Software"
url="http://kde.org/applications/education/cantor/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kconfig' 'knewstuff' 'ktexteditor' 'kcoreaddons' 'karchive' 'kparts' 'kpty' 'kalgebra'
         'kdelibs4support' 'libspectre' 'r' 'octave' 'python2' 'python3' 'libqalculate')
makedepends=('extra-cmake-modules' 'kdoctools' 'icu')
opdepends=('scilab' 'maxima') 
conflicts=('kdeedu-cantor')
replaces=('kdeedu-cantor')
groups=('kde' 'kdeedu' 'kde-uninstall' 'education')
install=${pkgname}.install
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        'qalculateexpression.diff')
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '8a14d450ac8cec6b6ccf1ba14d82ed88')

prepare() {
  cd $pkgname-${pkgver}
  
  patch -p1 -i $srcdir/qalculateexpression.diff
  sed -i -e 's|add_subdirectory(sage)|#add_subdirectory(sage)|' ${srcdir}/${pkgname}-${pkgver}/src/backends/CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
