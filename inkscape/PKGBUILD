
pkgname=inkscape
pkgver=0.92.2
pkgrel=3
pkgdesc="A vector-based drawing program - svg compliant"
url="https://inkscape.org/en/"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('poppler' 'imagemagick' 'aspell' 'gc' 'gtkmm' 'libxslt' 'gsl'
         'desktop-file-utils' 'popt' 'hicolor-icon-theme' 'python2-lxml' 'python2-numpy'
         'libwpg' 'libvisio' 'libwpd' 'potrace')
makedepends=('boost' 'pkg-config' 'intltool')
options=('!libtool')
source=("https://inkscape.global.ssl.fastly.net/media/resources/file/inkscape-${pkgver}.tar.bz2"
        "https://gitlab.com/inkscape/inkscape/commit/93ccf03162cd2e46d962822d5507865f3451168c.patch")
#source=("https://inkscape.org/en/gallery/item/10682/inkscape-${pkgver}.tar.bz2")
md5sums=('6f6bcbd0237ecd064460315465467b04'
         '6be509746489de06005ef01fc96acb6b')

prepare() {
  cd ${pkgname}-${pkgver}
  # poppler 0.58 & up needed patch
  patch -p1 -i ${srcdir}/93ccf03162cd2e46d962822d5507865f3451168c.patch
}

build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
