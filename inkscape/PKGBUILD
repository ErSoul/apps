
pkgname=inkscape
pkgver=0.48.5
pkgrel=1
pkgdesc="A vector-based drawing program - svg compliant"
url="http://inkscape.sourceforge.net/"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('poppler-glib' 'imagemagick' 'gtkspell' 'gc' 'gtkmm' 'libxslt' 'gsl' 'pyxml'
         'desktop-file-utils' 'popt' 'hicolor-icon-theme' 'python2-lxml' 'python2-numpy')
makedepends=('boost' 'pkg-config' 'intltool')
options=('!libtool')
install="inkscape.install"
source=("http://sourceforge.net/projects/inkscape/files/inkscape/${pkgver}/inkscape-${pkgver}.tar.bz2")
md5sums=('431cda5cd40cd4fdf2b89db1bdcce61f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '/extern unsigned GC_version;/c unsigned GC_version=GC_VERSION_MAJOR<<16+GC_VERSION_MINOR<<8+GC_VERSION_MICRO;' configure

  ./configure --prefix=/usr \
              --with-python \
              --with-perl \
              --enable-lcms \
              --enable-poppler-cairo \
              --disable-dependency-tracking

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}

