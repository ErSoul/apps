
pkgname=inkscape
pkgver=0.91
pkgrel=11
pkgdesc="A vector-based drawing program - svg compliant"
url="http://inkscape.sourceforge.net/"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('poppler-glib' 'imagemagick' 'gtkspell' 'gc' 'gtkmm' 'libxslt' 'gsl' 'pyxml'
         'desktop-file-utils' 'popt' 'hicolor-icon-theme' 'python2-lxml' 'python2-numpy'
         'libwpg' 'libvisio')
makedepends=('boost' 'pkg-config' 'intltool')
options=('!libtool')
install="inkscape.install"
source=("https://inkscape.global.ssl.fastly.net/media/resources/file/inkscape-${pkgver}.tar.bz2")
md5sums=('278dfa4514adcde23546370ec2c84581')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '/extern unsigned GC_version;/c unsigned GC_version=GC_VERSION_MAJOR<<16+GC_VERSION_MINOR<<8+GC_VERSION_MICRO;' configure
  # needed for newer libsic++, glibmm, don't use older libsigc++ to build any longer
  CXXFLAGS+=' -std=c++11'

  ./configure --prefix=/usr \
              --enable-lcms \
              --enable-poppler-cairo \
              --disable-dependency-tracking \
              --disable-strict-build

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}

