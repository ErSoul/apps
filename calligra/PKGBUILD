
pkgname=calligra
pkgver=2.9.125
_commit=6657dac8e5f14e89586790e2f51a5c858432b05b
pkgrel=1
arch=('x86_64')
pkgdesc="Suite is a collection of office applications linked together by a common base."
url='http://www.calligra-suite.org'
license=('FDL1.2' 'GPL2' 'LGPL')
depends=('karchive' 'kcodecs' 'kconfig' 'kcoreaddons' 'kdbusaddons' 'kguiaddons' 'ki18n' 'kitemviews' 'kwidgetsaddons'
         'threadweaver' 'kcompletion' 'kiconthemes' 'kparts' 'kxmlgui' 'kross' 'kconfigwidgets' 'ktexteditor' 
         'kdelibs4support' 'kio'
         'eigen3' 'freetds' 'xbase' 'libpqxx' 'libvisio' 'libgsf' 'libwpd' 'libwpg' 'libwps' 'pstoedit' 'gsl' 
         'lcms2' 'libmariadbclient' 'libodfgen' 'poppler-qt5' 'libspnav' 'fftw' 'hicolor-icon-theme' 
         'sqlite3' 'libkdcraw' 'exiv2' 'qca-qt5' 'qt5-declarative' 'marble' 'kactivities' 'kdiagram' 'kdeclarative' 'kcalcore'
         'kcontacts' 'akonadi-contacts' 'knotifyconfig' 'knotifications' 'okular' 'openexr' 'vc' 'kreport' 'qt5-webkit')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'boost' 'docbook-xsl' 'postgresql') # 'clang' vc fails clang
optdepends=('postgresql: PostgreSQL database driver')
#source=("http://mirrors.mit.edu/kde/stable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.xz")
source=("https://github.com/KDE/calligra/archive/${_commit}.zip"
        'libwps-0.4.patch'
        'warnings.diff')
md5sums=('796b71d0d59608aecb721f414e81b31f'
         '2529367ef6a10582436003aceb7dc3e8'
         '34ee1612f3fbb3342ff8dcfa8f91821f')

prepare() {
  cd ${pkgname}-${_commit}
  #patch -p1 -i $srcdir/warnings.diff
  #patch -p1 -i $srcdir/libwps-0.4.patch
  sed -i 's|"Karbon app (for Desktop)"  UNMAINTAINED  REQUIRES|"Karbon app (for Desktop)"  REQUIRES|' CalligraProducts.cmake
  sed -i 's|Okular5 1.0.0 QUIET|Okular5 0.99.60 QUIET|' CMakeLists.txt
}

build() {
  mkdir build
  cd build
  
  #export CC=/usr/bin/clang
  #export CXX=/usr/bin/clang++
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DPRODUCTSET=DESKTOP \
    -DBUILD_TESTING=FALSE \
    -DPACKAGERS_BUILD=true 
    #-DBUILD_UNMAINTAINED=TRUE
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
  
  # fix some bugs in .desktop files
  #sed -i 's|X-DBUS-StartupType=multi|#X-DBUS-StartupType=multi|' ${pkgdir}/usr/share/applications/org.kde.braindump.desktop
  rm ${pkgdir}/usr/share/applications/calligra_filter_odt2docx.desktop
  
}

