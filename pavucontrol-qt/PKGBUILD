 
pkgname=pavucontrol-qt
pkgver=0.14.1
pkgrel=1
pkgdesc="Qt port of volume control pavucontrol of sound server PulseAudio."
arch=("x86_64")
url="https://github.com/lxqt/pavucontrol-qt"
license=('GPL2')
depends=('pulseaudio' 'qt5-base')
makedepends=('lxqt-build-tools' 'qt5-tools')
source=("https://downloads.lxqt.org/downloads/pavucontrol-qt/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d805c9fd738b67a378edb432dc702ab4d009d2c9192fd977492ebbfc502ebca2')

build() {
    mkdir -p build
    cd build

    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make
}

package() {
    cd build
    
    make DESTDIR=${pkgdir} install
}
