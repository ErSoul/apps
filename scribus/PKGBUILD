
pkgname=scribus
_pkgname=Scribus
pkgver=1.5.2
pkgrel=11
pkgdesc="A desktop publishing program"
arch=('x86_64')
url="http://www.scribus.net"
license=('GPL')
depends=('libcups' 'lcms2' 'qt5-declarative' 'ghostscript' 'libart-lgpl' 'python2' 'libxml2' 'hyphen'
         'cairo' 'desktop-file-utils' 'shared-mime-info' 'ruby' 'aspell' 'podofo' 'librevenge' 'poppler'
         'hunspell' 'graphicsmagick' 'libvisio')
makedepends=('cmake' 'boost' 'qt5-tools')
groups=('office')
options=('!libtool' '!makeflags')
source=("http://sourceforge.net/projects/scribus/files/scribus-devel/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/scribusproject/scribus/commit/dbd56212a0b9b3aa4a14d523e2499a6cf7be6e2e.diff")
# view at https://www.scribus.net/websvn/
#source=("scribus::svn://scribus.net/trunk")
md5sums=('031661a8fe0ba0131c360d3e9edc2fc0'
         '5b667c0a40b7622f922c4abc976e4c3f')

prepare() {
  cd ${pkgname}-${pkgver}
  
  sed -i 's|hunspell-1.3 hunspell-1.2|hunspell-1.3 hunspell-1.6|' cmake/modules/FindHUNSPELL.cmake
  patch -p1 -i ${srcdir}/dbd56212a0b9b3aa4a14d523e2499a6cf7be6e2e.diff
  
}

build() {
	mkdir -p build
	cd build
	
	CXXFLAGS="-std=c++11"
	
	cmake ../${pkgname}-${pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_GRAPHICSMAGICK=1 \
	   -DLIB_SUFFIX=""
	make 
}

package() {
	cd build
	make DESTDIR=${pkgdir} install

	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/scribus.desktop \
	   ${pkgdir}/usr/share/applications/scribus.desktop
}
