
pkgname=go
pkgver=1.4.2
pkgrel=1
pkgdesc='Google Go compiler and tools (release version)'
arch=('x86_64')
url='http://golang.org/'
license=('BSD')
depends=('perl' 'gawk')
makedepends=('inetutils')
options=('!strip' 'staticlibs')
install="$pkgname.install"
# check for versions: http://golang.org/dl/
source=("https://storage.googleapis.com/golang/${pkgname}$pkgver.linux-amd64.tar.gz")
md5sums=('cdc1ff96c6c99b918402693a5cf0e1f3')

build() {
  cd "$pkgname/src"

  export GOROOT="$srcdir/$pkgname"
  export GOBIN="$GOROOT/bin"
  export GOPATH="$srcdir/"
  export GOROOT_FINAL=/usr/lib/go

  export GOOS=linux
  export GOARCH=arm

  bash make.bash

  for os in linux; do 
    for arch in amd64 386; do
      export GOOS="$os"
      export GOARCH="$arch"
      bash make.bash --no-clean
    done
  done

  GOOS=linux

  $GOROOT/bin/go get -d golang.org/x/tools/cmd/godoc
  $GOROOT/bin/go build -o $srcdir/godoc golang.org/x/tools/cmd/godoc
  for tool in vet cover; do
    $GOROOT/bin/go get -d golang.org/x/tools/cmd/${tool}
    $GOROOT/bin/go build -o $GOROOT/pkg/tool/${GOOS}_amd64/${tool} golang.org/x/tools/cmd/${tool}
  done
}

check() {
  cd "$pkgname"

  export GO386=387

  export GOOS=linux
  export GOARCH=amd64
  export GOROOT="$srcdir/$pkgname"
  export GOBIN="$GOROOT/bin"
  export PATH="$srcdir/$pkgname-$pkgver/bin:$PATH"

  cd src && bash run.bash --no-rebuild || true
}

package() {
  cd "$pkgname"

  export GOROOT="$srcdir/$pkgname"
  export GOBIN="$GOROOT/bin"

  install -Dm755 "$srcdir/godoc" "$pkgdir/usr/bin/godoc"

  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/go/LICENSE"

  mkdir -p \
    "$pkgdir/"{etc/profile.d,usr/{share/go,lib/go,lib/go/src,lib/go/site/src}}

  cp -r doc misc -t "$pkgdir/usr/share/go"
  ln -s /usr/share/go/doc "$pkgdir/usr/lib/go/doc"
  cp -a bin "$pkgdir/usr"
  cp -a pkg "$pkgdir/usr/lib/go"
  cp -a "$GOROOT/src" "$pkgdir/usr/lib/go/"
  cp -a "$GOROOT/src/cmd" "$pkgdir/usr/lib/go/src/cmd"
  cp -a "$GOROOT/src/lib9" "$pkgdir/usr/lib/go/src/"
  cp -a "$GOROOT/lib" "$pkgdir/usr/lib/go/"
  cp -a "$GOROOT/include" "$pkgdir/usr/lib/go/"

  install -Dm644 src/Make.* "$pkgdir/usr/lib/go/src"

  find "$pkgdir/usr/lib/go/src/" -type f -name '*.[ao]' -delete
  find "$pkgdir" -type f -name sql.go -exec chmod -x {} \;
  find "$pkgdir/usr/lib/go/src" -type f -executable -delete

  # Headers for C modules
  install -Dm644 src/runtime/runtime.h \
    "$pkgdir/usr/lib/go/src/runtime/runtime.h"
  install -Dm644 src/runtime/cgocall.h \
    "$pkgdir/usr/lib/go/src/runtime/cgocall.h"

  ln -sf /usr/bin "$pkgdir/usr/lib/go/bin"

  install -Dm755 src/make.bash "$pkgdir/usr/lib/go/src/make.bash"
  install -Dm755 src/run.bash "$pkgdir/usr/lib/go/src/run.bash"
  cp -r misc/ "$pkgdir/usr/lib/go/"

  install -Dm644 favicon.ico "$pkgdir/usr/lib/go/favicon.ico"

  rm -f "$pkgdir/usr/share/go/doc/articles/wiki/get.bin"

  install -Dm644 VERSION "$pkgdir/usr/lib/go/VERSION"

  find "$pkgdir/usr/"{lib/go/pkg,bin} -type f -exec touch '{}' +
}
