
pkgname=wireshark
pkgver=2.4.0
pkgrel=1
pkgdesc='Network packet analyzer that will try to capture network packets and tries to display that packet data as detailed as possible'
arch=('x86_64')
url='https://www.wireshark.org/'
license=('GPL2')
depends=('krb5' 'libgcrypt' 'libcap' 'libpcap' 'bash' 'geoip' 'gnutls' 'glib2' 'lua' 'qt5-multimedia'
         'qt5-svg' 'asciidoc' 'portaudio' 'c-ares' 'libssh')
makedepends=('cmake' 'python2' 'qt5-tools')
source=("https://www.wireshark.org/download/src/${pkgname}-${pkgver}.tar.xz"
        'udpdump.diff'
        'udpdump.pod')
md5sums=('655106f8cf3bb8f521336d3a8ab5b10b'
         'ffbcc92f2783d585995a532f5b9fa06f'
         '17a39ed1d9d69ae657af52422429fef1')

prepare() {
  cd ${pkgname}-${pkgver}

  # https://bugs.wireshark.org/bugzilla/show_bug.cgi?id=13903
  cp ${srcdir}/udpdump.pod ${srcdir}/${pkgname}-${pkgver}/doc/udpdump.pod
  patch -p1 -i ${srcdir}/udpdump.diff
}

build() {
  cd ${srcdir}
  
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT5=ON \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  sed -i 's|System;Monitor;GTK;|System;Monitor;Tools;|' ${srcdir}/${pkgname}-${pkgver}/wireshark.desktop
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/wireshark.desktop ${pkgdir}/usr/share/applications/wireshark.desktop
}

