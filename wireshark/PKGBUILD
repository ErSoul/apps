
pkgname=wireshark
pkgver=1.99.7
pkgrel=1
pkgdesc='Network packet analyzer that will try to capture network packets and tries to display that packet data as detailed as possible'
arch=('x86_64')
url='http://www.wireshark.org/'
license=('GPL2')
depends=('krb5' 'libgcrypt' 'libcap' 'libpcap' 'bash' 'geoip' 'gnutls' 'glib2' 'lua' 'qt5-multimedia' 'asciidoc')
makedepends=('cmake' 'python2' 'qt5-tools')
install=wireshark.install
source=("http://www.wireshark.org/download/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('11cb9643013a8aa27f16e5f596fd9b11')

build() {
  cd ${srcdir}
  
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT5=ON \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  sed -i 's|System;Monitor;GTK;|System;Monitor;Tools;|' ${srcdir}/${pkgname}-${pkgver}/wireshark.desktop
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/wireshark.desktop ${pkgdir}/usr/share/applications/wireshark.desktop
}

