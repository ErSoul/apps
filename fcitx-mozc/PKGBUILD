
# check https://github.com/google/mozc/issues/327 for Qt5 build
pkgname=fcitx-mozc
_bldtype=Release
_zipcode_rel=201504
_protobuf_rev=172019c40bf548908ab09bfd276074c929d48415
_gyp_rev=f06a5a0e46ce3a1d3a51fbd7fa8df5c72f47d881
_jsoncpp_rev=11086dd6a7eba04289944367ca82cea71299ed70
_japanese_usage_dictionary_rev=10
_mozc_rev=b4d9d5c4b1fe5ccac63446d917315d71398dd564
_fonttools_rev=5ba7d98a4153fad57258fca23b0bcb238717aec3
_jsr305_ver=2.0.2
_fcitx_patchver=2.17.2102.102.1
pkgdesc="Japanese Input Method Editor designed for multi-platform"
pkgver=2.17.2240.102
pkgrel=1
arch=('x86_64')
url="https://github.com/google/mozc"
license=('custom')
depends=('qt' 'fcitx' 'zinnia')
makedepends=('pkg-config' 'python2' 'curl' 'gtk2' 'mesa' 'subversion' 'ninja' 'git' 'clang')
source=("mozc::git+https://github.com/google/mozc.git#commit=${_mozc_rev}"
        "jsoncpp::git+https://github.com/open-source-parsers/jsoncpp.git#commit=${_jsoncpp_rev}"
        "japanese_usage_dictionary::svn+http://japanese-usage-dictionary.googlecode.com/svn/trunk#revision=$_japanese_usage_dictionary_rev"
        "gyp::git+https://chromium.googlesource.com/external/gyp#commit=${_gyp_rev}"
        "protobuf::git+https://github.com/google/protobuf.git#commit=${_protobuf_rev}"
        "http://findbugs.googlecode.com/svn/repos/release-repository/com/google/code/findbugs/jsr305/$_jsr305_ver/jsr305-$_jsr305_ver.jar"
        "fontTools::git+https://github.com/googlei18n/fonttools.git#commit=${_fonttools_rev}"
        "http://downloads.sourceforge.net/pnsft-aur/x-ken-all-${_zipcode_rel}.zip"
        "http://downloads.sourceforge.net/pnsft-aur/jigyosyo-${_zipcode_rel}.zip"
        "http://download.fcitx-im.org/fcitx-mozc/fcitx-mozc-${_fcitx_patchver}.patch"
        "http://download.fcitx-im.org/fcitx-mozc/fcitx-mozc-icon.tar.gz")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         '144c0767e2aaf0c21a935908d0e52c68'
         'SKIP'
         '5fa1b29d7e06a482b4eb99baf2b8597d'
         '22e7aeec439f889b0cda4bd23126414c'
         'd32e372c9e1fbbc028062e9f962089a7'
         '80fe2ef127b70649df22d133822ac8a7')

prepare() {
  cd mozc/src

  rm unix/fcitx -rf
  patch -p2 -i ${srcdir}/fcitx-mozc-${_fcitx_patchver}.patch

  python2 dictionary/gen_zip_code_seed.py --zip_code="${srcdir}/x-ken-all.csv" --jigyosyo="${srcdir}/JIGYOSYO.CSV" >> data/dictionary_oss/dictionary09.txt

  cd ${srcdir}
  for dep in jsoncpp gyp protobuf japanese_usage_dictionary fontTools
  do
    cp -a $dep mozc/src/third_party/
  done

  install -D jsr305-$_jsr305_ver.jar mozc/src/third_party/jsr305/jsr305-$_jsr305_ver.jar
}

build() {
  cd mozc/src

  _targets="server/server.gyp:mozc_server gui/gui.gyp:mozc_tool unix/fcitx/fcitx.gyp:fcitx-mozc"

  QTDIR=/usr GYP_DEFINES="document_dir=/usr/share/licenses/$pkgname use_libzinnia=1" python2 build_mozc.py gyp
  python2 build_mozc.py build -c $_bldtype $_targets

  head -n 29 server/mozc_server.cc > LICENSE
}

package() {
  cd mozc/src
  install -D -m 755 out_linux/${_bldtype}/mozc_server ${pkgdir}/usr/lib/mozc/mozc_server
  install    -m 755 out_linux/${_bldtype}/mozc_tool   ${pkgdir}/usr/lib/mozc/mozc_tool

  install -d ${pkgdir}/usr/share/licenses/$pkgname/
  install -m 644 LICENSE data/installer/*.html ${pkgdir}/usr/share/licenses/${pkgname}/

  for mofile in out_linux/${_bldtype}/gen/unix/fcitx/po/*.mo
  do
    filename=`basename $mofile`
    lang=${filename/.mo/}
    install -D -m 644 $mofile ${pkgdir}/usr/share/locale/$lang/LC_MESSAGES/fcitx-mozc.mo
  done

  install -D -m 755 out_linux/${_bldtype}/fcitx-mozc.so ${pkgdir}/usr/lib/fcitx/fcitx-mozc.so
  install -D -m 644 unix/fcitx/fcitx-mozc.conf ${pkgdir}/usr/share/fcitx/addon/fcitx-mozc.conf
  install -D -m 644 unix/fcitx/mozc.conf ${pkgdir}/usr/share/fcitx/inputmethod/mozc.conf

  install -d ${pkgdir}/usr/share/fcitx/mozc/icon
  install -m 644 $srcdir/fcitx-mozc-icons/mozc.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-alpha_full.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-alpha_full.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-alpha_half.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-alpha_half.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-direct.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-direct.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-hiragana.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-hiragana.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-katakana_full.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-katakana_full.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-katakana_half.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-katakana_half.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-dictionary.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-dictionary.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-properties.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-properties.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-tool.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-tool.png
}

