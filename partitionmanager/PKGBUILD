
pkgname=partitionmanager
pkgver=2.9.91
_commit=771c0ceaa2bfd5634f65e6e390733cec557e90de
pkgrel=1
epoch=1
pkgdesc="Easily manage disks, partitions and file systems on your KDE Desktop"
arch=('x86_64')
url="https://www.kde.org/applications/system/kdepartitionmanager"
license=('GPL2')
depends=('kconfig' 'ki18n' 'kcrash' 'kinit' 'kio' 'kiconthemes' 'kjobwidgets' 'kwindowsystem' 'kxmlgui' 'kwidgetsaddons'
         'parted' 'libatasmart' 'udisks2' 'kpmcore')
makedepends=('extra-cmake-modules' 'pkgconfig' 'kdoctools')
optdepends=('e2fsprogs: ext2/3/4 support'
            'xfsprogs: XFS support'
            'jfsutils: JFS support'
            'reiserfsprogs: Reiser support'
            'ntfsprogs: NTFS support'
            'dosfstools: FAT32 support')
conflicts=('kf5-partitionmanager')
replaces=('kf5-partitionmanager')
groups=('system')
options=('debug')	
#source=("http://download.kde.org/stable/partitionmanager/${pkgver}/src/partitionmanager-${pkgver}.tar.xz")
source=("https://github.com/KDE/partitionmanager/archive/${_commit}.zip")
md5sums=('7eb01ae46ab10ebca1554cc476f885be')

prepare() {
  cd ${pkgname}-${_commit}
  # filsystem code from src/fs/filesystem.h, set as default
  #sed -i -e 's/FileSystem::Ext3/FileSystem::Xfs/' ${srcdir}/${pkgname}-${pkgver}/src/config.kcfg
  sed -i -e 's/FileSystem::Ext4/FileSystem::Xfs/' src/partitionmanager.kcfg
}

build() {
  mkdir build
  cd build

  cmake ../${pkgname}-${_commit} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
        -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DBUILD_TESTING=OFF 

  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install 
}

