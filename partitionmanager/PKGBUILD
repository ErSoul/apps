
pkgname=partitionmanager
pkgver=2.0.3
#_pkgver=1.2.1
pkgrel=1
epoch=1
pkgdesc="Easily manage disks, partitions and file systems on your KDE Desktop"
arch=('x86_64')
url="https://www.kde.org/applications/system/kdepartitionmanager"
license=('GPL2')
depends=('kconfig' 'ki18n' 'kcrash' 'kinit' 'kio' 'kiconthemes' 'kjobwidgets' 'kwindowsystem' 'kxmlgui' 'kwidgetsaddons'
         'parted' 'libatasmart' 'udisks2' 'kpmcore')
makedepends=('extra-cmake-modules' 'pkgconfig' 'kdoctools')
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfsprogs: NTFS support'
	    'dosfstools: FAT32 support')
conflicts=('kf5-partitionmanager')
replaces=('kf5-partitionmanager')
groups=('system')
options=('debug')
install=partitionmanager.install	
source=("http://download.kde.org/stable/partitionmanager/${pkgver}/src/partitionmanager-${pkgver}.tar.xz")
#source=("git://anongit.kde.org/partitionmanager")
md5sums=('ff0a14cd2fbeb1543309b81965d78ffc')

build() {
  cd ${srcdir}
  # filsystem code from src/fs/filesystem.h, set as default
  #sed -i -e 's/FileSystem::Ext3/FileSystem::Xfs/' ${srcdir}/${pkgname}-${pkgver}/src/config.kcfg
  sed -i -e 's/FileSystem::Ext3/FileSystem::Xfs/' ${srcdir}/${pkgname}-${pkgver}/src/partitionmanager.kcfg
  
  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DLIB_INSTALL_DIR=lib \
        -DSYSCONF_INSTALL_DIR=/etc \
        -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
        -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DBUILD_TESTING=OFF 

  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install 
}

