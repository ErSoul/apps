 
pkgname=krename
pkgver=5.0.0
pkgrel=2
pkgdesc="Powerful batch renamer for KDE."
arch=("x86_64")
url="http://www.krename.net/"
license=('GPL')
depends=('kcompletion' 'kconfig' 'kcoreaddons' 'kcrash' 'ki18n' 'kiconthemes' 'kitemviews' 'kjobwidgets'
         'kjs' 'kio' 'kservice' 'kwidgetsaddons' 'kxmlgui' 'taglib' 'exiv2' 'podofo')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("https://download.kde.org/stable/krename/${pkgver}/src/krename-${pkgver}.tar.xz")
md5sums=('dbab11a9c958080c77e1a86019d3611c')
 
build() {
  mkdir -p build
  cd build
 
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}
 
package() {
  cd build
 
  make DESTDIR=${pkgdir} install
}
