 
pkgname=med
pkgver=3.3.1
pkgrel=1
arch=('x86_64')
pkgdesc="Library to store and exchange meshed data or computation results"
url="https://www.salome-platform.org/"
license=('LGPL')
depends=('hdf5' 'openmpi')
makedepends=('cmake' 'coreutils' 'python2' 'swig')
source=("http://files.salome-platform.org/Salome/other/${pkgname}-${pkgver}.tar.gz"
        "https://salsa.debian.org/science-team/med-fichier/raw/master/debian/patches/hdf5-1.10-support.patch")
md5sums=('4981a02fa50b1a487a022804512631ff'
         '8fe2bc9bc5e6756351e1ff80e613dba3')

prepare() {
  cd ${pkgname}-${pkgver}_SRC
  
  patch -p1 -i ${srcdir}/hdf5-1.10-support.patch
}
         
build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver}_SRC \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMEDFILE_BUILD_FORTRAN=ON \
    -DMEDFILE_USE_MPI=OFF \
    -DMEDFILE_BUILD_STATIC_LIBS=OFF \
    -DMEDFILE_INSTALL_DOC=OFF \
    -DMEDFILE_BUILD_PYTHON=ON \
    -DMEDFILE_BUILD_TESTS=OFF 
  make
}
 
package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
