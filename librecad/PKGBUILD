
pkgname=librecad
pkgver=2.0.2
pkgrel=1
pkgdesc="2D CAD drawing tool based on the community edition of QCad"
arch=('x86_64')
url="http://www.librecad.org/"
license=('GPL')
depends=('qt' 'muparser')
makedepends=('boost' 'imagemagick')
source=("${pkgname}-${pkgver}.tar.gz::https://codeload.github.com/LibreCAD/LibreCAD/tar.gz/${pkgver}")
md5sums=('0081a4028780d8a610c9f0aab8ac4eb6')

build() {
	cd "${srcdir}/LibreCAD-${pkgver}"

	# fix version string
	sed -i "/^SCMREVISION/c SCMREVISION=\"${pkgver}\"" librecad/src/src.pro

	export CPPFLAGS="-std=c++0x"
	qmake librecad.pro
	make
}

package() {
	cd "${srcdir}/LibreCAD-${pkgver}"

	install -D -m0755 unix/librecad "${pkgdir}/usr/bin/librecad"
	install -D -m0755 unix/ttf2lff "${pkgdir}/usr/bin/ttf2lff"

	install -D -m0644 desktop/librecad.desktop "${pkgdir}/usr/share/applications/librecad.desktop"
	install -D -m0644 desktop/librecad.1 "${pkgdir}/usr/share/man/man1/librecad.1"

	for SIZE in 16 24 32 48 64 96 128; do
		convert -scale ${SIZE} \
			desktop/graphics_icons_and_splash/Icon\ LibreCAD/Icon_Librecad.svg \
			${srcdir}/librecad.png
		install -D -m0644 ${srcdir}/librecad.png "${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/librecad.png"
	done
	install -D -m0644 desktop/graphics_icons_and_splash/Icon\ LibreCAD/Icon_Librecad.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/librecad.svg"

	mkdir -p "${pkgdir}/usr/share/librecad/"
	cp -r unix/resources/{library,patterns,fonts,qm} "${pkgdir}/usr/share/librecad/"
}

