
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kwin
pkgver=${_plasmaver}
pkgrel=2
pkgdesc='KDE Window Manager'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kwin'
license=('LGPL')
depends=('qt5-multimedia' 'plasma-framework' 'kcmutils' 'kinit' 'knewstuff' 'libxcursor'
         'libepoxy' 'kwayland' 'kdecoration') # kwin not ready for 0.8.0, fixed in 5.2.1 'libinput' 
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools')
groups=('plasma5')
conflicts=('kde-workspace')
replaces=('kde-workspace')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=(`grep ${pkgname}-$_plasmaver.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

