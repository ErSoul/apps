
pkgname=qrencode
pkgver=4.0.0
pkgrel=1
pkgdesc="C library for encoding data in a QR Code symbol."
arch=('x86_64')
url="https://fukuchi.org/works/qrencode/"
license=('GPL')
depends=('libpng' 'zlib')
makedepends=('cmake')
options=('!libtool')
source=("https://fukuchi.org/works/qrencode/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('d4140d77fce3a1c39735cc3e98e3a19018daaa08c870644f71727d27ffb17711e76b4ed4e51718d90d812e78df75fe575617580ea9b1bf210438f8fd1734204e')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
