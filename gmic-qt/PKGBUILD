
pkgname=gmic-qt
pkgver=2.3.6
_gmic=2d9aa7833ab9d9454f455ed83abbbea9aedf4bfb
_commit=9e992cff2db418032b849458f5c9443267c7162c
pkgrel=1
pkgdesc="Plug-in to bring the full-featured open-source framework for image processing -G'MIC- to Krita"
url="http://gmic.eu/"
license=('GPLv3')
arch=('x86_64')
depends=('qt5-base' 'libpng' 'zlib' 'fftw' 'curl')
makedepends=('cmake' 'qt5-tools')
source=("https://github.com/dtschump/gmic/archive/${_gmic}.zip"
        "https://github.com/c-koi/gmic-qt/archive/${_commit}.zip")
md5sums=('b25d0e9083ff24531c74ce0632025bfa'
         'f875012fceac5a8023ce4fd6616642ef')
build() {
  cd gmic-${_gmic}/
  make -C src CImg.h gmic_stdlib.h
  
  cd ../gmic-qt-${_commit}
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGMIC_QT_HOST=krita \
    -DGMIC_PATH="../gmic-${_gmic}/src" 
  make
}

package() {
  cd gmic-qt-${_commit}
  
  #make DESTDIR=${pkgdir} install
  install -D -m755 gmic_krita_qt ${pkgdir}/usr/bin/gmic_krita_qt
  #install -D -m755 *.qm ${pkgdir}/usr/share/gmic-qt/locale/
  
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
} 
