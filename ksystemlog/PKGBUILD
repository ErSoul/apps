
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ksystemlog
pkgver=${_kdever}
#pkgver=15.07.71
pkgrel=2
pkgdesc='System log viewer tool'
url='http://kde.org/applications/system/ksystemlog/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kxmlgui' 'kcoreaddons' 'kwidgetsaddons' 'kitemviews' 'kiconthemes' 'kio' 'kconfig' 'karchive'
         'ki18n' 'kcompletion' 'ktextwidgets')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kdeadmin' 'kde-uninstall')
source=("http://download.kde.org/stable/applications/$_kdever/src//${pkgname}-$_kdever.tar.xz"
        'syslog-path.patch')
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '5486c901f707d7ea191eed58d3aef2c1')

prepare() {
  cd $pkgname-${pkgver}
  
  sed -i -e 's|<Action name="kernelLogMode" />|<Action name="cupsLogMode" />|' src/ksystemlogui.rc
  sed -i -e 's|i18n("System Log")|i18n("Pacman Log")|' src/modes/system/systemLogMode.cpp
  patch -p1 -i ${srcdir}/syslog-path.patch
}

build() {
  cd ${srcdir}
  
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
