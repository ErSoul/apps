
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ksystemlog
pkgver=${_kdever}
#pkgver=15.07.71
pkgrel=1
pkgdesc='System log viewer tool'
url='http://kde.org/applications/system/ksystemlog/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kxmlgui' 'kcoreaddons' 'kwidgetsaddons' 'kitemviews' 'kiconthemes' 'kio' 'kconfig' 'karchive'
         'ki18n' 'kcompletion' 'ktextwidgets')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kdeadmin' 'kde-uninstall')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        'ksystemlogui.rc')
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '442c9a8ac0bf18deed43a810bf81ce51')

prepare() {
  cd $pkgname-${pkgver}
  rm src/ksystemlogui.rc
  cp ${srcdir}/ksystemlogui.rc src/ksystemlogui.rc
  
  sed -i -e 's|<default>systemLogMode</default>|<default>xorgLogMode</default>|' src/config/ksystemlog.kcfg
}

build() {
  cd ${srcdir}
  
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
