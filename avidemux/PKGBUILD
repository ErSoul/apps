
pkgname=avidemux
pkgver=2.6.12
_commit=8987c1d95e2a9907a4ae01943caf9766ac814c4c
pkgrel=2
pkgdesc="Video editor designed for simple cutting, filtering and encoding tasks using a variety of codecs."
arch=('x86_64')
url="http://fixounet.free.fr/avidemux/"
license=('Public Domain')
depends=('qt5-script' 'flac' 'libva' 'libxv' 'desktop-file-utils' 'x264' 'pulseaudio' 'jack'
         'lame' 'sqlite' 'libvpx' 'x265')
makedepends=('cmake' 'yasm' 'pkg-config' 'qt5-tools')
options=('!makeflags')
#source=("http://downloads.sourceforge.net/avidemux/avidemux_${pkgver}.tar.gz"
source=("https://github.com/mean00/avidemux2/archive/${_commit}.zip"
        'bootStrap.diff')
md5sums=('b5ab9d1d382af8826581e68a8d1fea10'
         '186d31ea27c96e441e8c8270f1f40003')

prepare() {
    cd ${pkgname}2-${_commit}
    #sed 's|export O_PARAL="-j 2"|export O_PARAL="-j 1"|g' -i bootStrap.bash
    sed 's|lrelease-qt4|lrelease-qt5|g' -i cmake/Ts.cmake
    # try to disable vdpau, build fails on vdpau
    sed 's|SET(USE_VDPAU 1)|#SET(USE_VDPAU 1)|g' -i cmake/admCheckVDPAU.cmake
    patch -p1 -i $srcdir/bootStrap.diff
}

build() {
    cd ${pkgname}2-${_commit}
    
    bash bootStrap.bash --with-core \
        --with-plugins 
}

package() {
    cd ${pkgname}2-${_commit}
    cp -r install/usr/ $pkgdir/

    install -Dm644 ${pkgname}_icon.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
    install -Dm644 ${pkgname}2.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
    sed 's|Exec=avidemux2_gtk|Exec=avidemux3_qt5|' -i ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
