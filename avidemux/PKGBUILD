
pkgname=avidemux
pkgver=2.6.12
_commit=e9287b0f902916a15946d6c8b7def251e05a10d6
pkgrel=2
pkgdesc="Video editor designed for simple cutting, filtering and encoding tasks using a variety of codecs."
arch=('x86_64')
url="http://fixounet.free.fr/avidemux/"
license=('Public Domain')
depends=('qt5-script' 'flac' 'libva' 'libxv' 'desktop-file-utils' 'x264' 'pulseaudio' 'jack'
         'lame' 'sqlite' 'libvpx')
makedepends=('cmake' 'yasm' 'pkg-config' 'qt5-tools')
options=('!makeflags')
#source=("http://downloads.sourceforge.net/avidemux/avidemux_${pkgver}.tar.gz"
source=("https://github.com/mean00/avidemux2/archive/${_commit}.zip"
        'bootStrap.diff')
md5sums=('d2e527496730b8df1e49a021c16eba64'
         '186d31ea27c96e441e8c8270f1f40003')

prepare() {
    cd ${pkgname}2-${_commit}
    #sed 's|export O_PARAL="-j 2"|export O_PARAL="-j 1"|g' -i bootStrap.bash
    sed 's|lrelease-qt4|lrelease-qt5|g' -i cmake/Ts.cmake
    patch -p1 -i $srcdir/bootStrap.diff
}

build() {
    cd ${pkgname}2-${_commit}
    
    bash bootStrap.bash --with-core \
        --with-plugins
}

package() {
    cd ${pkgname}2-${_commit}
    cp -r install/usr/ $pkgdir/

    install -Dm644 ${pkgname}_icon.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
    install -Dm644 ${pkgname}2.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
    sed 's|Exec=avidemux2_gtk|Exec=avidemux3_qt5|' -i ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
