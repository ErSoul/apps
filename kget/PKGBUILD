
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kget
#pkgver=${_kdever}
pkgver=14.12.80
pkgrel=1
pkgdesc='Download Manager'
url='http://kde.org/applications/internet/kget/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('network')
depends=('kcoreaddons' 'kwallet' 'kconfig' 'kdbusaddons' 'ki18n' 'kconfigwidgets' 'knotifications' 'kio'
         'kdelibs4support' 'kiconthemes' 'kcmutils' 'knotifyconfig' 'solid'  'libmms' 'gpgmepp' 
         'qca-qt5') # libktorrent
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'boost')
install=${pkgname}.install
conflicts=('kdenetwork-kget')
replaces=('kdenetwork-kget')
#source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
#md5sums=(`grep ${_pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)
source=("git://anongit.kde.org/kget.git#branch=kf5_port")
md5sums=('SKIP')

prepare() {
  cd $pkgname
  sed -i -e 's|find_package(Qca 2.1.0)|find_package(Qca 2.1.0 NAMES Qca-qt5 Qca)|' ${srcdir}/${pkgname}/CMakeLists.txt
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}

