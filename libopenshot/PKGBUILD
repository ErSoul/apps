
pkgname=libopenshot
pkgver=0.0.4
pkgrel=1
pkgdesc="Open-source project dedicated to delivering high quality video editing, animation, and playback solutions to the world."
arch=('x86_64')
url="http://www.openshot.org/"
license=('LGPL3')
depends=('libopenshot-audio' 'ffmpeg' 'imagemagick' 'libx11' 'libxcursor' 'libxinerama' 'python3'
         'ruby' 'x264' 'qt5-multimedia')
makedepends=('cmake' 'swig' 'doxygen')
# http://bazaar.launchpad.net/~openshot.code/libopenshot/trunk/files
source=("https://launchpad.net/libopenshot/0.0/$pkgver/+download/libopenshot-$pkgver.tar.gz")
md5sums=('d2a8d217ea07cee7d6de4bd542823d8e')

prepare () {
  cd ${pkgname}-${pkgver}
  
  sed -i 's|add_subdirectory(tests)|#add_subdirectory(tests)|' ${srcdir}/${pkgname}-${pkgver}/CMakeLists.txt
}   

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMAGICKCORE_HDRI_ENABLE=1
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -d ${pkgdir}/usr/bin
  install -m755 src/openshot-* ${pkgdir}/usr/bin
}
