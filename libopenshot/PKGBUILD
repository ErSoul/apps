
pkgname=libopenshot
pkgver=0.1.2
pkgrel=2
pkgdesc="Open-source project dedicated to delivering high quality video editing, animation, and playback solutions to the world."
arch=('x86_64')
url="http://www.openshot.org/"
license=('LGPL3')
depends=('libopenshot-audio' 'ffmpeg' 'imagemagick' 'libx11' 'libxcursor' 'libxinerama' 'python3'
         'ruby' 'x264' 'qt5-multimedia' 'zeromq')
makedepends=('cmake' 'swig' 'doxygen')
# http://bazaar.launchpad.net/~openshot.code/libopenshot/trunk/files
source=("https://launchpad.net/libopenshot/0.1/$pkgver/+download/libopenshot-$pkgver.tar.gz")
md5sums=('1491f454af8ef23b6c2f7f3e4ce39291')

prepare () {
  #cd ${pkgname}-${pkgver}
  
  sed -i 's|add_subdirectory(tests)|#add_subdirectory(tests)|' CMakeLists.txt
}   

build() {
  mkdir -p build
  cd build
  
  cmake ../ \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMAGICKCORE_HDRI_ENABLE=1 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  # install the example and simple player
  install -d ${pkgdir}/usr/bin
  install -m755 src/openshot-* ${pkgdir}/usr/bin
}
