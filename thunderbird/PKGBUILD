
pkgname=thunderbird
pkgver=52.8.0
pkgrel=1
pkgdesc="Standalone Mail/News reader"
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2'
         'gtk3' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng'
         'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss' 'shared-mime-info' 'sqlite'
         'startup-notification' 'libcanberra')
source=("https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/linux-x86_64/en-US/thunderbird-${pkgver}.tar.bz2"
        'thunderbird.desktop'
        'vendor.js')
sha256sums=('ddbb2eee2e1dccb1a9b9c25eb024b3c3e90b64f2561c3ac888cd313c39186b15'
            '8d0b499b3009934f66fd10975acee985fe64a8c0447918b309b8c5e6a5aa4372'
            'e4ea8e6788163d9f8db8f1f40023db3ea0a1358f9a4510169f2d4c4fe6a887ed')

package() {
  install -d ${pkgdir}/{usr/bin,opt}

  cp -r thunderbird/ ${pkgdir}/opt/$pkgname-$pkgver/
  cp vendor.js ${pkgdir}/opt/$pkgname-$pkgver/defaults/pref/
  
  ln -s /opt/$pkgname-$pkgver/thunderbird ${pkgdir}/usr/bin/thunderbird
  
  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 thunderbird/chrome/icons/default/default${i/x*/}.png ${pkgdir}/usr/share/icons/hicolor/$i/apps/thunderbird.png
  done
  
  install -Dm644 thunderbird.desktop ${pkgdir}/usr/share/applications/thunderbird.desktop
  
  rm -rf ${pkgdir}/opt/$pkgname-$pkgver/dictionaries/
  ln -sf /usr/share/hunspell/ ${pkgdir}/opt/$pkgname-$pkgver/dictionaries
  ln -sf /usr/share/hyphen/ ${pkgdir}/opt/$pkgname-$pkgver/hyphenation
}
