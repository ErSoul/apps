
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=solid
pkgver=${_kf5ver}
pkgrel=2
pkgdesc='Solid'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/solid'
license=('LGPL')
depends=('media-player-info' 'qt5-declarative' 'upower' 'udisks2')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "http://git.frugalware.org/gitweb/gitweb.cgi?p=frugalware-current.git;a=blob_plain;f=source/kf5/solid/sort_cores.patch")
md5sums=(`grep ${pkgname}-$_kf5ver.tar.xz ../kf5.md5 | cut -d" " -f1`
         'e086776ae503f25f40b558ff0dd8579a')

prepare () {
  cd $pkgname-$pkgver
  # patch from frugalware, make sure cpu's show correct
  patch -p1 -i $srcdir/sort_cores.patch
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

