
pkgname=unetbootin
pkgver=613
pkgrel=1
pkgdesc="Allows the creation of bootable Live USB drives."
url="http://unetbootin.github.io/"
license=('GPL3')
arch=('x86_64')
depends=('polkit' 'libpng' 'mtools' 'p7zip' 'qt' 'syslinux') 
groups=('system')
source=("https://github.com/unetbootin/unetbootin/archive/$pkgver.tar.gz")
md5sums=('946712f22f962b87f7a2e2ed0847c5f2')

build() {
  cd $srcdir/$pkgname-$pkgver/src/unetbootin
  lupdate $pkgname.pro
  lrelease $pkgname.pro
  qmake
  make
}

package() {
  # Executable 
  install -Dm755 $srcdir/$pkgname-$pkgver/src/unetbootin/$pkgname $pkgdir/usr/bin/$pkgname

  # Translations 
  mkdir -p $pkgdir/usr/share/$pkgname
  install -m644 $srcdir/$pkgname-$pkgver/src/unetbootin/$pkgname_*.qm $pkgdir/usr/share/$pkgname/

  install -Dm644 $srcdir/$pkgname-$pkgver/src/unetbootin/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 $srcdir/$pkgname-$pkgver/src/unetbootin/${pkgname}_48.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
