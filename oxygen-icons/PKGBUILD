
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

# original name (used for the source and pkgnames)
_origname=oxygen-icons

pkgname=('oxygen-icons' 'oxygen-icons-svg')
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="split package"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
install=oxygen-icons.install
makedepends=('pkg-config' 'cmake' 'automoc4')
source=($_mirror/${_origname}-$_kdever.tar.xz)
md5sums=(`grep ${_origname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

build() {
	cd $srcdir/$_origname-$pkgver
	
	cmake . -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=${_installprefix}

	make
}

package_oxygen-icons() {
	pkgdesc="Oxygen (Emot)Icons"
	groups=("kde" "kde-minimal" "kde-uninstall")
	
	cd $srcdir/$_origname-$pkgver
	
	make DESTDIR=$pkgdir install 
}

package_oxygen-icons-svg() {
	pkgdesc="Oxygen (Emot)Icons - SVG Sources"
	groups=("kde-uninstall")
	depends=("${_origname}")
	
	cd $srcdir/$_origname-${pkgver}
	find scalable -type f ! -name '*.sh' -exec \
		install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
