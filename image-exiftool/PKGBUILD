
# only build local, server for unknown reasons does not follow perl install dir
pkgname=image-exiftool
_pkgname=Image-ExifTool
pkgver=10.03
pkgrel=2
pkgdesc="Platform-independent Perl library plus a command-line application for reading, writing and editing meta information in a wide variety of files."
arch=('x86_64')
url="http://www.sno.phy.queensu.ca/~phil/exiftool/"
license=('GPL')
depends=('perl')
options=('!emptydirs')
source=("http://www.sno.phy.queensu.ca/~phil/exiftool/${_pkgname}-$pkgver.tar.gz")
md5sums=('18641484709d5a5a26512344d9a0c6bf')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  
  perl Makefile.PL INSTALLDIRS=vendor 
  make
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make test
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}