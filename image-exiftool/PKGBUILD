
# only build local, server for unknown reasons does not follow perl install dir
pkgname=image-exiftool
_pkgname=Image-ExifTool
pkgver=11.76
pkgrel=1
pkgdesc="Platform-independent Perl library plus a command-line application for reading, writing and editing meta information in a wide variety of files."
arch=('x86_64')
url="https://www.sno.phy.queensu.ca/~phil/exiftool/"
license=('GPL')
depends=('perl')
options=('!emptydirs')
source=("https://www.sno.phy.queensu.ca/~phil/exiftool/${_pkgname}-$pkgver.tar.gz")
sha1sums=('0672b7ca3bd6292c848f87d379088af235f70b74')

build() {
  cd ${_pkgname}-${pkgver}
  
  perl Makefile.PL INSTALLDIRS=vendor 
  make
}

check() {
  cd ${_pkgname}-${pkgver}

  make test
}

package() {
  cd ${_pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
