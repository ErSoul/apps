
pkgname=bluedevil
pkgver=2.0rc1
_pkgver=2.0-rc1
pkgrel=1
pkgdesc='KDE bluetooth framework'
arch=('x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kde-workspace' 'libbluedevil' 'obex-data-server' 'bluez')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgname}/${_pkgver}/src/${pkgname}-${_pkgver}.tar.xz")
md5sums=('d49a3037d29cf4ba9e0a0968a0139333')

build(){
  cd "${srcdir}"
  mkdir build
  
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  
  make DESTDIR="${pkgdir}" install
}
