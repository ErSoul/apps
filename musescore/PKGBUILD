
pkgname=musescore
pkgver=2.0.2
pkgrel=2
pkgdesc="Music notation and composition software written in Qt"
arch=('x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('qt5-webkit' 'qt5-svg' 'qt5-tools' 'openssl' 'pulseaudio' 'lame' 'portaudio-svn' 'jack')
makedepends=('cmake' 'doxygen' 'texlive-core')
groups=('multimedia')
source=("https://github.com/musescore/MuseScore/archive/v${pkgver}.tar.gz")
md5sums=('536632087474d701e3c74651869e1bf7')

prepare() {
  cd ${srcdir}/MuseScore-${pkgver}
  #sed -i 's|add_subdirectory(manual)|#add_subdirectory(manual)|' CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  
  cmake ../MuseScore-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=On
  make lrelease
  make manpages
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  # package mscore fonts so they can be used by other apps
  cd ../MuseScore-${pkgver}
  install -m755 -d ${pkgdir}/usr/share/fonts/mscore
  install -m644 fonts/mscoreTab.*tf $pkgdir/usr/share/fonts/mscore/
  install -m644 fonts/MuseJazz.ttf $pkgdir/usr/share/fonts/mscore/
  install -m644 fonts/mscore/mscore.otf $pkgdir/usr/share/fonts/mscore/
  install -m644 fonts/mscore/*.ttf $pkgdir/usr/share/fonts/mscore/
}
