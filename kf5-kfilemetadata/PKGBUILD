
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kf5-kfilemetadata
_pkgname=kfilemetadata
pkgver=4.96.0
pkgrel=1
pkgdesc="A library for extracting file metadata"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kfilemetadata'
license=('LGPL')
depends=('ebook-tools' 'karchive' 'exiv2' 'kservice' 'poppler-qt5' 'taglib' 'ffmpeg')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/${_pkgname}-$pkgver.tar.xz")
md5sums=('1b6c9973e439a6b78cf9827d03a0c889')

build() {
  mkdir -p build
  cd build
  
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
