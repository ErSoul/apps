
pkgname=fcitx
pkgver=4.2.8.5
pkgrel=3
pkgdesc="A Flexible Input Method Framework"
arch=('x86_64')
license=('GPL')
url="https://github.com/fcitx/"
depends=('pango' 'dbus-core' 'opencc' 'iso-codes' 'icu' 'libxkbfile' 'enchant' 'qt' 'gettext'
         'shared-mime-info' 'hicolor-icon-theme' 'libxinerama')
makedepends=('cmake' 'doxygen' 'gobject-introspection')
groups=('localization')
install=fcitx.install
source=("http://download.fcitx-im.org/$pkgname/$pkgname-${pkgver}_dict.tar.xz")
md5sums=('8cf81a003a02b2f16d04dfec6e82e90a')

build() {
  cd $srcdir/  
  mkdir -p $srcdir/build

  cd $srcdir/build
  cmake ../${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DSYSCONFDIR=/etc \
      -DENABLE_QT_IM_MODULE=On \
      -DENABLE_GTK2_IM_MODULE=Off \
      -DENABLE_GTK3_IM_MODULE=Off \
      -DENABLE_OPENCC=On \
      -DENABLE_GIR=Off
  make
}

package(){
    cd $srcdir/build
    
    make DESTDIR=$pkgdir install
}

