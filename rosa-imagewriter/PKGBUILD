 
pkgname=rosa-imagewriter
pkgver=2.6.2.0
pkgrel=1
pkgdesc="Tool for creating bootable installation USB flash drives"
arch=('x86_64')
url="https://abf.io/soft/rosa-imagewriter"
license=('GPLv2')
depends=('qt5-base')
makedepends=('qt5-tools')
source=("https://abf.io/soft/rosa-imagewriter/archive/${pkgname}-version-${pkgver}.tar.gz"
        'rosa-imagewriter.desktop'
        'icon-open.ico'
        'icon-refresh.ico'
        'logo-rosa.png')
md5sums=('e064feb95d6297d02d695861d4ea30d9'
         '4a783ba6bb97d4ef289f7fd0fbd03bc5'
         'fe59e2abf307d7e4a5f42cbe9bba4857'
         'bff77e8154b214396cc3e42eb433ccaf'
         'd587f0cb7c7104118f380589a942a42f')

build() { 
  cd ${pkgname}-version-${pkgver}
  cp ${srcdir}/icon-open.ico res/icon-open.ico
  cp ${srcdir}/icon-refresh.ico res/icon-refresh.ico
  cp ${srcdir}/logo-rosa.png res/logo-rosa.png
  
  /usr/lib/qt5/bin/qmake RosaImageWriter.pro
  /usr/lib/qt5/bin/lrelease RosaImageWriter.pro
  make 
}

package() {
  cd ${pkgname}-version-${pkgver}
  install -D -m755 RosaImageWriter ${pkgdir}/usr/bin/RosaImageWriter
  
  mkdir -p ${pkgdir}/usr/share/rosa-imagewriter/locale
  install -Dm644 lang/*.qm ${pkgdir}/usr/share/rosa-imagewriter/locale/
  
  install -Dm644 -p ../rosa-imagewriter.desktop ${pkgdir}/usr/share/applications/rosa-imagewriter.desktop
  install -Dm644 -p res/icon-rosa.svg ${pkgdir}/usr/share/pixmaps/icon-rosa.svg
  
  install -Dm644 doc/LICENSE.html ${pkgdir}/usr/share/licenses/LICENSE.html
}
