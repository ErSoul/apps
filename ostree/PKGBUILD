 
pkgname=ostree
_pkgname=libostree
pkgver=2019.3
_pkgver=2019.3
pkgrel=1
pkgdesc="Suite of command line tools that combines a git-like model for committing and downloading bootable filesystem trees"
url="https://github.com/ostreedev/ostree"
arch=('x86_64')
license=('GPL')
depends=('glib2' 'gpgme' 'fuse' 'libarchive' 'libsoup' 'mkinitcpio' 'util-linux' 'xz' 'zlib' 'openssl')
makedepends=('e2fsprogs' 'libxslt' 'python3')
source=("https://github.com/ostreedev/ostree/releases/download/v${_pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'ostree-mkinitcpio.conf'
        "https://github.com/ostreedev/ostree/commit/522d31b2d4604026f1c0a442887ca9d4b319e9cc.diff"
        "https://github.com/ostreedev/ostree/commit/d14472a7f04d02d392d2754cfb3cec881aadc704.diff"
        "https://github.com/ostreedev/ostree/commit/e49060c207fd201f71316bf174e1b467827abc7a.diff")
md5sums=('779e6f610882e6db96c2a7e26b037a00'
         '5b424fa5c03398f9ab3a60fd669a768a'
         'c0ba04e9b79e4ab77dba5ddd85991cc9'
         '8249476bcf8f01f70176dd6c3ad8eaf7'
         'a62d103eb26c047d55f7c8eb589172e5')

prepare() {
  cd ${_pkgname}-${_pkgver}
  
  cp ${srcdir}/ostree-mkinitcpio.conf src/boot/mkinitcpio/ostree-mkinitcpio.conf
  # https://bugs.kde.org/show_bug.cgi?id=411657
  patch -p1 -i $srcdir/522d31b2d4604026f1c0a442887ca9d4b319e9cc.diff
  patch -p1 -i $srcdir/d14472a7f04d02d392d2754cfb3cec881aadc704.diff
  patch -p1 -i $srcdir/e49060c207fd201f71316bf174e1b467827abc7a.diff
}

build() {
  cd ${_pkgname}-${_pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-mkinitcpio \
    --with-builtin-grub2-mkconfig \
    --with-openssl

  make
}

package() {
  cd ${_pkgname}-${_pkgver}
  
  make DESTDIR=${pkgdir} install
}
