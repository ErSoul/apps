
pkgname=octopi
pkgver=0.8.92
pkgrel=1
_commit=a45ca53c2e3281776caae632cd6b6bf325ae2e7c
pkgdesc="This is Octopi, a powerful Pacman frontend using Qt libs"
url="https://octopiproject.wordpress.com/"
arch=('x86_64')
license=('GPL3')
depends=('qt5-quickcontrols' 'pacman' 'pkgfile' 'knotifications' 'gist' 'alpm_octopi_utils')
#makedepends=('cmake' 'asciidoc')
groups=('system')
install=octopi.install
source=("https://github.com/aarnt/octopi/archive/${_commit}.zip"
        "images2.tar.xz")
md5sums=('c49ca28c172b27ff40fea7e23cf4717e'
         '858db0982326bcbc77526a7945d35bc7')

prepare() {
   cd ${pkgname}-${_commit}/
   rm -r resources/images
   mv -iv ${srcdir}/images/ resources/

   # enable the kstatus switch
   sed -e "s|# DEFINES += KSTATUS| DEFINES += KSTATUS|" -i notifier/octopi-notifier/octopi-notifier.pro
   # enable alpm backend
   sed -e "s|#ALPM_BACKEND|ALPM_BACKEND|" -i octopi.pro
}
         
build() {
   cd ${pkgname}-${_commit}/
   
   /usr/lib/qt5/bin/qmake octopi.pro
   make
   
   cd notifier/pacmanhelper
   /usr/lib/qt5/bin/qmake pacmanhelper.pro
   make
   cd ../..
   
   cd notifier/octopi-notifier
   /usr/lib/qt5/bin/qmake octopi-notifier.pro
   make
   cd ../..
   
   cd cachecleaner
   /usr/lib/qt5/bin/qmake octopi-cachecleaner.pro
   make
}

package() {
   cd ${pkgname}-${_commit}/
   
   install -D -m644 resources/images/octopi_green.png ${pkgdir}/usr/share/icons/octopi.png
   install -D -m644 resources/images/octopi_yellow.png ${pkgdir}/usr/share/icons/octopi_yellow.png
   
   install -D -m755 bin/octopi ${pkgdir}/usr/bin
   
   install -D -m644 octopi.desktop ${pkgdir}/usr/share/applications
   
   #Pacmanhelper service files
   install -D -m755 notifier/bin/pacmanhelper ${pkgdir}/usr/lib/octopi/pacmanhelper

   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacman.policy ${pkgdir}/usr/share/polkit-1/actions/org.octopi.pacman.policy
   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf ${pkgdir}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf
   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml ${pkgdir}/usr/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml
   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service ${pkgdir}/usr/share/dbus-1/system-services/org.octopi.pacmanhelper.service
   
   #Octopi-notifier file
   install -D -m755 notifier/bin/octopi-notifier ${pkgdir}/usr/bin
   install -D -m644 octopi-notifier.desktop ${pkgdir}/usr/share/applications
   
   #Octopi-cachecleaner file
   install -D -m755 cachecleaner/bin/octopi-cachecleaner ${pkgdir}/usr/bin
   install -D -m644 cachecleaner/octopi-cachecleaner.desktop ${pkgdir}/usr/share/applications

}
