
pkgname=octopi
pkgver=0.8.106
pkgrel=1
_commit=f7dbf97c5346117e7c432dda68f60cb5fb6bb4a3
pkgdesc="This is Octopi, a powerful Pacman frontend using Qt libs"
url="https://octopiproject.wordpress.com/"
arch=('x86_64')
license=('GPL2')
depends=('pacman' 'pkgfile' 'knotifications' 'gist' 'alpm_octopi_utils')
groups=('system')
#install=octopi.install
source=("https://github.com/aarnt/octopi/archive/${_commit}.zip"
        "images2.tar.xz")
md5sums=('c2448d46e02410a5948adf86cd353869'
         '858db0982326bcbc77526a7945d35bc7')

prepare() {
   cd ${pkgname}-${_commit}/
   rm -r resources/images
   mv -iv ${srcdir}/images/ resources/
   cp resources/images/octopi_green.png resources/images/octopi.png

   # enable the kstatus switch
   sed -e "s|#KSTATUS|KSTATUS|" -i notifier/octopi-notifier/octopi-notifier.pro
   # enable mirror-check
   #sed -e "s|//doMirrorCheck();|doMirrorCheck();|" -i src/mainwindow_refresh.cpp
   
   sed -e "s|pacman -U --force|pacman -U|" -i src/pacmanexec.cpp
}
         
build() {
   cd ${pkgname}-${_commit}
   
   /usr/lib/qt5/bin/qmake octopi.pro
   make
   
   cd notifier/pacmanhelper
   /usr/lib/qt5/bin/qmake pacmanhelper.pro
   make
   cd ../..
   
   cd notifier/octopi-notifier
   /usr/lib/qt5/bin/qmake octopi-notifier.pro
   make
   cd ../..
   
   cd cachecleaner
   /usr/lib/qt5/bin/qmake octopi-cachecleaner.pro
   make
}

package() {
   cd ${pkgname}-${_commit}
   make INSTALL_ROOT=${pkgdir} install
   
   cd notifier/pacmanhelper
   make INSTALL_ROOT=${pkgdir} install
   cd ../..
   
   cd notifier/octopi-notifier
   make INSTALL_ROOT=${pkgdir} install
   cd ../..
   
   cd cachecleaner
   make INSTALL_ROOT=${pkgdir} install
}
