
pkgname=octopi
pkgver=0.8.129
pkgrel=1
_commit=d36e79386469d9dd2709a9b97fc35382964a8168
pkgdesc="This is Octopi, a powerful Pacman frontend using Qt libs"
url="https://octopiproject.wordpress.com/"
arch=('x86_64')
license=('GPL2')
depends=('pacman' 'pkgfile' 'knotifications' 'alpm_octopi_utils' 'qtermwidget')
groups=('system')
#install=octopi.install
source=("https://github.com/aarnt/octopi/archive/${_commit}.zip"
        "images3.tar.xz")
md5sums=('fcf6ed84b2434af472836a37c218bb08'
         '3c3345539c8500d303f33f890fe733fe')

prepare() {
   cd ${pkgname}-${_commit}/
   rm -r resources/images
   mv -iv ${srcdir}/images/ resources/
   cp resources/images/octopi_green.png resources/images/octopi.png

   # enable the kstatus switch
   sed -e "s|#KSTATUS|KSTATUS|" -i notifier/octopi-notifier/octopi-notifier.pro
   
   sed -e "s|pacman -U --force|pacman -U|" -i src/pacmanexec.cpp
   #patch -p1 -i ${srcdir}/log2.diff
   #sed -e "s|return p.readAllStandardOutput();|return p.toLatin1();|" -i src/unixcommand.cpp
}
         
build() {
   cd ${pkgname}-${_commit}
   
   /usr/lib/qt5/bin/qmake octopi.pro
   make
   
   cd notifier/pacmanhelper
   /usr/lib/qt5/bin/qmake pacmanhelper.pro
   make
   cd ../..
   
   cd notifier/octopi-notifier
   /usr/lib/qt5/bin/qmake octopi-notifier.pro
   make
   cd ../..
   
   cd cachecleaner
   /usr/lib/qt5/bin/qmake octopi-cachecleaner.pro
   make
}

package() {
   cd ${pkgname}-${_commit}
   make INSTALL_ROOT=${pkgdir} install
   
   cd notifier/pacmanhelper
   make INSTALL_ROOT=${pkgdir} install
   cd ../..
   
   cd notifier/octopi-notifier
   make INSTALL_ROOT=${pkgdir} install
   cd ../..
   
   cd cachecleaner
   make INSTALL_ROOT=${pkgdir} install
}
