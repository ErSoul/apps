
pkgname=octopi
pkgver=0.8.93
pkgrel=1
_commit=e5b27d290b52589403f6d8e2fd1265310bffe336
pkgdesc="This is Octopi, a powerful Pacman frontend using Qt libs"
url="https://octopiproject.wordpress.com/"
arch=('x86_64')
license=('GPL2')
depends=('qt5-quickcontrols' 'pacman' 'pkgfile' 'knotifications' 'gist' 'alpm_octopi_utils')
#makedepends=('cmake' 'asciidoc')
groups=('system')
install=octopi.install
source=("https://github.com/demmm/octopi/archive/${_commit}.zip"
        "images2.tar.xz")
md5sums=('d84ff607d9bcd91c28485d915150b3e3'
         '858db0982326bcbc77526a7945d35bc7')

prepare() {
   cd ${pkgname}-${_commit}/
   rm -r resources/images
   mv -iv ${srcdir}/images/ resources/
   cp resources/images/octopi_green.png resources/images/octopi.png

   # enable the kstatus switch
   sed -e "s|# DEFINES += KSTATUS| DEFINES += KSTATUS|" -i notifier/octopi-notifier/octopi-notifier.pro
   # enable alpm backend
   sed -e "s|#ALPM_BACKEND|ALPM_BACKEND|" -i octopi.pro
}
         
build() {
   cd ${pkgname}-${_commit}
   
   /usr/lib/qt5/bin/qmake octopi.pro
   make
   
   cd notifier/pacmanhelper
   /usr/lib/qt5/bin/qmake pacmanhelper.pro
   make
   cd ../..
   
   cd notifier/octopi-notifier
   /usr/lib/qt5/bin/qmake octopi-notifier.pro
   make
   cd ../..
   
   cd cachecleaner
   /usr/lib/qt5/bin/qmake octopi-cachecleaner.pro
   make
}

package() {
   cd ${pkgname}-${_commit}
   make INSTALL_ROOT=${pkgdir} install
   
   cd notifier/pacmanhelper
   make INSTALL_ROOT=${pkgdir} install
   cd ../..
   
   cd notifier/octopi-notifier
   make INSTALL_ROOT=${pkgdir} install
   cd ../..
   
   cd cachecleaner
   make INSTALL_ROOT=${pkgdir} install
}
