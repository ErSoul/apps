
pkgname=darktable
pkgver=2.6.3
_pkgver=2.6.3
pkgrel=1
pkgdesc="Open source photography workflow application and RAW developer"
arch=('x86_64')
url="https://www.darktable.org/"
license=('GPL3')
depends=('exiv2' 'lcms2' 'lensfun' 'dbus-glib' 'curl' 'kwallet'
         'libgphoto2' 'openexr' 'sqlite' 'libxslt' 'libsoup' 'json-glib' 'flickcurl' 'librsvg'
         'colord' 'graphicsmagick' 'libwebp' 'gtk3' 'pugixml' 'openjpeg')
makedepends=('cmake' 'clang' 'intltool')
options=('!libtool')
source=("https://github.com/darktable-org/darktable/releases/download/release-${pkgver}/darktable-${_pkgver}.tar.xz")
sha256sums=('a518999c8458472edfc04577026ce5047d74553052af0f52d10ba8ce601b78f0')

build() {
  mkdir -p build
  cd build
  
  CXXFLAGS+=" -fpermissive"
  cmake ../${pkgname}-${_pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBINARY_PACKAGE_BUILD=1 \
      -DBUILD_USERMANUAL=False \
      -DDONT_USE_INTERNAL_LUA=Off \
      -DUSE_COLORD=On 
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
