
pkgname=darktable
pkgver=2.0.3
_pkgver=2.0.3
pkgrel=1
pkgdesc="Open source photography workflow application and RAW developer"
arch=('x86_64')
url="http://darktable.sf.net/"
license=('GPL3')
depends=('exiv2' 'intltool' 'lcms2' 'lensfun' 'libglade' 'dbus-glib' 'curl' 'kwallet'
         'libgphoto2' 'openexr' 'sqlite' 'libxslt' 'libsoup' 'json-glib' 'flickcurl' 'librsvg'
         'colord' 'graphicsmagick' 'sdl' 'libwebp' 'openjpeg' 'gtk3' 'pugixml')
makedepends=('cmake')
install=darktable.install
options=('!libtool')
source=("https://github.com/darktable-org/darktable/releases/download/release-$pkgver/darktable-$pkgver.tar.xz")
md5sums=('31fb4f4699454d23ac6a02cf64aafe00')

build() {
  mkdir -p build
  cd build
  
  CXXFLAGS+=" -fpermissive"
  cmake ../${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBINARY_PACKAGE_BUILD=1 \
      -DBUILD_USERMANUAL=False \
      -DDONT_USE_INTERNAL_LUA=Off \
      -DUSE_COLORD=On 
  make
}

package() {
  cd build
  
  make DESTDIR="$pkgdir" install
}
