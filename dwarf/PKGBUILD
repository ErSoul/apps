 
pkgname=dwarf
pkgver=20180527
pkgrel=1
pkgdesc="DWARF Debugging Information Format is of interest to programmers working on compilers and debuggers"
arch=('x86_64')
license=('GPL' 'LGPL')
url="https://www.prevanders.net/dwarf.html"
depends=('elfutils')
source=("https://www.prevanders.net/libdwarf-${pkgver}.tar.gz")
md5sums=('c35a5b2d7d07e1addfb951c23f5e91f7')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --enable-shared
  make dd
}

package() {
  cd ${pkgname}-${pkgver}/libdwarf

  install -dm755 ${pkgdir}/usr/lib
  install -m644 libdwarf.{a,so.1} ${pkgdir}/usr/lib/
  ln -s libdwarf.so.1 ${pkgdir}/usr/lib/libdwarf.so

  install -dm755 ${pkgdir}/usr/include/libdwarf
  install -m644 dwarf.h libdwarf.h ${pkgdir}/usr/include/libdwarf/

  cd ../../${pkgname}-${pkgver}/dwarfdump
  install -Dm755 dwarfdump ${pkgdir}/usr/bin/dwarfdump
  install -Dm644 dwarfdump.1 ${pkgdir}/usr/share/man/man1/dwarfdump.1
  install -Dm644 dwarfdump.conf ${pkgdir}/usr/lib/dwarfdump.conf
}
