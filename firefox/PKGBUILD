
pkgname=firefox
pkgdesc='Standalone web browser from mozilla.org'
url='http://www.mozilla.org/projects/firefox'
pkgver=49.0
pkgrel=1
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
groups=('network')
#install=firefox.install
depends=('desktop-file-utils' 'libxt' 'mime-types' 'nss' 'shared-mime-info' 'gtk2' 'networkmanager'
         'gst-plugins-good' 'gst-plugins-bad' 'gst-libav' 'mozilla-common' 'startup-notification'
         'dbus-glib' 'alsa-lib' 'hicolor-icon-theme' 'libvpx' 'icu' 'libevent' 'sqlite' 'hunspell'
         'pulseaudio')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake' 'xorg-server' 
             'inetutils' 'gconf' 'autoconf2.13' 'rust') #clang
optdepends=('freshplayerplugin: full flash support in firefox')
options=('!emptydirs' '!makeflags')
source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.xz"
        'mozconfig'
        'firefox.desktop'
        'firefox-install-dir.patch'
        'vendor.js')
md5sums=('b96eda40533dbd644eb2ae7f3da9f850'
         '62beb8e4eb0de0cd68b63c1f3be5d31c'
         '174a2a286e32d5c9a48f51a18ffa4e6c'
         'dbf14588e85812ee769bd735823a0146'
         '0d053487907de4376d67d8f499c5502b')

prepare() {
  cd ${pkgname}-${pkgver}

  cp ../mozconfig .mozconfig
  patch -p1 -i ${srcdir}/firefox-install-dir.patch

  mkdir ${srcdir}/path
  
  #sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure
}

build() {
  cd ${pkgname}-${pkgver}

  export PATH=${srcdir}/path:$PATH
  unset CPPFLAGS

  make -f client.mk build MOZ_PGO=0
}

package() {
  cd ${pkgname}-${pkgver}
  make -f client.mk DESTDIR=${pkgdir} INSTALL_SDK= install

  install -Dm644 ../vendor.js ${pkgdir}/usr/lib/firefox/browser/defaults/preferences/vendor.js

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png
  done
  install -Dm644 browser/branding/official/content/icon64.png ${pkgdir}/usr/share/icons/hicolor/64x64/apps/firefox.png
  install -Dm644 browser/branding/official/mozicon128.png ${pkgdir}/usr/share/icons/hicolor/128x128/apps/firefox.png
  install -Dm644 browser/branding/official/content/about-logo.png ${pkgdir}/usr/share/icons/hicolor/192x192/apps/firefox.png
  install -Dm644 browser/branding/official/content/about-logo@2x.png ${pkgdir}/usr/share/icons/hicolor/384x384/apps/firefox.png

  install -Dm644 ../firefox.desktop ${pkgdir}/usr/share/applications/firefox.desktop

  ln -sf firefox ${pkgdir}/usr/lib/firefox/firefox-bin
}
