
pkgname=firefox
pkgdesc='Standalone web browser from mozilla.org'
url='http://www.mozilla.org/projects/firefox'
pkgver=56.0
pkgrel=1
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
groups=('network')
#install=firefox.install
depends=('desktop-file-utils' 'libxt' 'mime-types' 'nss' 'shared-mime-info' 'gtk2' 'gtk3' 'networkmanager'
         'gst-plugins-good' 'gst-plugins-bad' 'gst-libav' 'mozilla-common' 'startup-notification'
         'dbus-glib' 'alsa-lib' 'hicolor-icon-theme' 'libvpx' 'libevent' 'sqlite' 'hunspell'
         'pulseaudio') # 'libjpeg-turbo' 'icu')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake' 'xorg-server' 
             'inetutils' 'gconf' 'autoconf2.13' 'rust' 'clang')
optdepends=('freshplayerplugin: full flash support in firefox')
options=('!emptydirs' '!makeflags')
source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.xz"
        'mozconfig'
        'firefox.desktop'
        'firefox-install-dir.patch'
        'vendor.js')
sha256sums=('0d8b7e91429f622c25509b7e73530807506ef3ff9f749d962251a35ed2bfed7e'
            'add74df2a5f3d98c22691ac2ab3ac08ec53dbfb703c9491b279a6d1c6a151485'
            '9f25db9a95d8fb29545e9864c69fae41b95a904e1580cf4b838e22fbdcbbdcc3'
            'd86e41d87363656ee62e12543e2f5181aadcff448e406ef3218e91865ae775cd'
            '4b50e9aec03432e21b44d18c4c97b2630bace606b033f7d556c9d3e3eb0f4fa4')

prepare() {
  cd ${pkgname}-${pkgver}

  cp ../mozconfig .mozconfig
  patch -p1 -i ${srcdir}/firefox-install-dir.patch

  mkdir ${srcdir}/path
  
  #sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure
}

build() {
  cd ${pkgname}-${pkgver}

  export PATH=${srcdir}/path:$PATH
  unset CPPFLAGS

  make -f client.mk build # MOZ_PGO=0
}

package() {
  cd ${pkgname}-${pkgver}
  make -f client.mk DESTDIR=${pkgdir} INSTALL_SDK= install

  install -Dm644 ../vendor.js ${pkgdir}/usr/lib/firefox/browser/defaults/preferences/vendor.js

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png
  done
  install -Dm644 browser/branding/official/content/icon64.png ${pkgdir}/usr/share/icons/hicolor/64x64/apps/firefox.png
  install -Dm644 browser/branding/official/mozicon128.png ${pkgdir}/usr/share/icons/hicolor/128x128/apps/firefox.png
  install -Dm644 browser/branding/official/content/about-logo.png ${pkgdir}/usr/share/icons/hicolor/192x192/apps/firefox.png
  install -Dm644 browser/branding/official/content/about-logo@2x.png ${pkgdir}/usr/share/icons/hicolor/384x384/apps/firefox.png

  install -Dm644 ../firefox.desktop ${pkgdir}/usr/share/applications/firefox.desktop

  ln -sf firefox ${pkgdir}/usr/lib/firefox/firefox-bin
}
