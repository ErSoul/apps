
pkgname=godot
pkgver=2.0.3
_pkgver=2.0.3-stable
pkgrel=1
pkgdesc="Full featured, open source, MIT licensed, game engine."
arch=('x86_64')
url="http://www.godotengine.org"
license=('MIT')
depends=('glu' 'libxcursor' 'alsa-lib' 'freetype2' 'mesa' 'libwebp' 'libxinerama')
makedepends=('scons')
source=("https://github.com/okamstudio/godot/archive/${_pkgver}.tar.gz"
        'godot.desktop'
        'icon.png')
md5sums=('7206db8cfb320f379b1e1bcd41eadb31'
         'dca7c5c5682bdc8cc83386034e0d7d07'
         'f756e85756a9cbc06a328414abf74585')

build() {
    cd ${pkgname}-${_pkgver}
    sed -n '/\/* Copyright/,/IN THE SOFTWARE./p' main/main.cpp | sed 's/\/\*//' | sed 's/\*\///' > LICENSE

    scons platform=x11
}

package() {
    cd ${pkgname}-${_pkgver}
    
    install -Dm755 bin/godot.x11.tools.64 ${pkgdir}/usr/bin/godot

    install -Dm644 ${srcdir}/godot.desktop ${pkgdir}/usr/share/applications/godot.desktop
    install -Dm644 ${srcdir}/icon.png ${pkgdir}/usr/share/pixmaps/godot.png  
    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/godot-git/LICENSE
}

