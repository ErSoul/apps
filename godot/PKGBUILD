
pkgname=godot
pkgver=3.1
_pkgver=3.1-stable
pkgrel=1
pkgdesc="Full featured, open source, MIT licensed, game engine."
arch=('x86_64')
url="http://www.godotengine.org"
license=('MIT')
depends=('alsa-lib' 'glu' 'libx11' 'libxcursor' 'libxinerama' 'libxrandr' 'freetype2'
         'mesa' 'libwebp' 'pulseaudio' 'openssl')
makedepends=('scons' 'clang')
source=("https://github.com/godotengine/godot/archive/${_pkgver}.tar.gz")
md5sums=('b59ffd636e46ba67e2a8caf06bf3216a')

build() {
    cd ${pkgname}-${_pkgver}
    # https://github.com/godotengine/godot/issues/7676
    #patch -p1 -i ${srcdir}/43bc27e2755f34017148097773da8a6988718aaf.diff

    scons platform=x11 \
          use_llvm=yes \
          target=release_debug
}

package() {
    cd ${pkgname}-${_pkgver}
    
    install -Dm755 bin/godot.x11.tools.64.llvm ${pkgdir}/usr/bin/godot

    install -Dm644 misc/dist/linux/org.godotengine.Godot.desktop ${pkgdir}/usr/share/applications/org.godotengine.Godot.desktop
    install -Dm644 icon.svg ${pkgdir}/usr/share/pixmaps/godot.svg  
    install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/godot/LICENSE.txt
}

