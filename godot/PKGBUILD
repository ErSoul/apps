
pkgname=godot
pkgver=2.1.2
_pkgver=2.1.2-stable
pkgrel=1
pkgdesc="Full featured, open source, MIT licensed, game engine."
arch=('x86_64')
url="http://www.godotengine.org"
license=('MIT')
depends=('alsa-lib' 'glu' 'libx11' 'libxcursor' 'libxinerama' 'libxrandr' 'freetype2'
         'mesa' 'libwebp' 'pulseaudio' 'openssl')
makedepends=('scons' 'clang')
source=("https://github.com/okamstudio/godot/archive/${_pkgver}.tar.gz"
        'godot.desktop'
        'icon.png')
md5sums=('d256cbbad929c591c11a0a6446cc741a'
         'dca7c5c5682bdc8cc83386034e0d7d07'
         'f756e85756a9cbc06a328414abf74585')

build() {
    cd ${pkgname}-${_pkgver}

    scons platform=x11 \
        use_llvm=yes \
        tools=no \
        target=release \
        bits=64
}

package() {
    cd ${pkgname}-${_pkgver}
    
    install -Dm755 bin/godot.x11.opt.64.llvm ${pkgdir}/usr/bin/godot

    install -Dm644 ../godot.desktop ${pkgdir}/usr/share/applications/godot.desktop
    install -Dm644 ../icon.png ${pkgdir}/usr/share/pixmaps/godot.png  
    install -Dm644 LICENSE.md ${pkgdir}/usr/share/licenses/godot/LICENSE.md
}

