
pkgname=polkit
pkgver=0.114
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('x86_64')
license=('LGPL')
url="https://www.freedesktop.org/wiki/Software/polkit"
depends=('glib2' 'pam' 'expat' 'systemd' 'js')
makedepends=('intltool' 'gobject-introspection' 'docbook-xsl')
provides=('consolekit')
options=('!libtool')
install=polkit.install
source=("https://www.freedesktop.org/software/polkit/releases/${pkgname}-${pkgver}.tar.gz"
        'polkit.pam')
md5sums=('93ff41874e7df8c62ed9e41893817f04'
         '6564f95878297b954f0572bc1610dd15')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/polkit-1 \
      --enable-libsystemd-login=yes \
      --disable-static \
      --with-os-type=KaOS
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  chown 102 ${pkgdir}/etc/polkit-1/rules.d
  chown 102 ${pkgdir}/usr/share/polkit-1/rules.d
  
  install -m644 ${srcdir}/polkit.pam ${pkgdir}/etc/pam.d/polkit-1 
}
