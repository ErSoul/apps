
pkgname=libfilezilla
pkgver=0.17.1
pkgrel=2
pkgdesc="Small C++ library, offering a typesafe, multi-threaded event system and process handling for spawning child processes."
arch=('x86_64')
url="https://lib.filezilla-project.org/"
license=('GPL2')
depends=('gcc' 'nettle')
source=("https://download.filezilla-project.org/libfilezilla/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('c9c88bf3cbeb5284c3e55571b40b07f780cb23aeb802284d385a2564bf46396875a34739f9819b0c3f89923950f19a13f6454940434a987bb91b2f23b898f774')

build() {
  cd ${pkgname}-${pkgver}
  sed -e "s|<uint8_t> random_bytes(size_t size)|<uint8_t> FZ_PUBLIC_SYMBOL random_bytes(size_t size)|" -i lib/libfilezilla/util.hpp

  ./configure --prefix=/usr \
    --disable-static 
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
