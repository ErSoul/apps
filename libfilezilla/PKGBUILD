
pkgname=libfilezilla
pkgver=0.13.1
pkgrel=1
pkgdesc="Small C++ library, offering a typesafe, multi-threaded event system and process handling for spawning child processes."
arch=('x86_64')
url="https://lib.filezilla-project.org/"
license=('GPL2')
depends=('gcc')
source=("https://download.filezilla-project.org/libfilezilla/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('d6a0cb14d8008a945df94081a64d3b8857c5e45ce32275eddccae931434eb98a586bfcd8430adad09ee4e17ad42bb83abfed3a2d227d98714b1ba1720bbd2e7f')

build() {
  cd ${pkgname}-${pkgver}
  sed -e "s|<uint8_t> random_bytes(size_t size)|<uint8_t> FZ_PUBLIC_SYMBOL random_bytes(size_t size)|" -i lib/libfilezilla/util.hpp

  ./configure --prefix=/usr \
    --disable-static 
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
