
pkgname=liri-browser
pkgver=0.3.1
pkgrel=1
pkgdesc="Minimalistic Google's Material Design web browser, relies on QtWebEngine, the brand new Qt web renderer based on Chromium"
url="http://liri-browser.github.io/#discover"
arch=('x86_64')
license=('LGPL')
depends=('qtwebengine' 'qt5-multimedia' 'vlc' 'qml-material')
makedepends=('git')
#source=("https://github.com/liri-browser/liri-browser/archive/v${pkgver}.tar.gz"
source=("git://github.com/liri-browser/liri-browser"
        "git://github.com/pierremtb/liri-player"
        "git://github.com/RSATom/QmlVlc"
        "git://github.com/RSATom/ya-libvlc-wrapper"
        "git://github.com/RSATom/libvlc-sdk"
        'liri-browser.desktop')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         '13a5ba458d3ae8135eeb9126ee329633')

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule.dependencies/liri-player.url $srcdir/liri-player
  git submodule update

  cd dependencies/liri-player
  git submodule init
  git config submodule.dependencies/QmlVlc.url $srcdir/QmlVlc
  git submodule update

  cd dependencies/QmlVlc
  git submodule init
  git config submodule.libvlc_wrapper.url $srcdir/ya-libvlc-wrapper
  git submodule update

  cd libvlc_wrapper
  git submodule init
  git config submodule.libvlc-sdk.url $srcdir/libvlc-sdk
  git submodule update
  
  sed -i -e 's|target.path = /opt/$${TARGET}/bin|target.path = /usr/bin|' ${srcdir}/${pkgname}/deployment.pri
  sed -i -e 's|https://www.google.de|https://www.google.com|' ${srcdir}/${pkgname}/src/qml/BaseApplication.qml
  #KaOS colors
  sed -i -e 's|primaryColor: "#F44336"|primaryColor: "#31A3DD"|' ${srcdir}/${pkgname}/src/qml/BrowserWindow.qml
  sed -i -e 's|primaryDarkColor: "#D32F2F"|primaryDarkColor: "#2397D2"|' ${srcdir}/${pkgname}/src/qml/BrowserWindow.qml
  sed -i -e 's|accentColor: "#FF5722"|accentColor: "#31C0DD"|' ${srcdir}/${pkgname}/src/qml/BrowserWindow.qml
  sed -i -e 's|darkThemeColor : "#f3f3f3"|darkThemeColor : "#CCCCCC"|' ${srcdir}/${pkgname}/src/qml/BrowserWindow.qml
}

build() {
   cd ${pkgname}

   /usr/lib/qt5/bin/qmake 
   make
}

package() {
   cd ${pkgname}
   make INSTALL_ROOT=$pkgdir install
   
   mkdir -p $pkgdir/usr/share/applications

  for i in 16x16 22x22 32x32 48x48 64x64 128x128 256x256; do
    install -Dm644 icons/liri-browser.png $pkgdir/usr/share/icons/hicolor/$i/apps/liri-browser.png
    install -Dm644 icons/liri-browser-ubuntu.png $pkgdir/usr/share/icons/hicolor/$i/apps/liri-browser-ubuntu.png
  done
  
  install -m755 $srcdir/liri-browser.desktop $pkgdir/usr/share/applications/liri-browser.desktop
}
