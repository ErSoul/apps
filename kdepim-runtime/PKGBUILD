
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdepim-runtime
pkgver=${_kdever}
pkgrel=2
arch=('x86_64')
pkgdesc="KDE Pim Runtime"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
depends=('akonadi-mime' 'akonadi-notes' 'akonadi-contacts' 'knotifyconfig' 'kross' 'knewstuff' 'kcmutils' 'kdelibs4support' 
         'kconfig' 'kio' 'kitemmodels' 'kcodecs' 'accountsqt' 'signonqt' 'shared-mime-info' 
         'akonadi' 'kmime' 'kmailtransport' 'kidentitymanagement' 'kcontacts' 'kalarmcal' 'kcalcore' 
         'kcalutils' 'kmbox' 'kpimtextedit' 'kimap' 'syndication' 'akonadi-calendar' 'kaccounts-integration'
         'libkgapi' 'libkolab' 'qt5-webkit') # not used 'libkfbapi' disable 'qtspeech', might cause crashes
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
options=('debug')
conflicts=('kf5-kdepim-runtime')
replaces=('kf5-kdepim-runtime')
groups=('kde' 'kdepim' 'kde-uninstall')
source=($_mirror/${pkgname}-${_kdever}.tar.xz
        "https://github.com/KDE/kdepim-runtime/commit/c25dcbd8852879a2f7ee102ef0f6555583b3dad7.diff"
        "https://github.com/KDE/kdepim-runtime/commit/acb496c86356d2b2fb6df23c108436db879a58ab.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'cd9e2e6f43e37d80208f36f3f5d93280'
         '402065cc23d9b8c5b88c0cab57f1d4b4')
#source=("git://anongit.kde.org/kdepim-runtime.git")      
#md5sums=('SKIP')

prepare() {
    cd $pkgname-${pkgver}
  
    patch -p1 -i $srcdir/c25dcbd8852879a2f7ee102ef0f6555583b3dad7.diff
    patch -p1 -i $srcdir/acb496c86356d2b2fb6df23c108436db879a58ab.diff
    #sed -i -e 's/Libkolabxml 1.2 QUIET/Libkolabxml 1.1 QUIET/' resources/CMakeLists.txt
}

build() {    
    mkdir -p build
    cd build

    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
        -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
        #-DBUILD_TESTING=OFF \
        #-DENABLE_TESTING=OFF
    make
}

package() {
    cd ${srcdir}/build

    make DESTDIR=${pkgdir} install
}
