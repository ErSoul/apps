
pkgname=qgis
_pkgname=QGIS
pkgver=2.16.2
_commit=979e1a2d03db4ff18dc6f9510b4f7576df338e27
pkgrel=1
pkgdesc='Quantum GIS is a Geographic Information System (GIS) that supports vector, raster & database formats'
url='http://qgis.org/'
license=('GPL')
arch=('x86_64')
depends=('libmariadbclient' 'postgresql-libs' 'sqlite' 'jasper' 'curl' 'python2' 'giflib' 'gsl' 'grass'
         'cfitsio' 'qwt' 'proj' 'gdal' 'geos' 'libspatialite' 'spatialindex' 'python2-qscintilla-qt5' 
         'postgis' 'fcgi' 'qt5-svg' 'qt5-script' 'python2-six' 'python2-psycopg2' 'python2-dateutil'
         'python2-pytz' 'python2-jinja' 'pyqt5-python2' 'qca-qt5') # 'qt5-webkit')
makedepends=('cmake'  'netcdf' 'sip' 'qt5-tools') # 'qca')
conflicts=('pyqt-python2' 'python2-qscintilla' 'pyqt-python3')
#replaces=('pyqt-python2' 'python2-qscintilla' 'pyqt-python3')
#source=("http://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2")
source=("https://github.com/qgis/QGIS/archive/${_commit}.zip")
md5sums=('0adf076ab12da37c6a86e3de451eaa4f') 
         
prepare() {
     cd ${_pkgname}-${_commit}
     #patch -p1 -i "$srcdir/canvas-qt-flicker.patch" 
     sed -i -e 's|QtCrypto qt4/QtCrypto Qca-qt5/QtCrypto|QtCrypto qt4/QtCrypto qt5/Qca-qt5/QtCrypto|' cmake/FindQCA.cmake
     sed -i -e 's|set(QWT_LIBRARY_NAMES qwt-qt5 qwt6-qt5)|set(QWT_LIBRARY_NAMES qwt qwt6)|' cmake/FindQwt.cmake
}

 build() {
      mkdir -p build
      cd build
     
      cmake ../${_pkgname}-${_commit} \
        -DENABLE_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DGRASS_PREFIX=/opt/grass \
        -DQGIS_MANUAL_SUBDIR=share/man \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DWITH_INTERNAL_SIX=FALSE \
        -DWITH_INTERNAL_DATEUTIL=FALSE \
        -DWITH_INTERNAL_JINJA2=FALSE \
        -DWITH_INTERNAL_PYTZ=FALSE \
        -DWITH_QWTPOLAR=FALSE \
        -DWITH_QTWEBKIT=FALSE \
        -DENABLE_QT5=TRUE \
        -DENABLE_PYTHON3=FALSE
     
      make
}

package() {
      cd build
     
      make DESTDIR=${pkgdir} install
     
      # install some freedesktop.org compatibility
      install -D -m644 $srcdir/${_pkgname}-${_commit}/debian/qgis.desktop $pkgdir/usr/share/applications/qgis.desktop
      install -m644 $srcdir/${_pkgname}-${_commit}/debian/qbrowser.desktop $pkgdir/usr/share/applications
      install -dm755 $pkgdir/usr/share/pixmaps $pkgdir/usr/share/mimelnk/application
      for mime in $srcdir/${_pkgname}-${_commit}/debian/mime/application/*.desktop
        do install -m644 ${mime} $pkgdir/usr/share/mimelnk/application
      done
      ln -s /usr/share/qgis/images/icons/qgis-icon.png $pkgdir/usr/share/pixmaps/qgis-icon.png
      ln -s /usr/share/qgis/images/icons/qgis-mime-icon.png $pkgdir/usr/share/pixmaps/qgis-mime-icon.png
     
      python2 -m compileall -q $pkgdir
}
