
pkgname=qgis
pkgver=2.14.1
pkgrel=1
pkgdesc='Quantum GIS is a Geographic Information System (GIS) that supports vector, raster & database formats'
url='http://qgis.org/'
license=('GPL')
arch=('x86_64')
depends=('libmariadbclient' 'postgresql-libs' 'sqlite' 'jasper' 'curl' 'python3' 'giflib' 'gsl' 'grass'
         'cfitsio' 'qwt' 'proj' 'gdal' 'geos' 'libspatialite' 'spatialindex' 'python3-qscintilla-qt5' 
         'postgis' 'fcgi' 'qt5-svg' 'qt5-script' 'python3-six' 'python3-psycopg2' 'python3-dateutil'
         'python3-pytz' 'python3-jinja' 'pyqt5-python3' 'qca-qt5') # 'qt5-webkit'
makedepends=('cmake'  'netcdf' 'sip' 'qt5-tools' 'qca')
install=${pkgname}.install
source=("http://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2")
md5sums=('60e0708650303f28fdfe84b555c41c18') 
         
prepare() {
     cd $pkgname-$pkgver
     #cd "$srcdir/QGIS"
     #patch -p1 -i "$srcdir/canvas-qt-flicker.patch" 
     #sed -i -e 's|set(QCA_LIBRARY_NAMES qca-qt5 qca2-qt5)|set(QCA_LIBRARY_NAMES qca qca2)|' cmake/FindQCA.cmake
     sed -i -e 's|set(QWT_LIBRARY_NAMES qwt-qt5 qwt6-qt5)|set(QWT_LIBRARY_NAMES qwt qwt6)|' cmake/FindQwt.cmake
}

 build() {
      mkdir build
      cd build
     
      cmake ../${pkgname}-${pkgver} \
        -DENABLE_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DGRASS_PREFIX=/opt/grass \
        -DQGIS_MANUAL_SUBDIR=share/man \
        -DENABLE_PYTHON3=ON \
        -DPYTHON_LIBRARY=/usr/lib/libpython3.5mu.so \
        -DPYTHON_EXECUTABLE=/usr/bin/python3 \
        -DPYTHON_SITE_PACKAGES_DIR=/usr/lib/python3.5/site-packages \
        -DPYTHON_INCLUDE_PATH=/usr/include/python3.5 \
        -DWITH_INTERNAL_SIX=FALSE \
        -DWITH_INTERNAL_DATEUTIL=FALSE \
        -DWITH_INTERNAL_JINJA2=FALSE \
        -DWITH_INTERNAL_PYTZ=FALSE \
        -DWITH_QWTPOLAR=FALSE \
        -DWITH_QTWEBKIT=FALSE \
        -DENABLE_QT5=TRUE 
     
      make
}

package() {
      cd build
     
      make DESTDIR=${pkgdir} install
     
      # create a more user-friendly application name link
      ln -s /usr/bin/qgis $pkgdir/usr/bin/quantum-gis
     
      # install some freedesktop.org compatibility
      install -D -m644 "$srcdir"/"${pkgname}-${pkgver}"/debian/qgis.desktop \
                          "$pkgdir"/usr/share/applications/qgis.desktop
      install -m644 "$srcdir"/"${pkgname}-${pkgver}"/debian/qbrowser.desktop "$pkgdir"/usr/share/applications
      install -dm755 $pkgdir/usr/share/pixmaps $pkgdir/usr/share/mimelnk/application
      for mime in $srcdir/${pkgname}-${pkgver}/debian/mime/application/*.desktop
        do install -m644 ${mime} $pkgdir/usr/share/mimelnk/application
      done
      ln -s /usr/share/qgis/images/icons/qgis-icon.png $pkgdir/usr/share/pixmaps/qgis-icon.png
      ln -s /usr/share/qgis/images/icons/qgis-mime-icon.png $pkgdir/usr/share/pixmaps/qgis-mime-icon.png
     
      # enable qgis for python by setting PYTHONPATH
      install -Dm644 ${srcdir}/qgis.sh ${pkgdir}/etc/profile.d/qgis.sh
}
