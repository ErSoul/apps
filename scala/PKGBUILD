
pkgname=scala
pkgver=2.10.4
pkgrel=1
pkgdesc="Acronym for Scalable Language, running on the JVM. Java and Scala classes"
arch=('x86_64')
url="http://www.scala-lang.org"
license=('custom')
depends=('sh' 'java-environment')
source=("http://www.scala-lang.org/files/archive/scala-$pkgver.tgz")
md5sums=('11854f09c35312d9f6966e163e6e436c')

package() {
  cd ${srcdir}/scala-${pkgver}

  install -d ${pkgdir}/usr/{bin,share} ${pkgdir}/usr/share/man/man1 ${pkgdir}/usr/share/scala/{bin,lib}
  cp -r {lib,misc} ${pkgdir}/usr/share/scala/
  cp -r man ${pkgdir}/usr/share/
  install -m 755 bin/{fsc,scala,scalac,scalap,scaladoc} ${pkgdir}/usr/share/scala/bin
  install -D -m0644 doc/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  ln -s ../share/scala/bin/fsc ${pkgdir}/usr/bin/fsc
  ln -s ../share/scala/bin/scala ${pkgdir}/usr/bin/scala
  ln -s ../share/scala/bin/scalac ${pkgdir}/usr/bin/scalac
  ln -s ../share/scala/bin/scalap ${pkgdir}/usr/bin/scalap
  ln -s ../share/scala/bin/scaladoc ${pkgdir}/usr/bin/scaladoc
}