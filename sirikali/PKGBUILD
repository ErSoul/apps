
pkgname=sirikali
pkgver=1.3.3
pkgrel=1
pkgdesc="GUI application that manages ecryptfs, cryfs, encfs, gocryptfs and securefs based encrypted folders."
url="https://mhogomchungu.github.io/sirikali/"
arch=('x86_64')
license=('GPL') 
depends=('encfs' 'libgcrypt' 'libpwquality'  'libxkbcommon' 'qt5-base' 'kwalletmanager' 'libsecret')
makedepends=('cmake')
conflicts=('cryfs-gui')
replaces=('cryfs-gui')
source=("https://github.com/mhogomchungu/sirikali/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz") 
sha256sums=('2ab44411f9aee06d0d7ca4592fbf8d830078b99f6bc60469648c5b0d8baefa21')

build() {
  mkdir -p build
  cd build
  
  cmake ../SiriKali-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DCMAKE_BUILD_TYPE=release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DQT5=true \
    -DNOSECRETSUPPORT=false \
    -DNOKDESUPPORT=false \
    -DINTERNAL_LXQT_WALLET=false
  make
}

package() {
  cd build

  make DESTDIR=$pkgdir install
}

