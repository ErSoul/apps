
pkgname=sirikali
pkgver=1.3.7
pkgrel=1
pkgdesc="GUI application that manages ecryptfs, cryfs, encfs, gocryptfs and securefs based encrypted folders."
url="https://mhogomchungu.github.io/sirikali/"
arch=('x86_64')
license=('GPL') 
depends=('encfs' 'libgcrypt' 'libpwquality'  'libxkbcommon' 'qt5-base' 'kwalletmanager' 'libsecret')
makedepends=('cmake')
conflicts=('cryfs-gui')
replaces=('cryfs-gui')
source=("https://github.com/mhogomchungu/sirikali/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'cmake.diff') 
sha256sums=('ae450eb9d0747f50fd4eb17a3315ac556a8498f123d4a079e44fde37ffa61e64'
            'faae1d0b60662de12fe9f343da24130393cbb595a419ac62949e06adc092c746')

prepare() {
  cd SiriKali-${pkgver}
  
  patch -p1 -i ${srcdir}/cmake.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../SiriKali-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DCMAKE_BUILD_TYPE=release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DQT5=true \
    -DNOSECRETSUPPORT=false \
    -DNOKDESUPPORT=false \
    -DINTERNAL_LXQT_WALLET=true
  make
}

package() {
  cd build

  make DESTDIR=$pkgdir install
}

