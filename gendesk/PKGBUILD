
pkgname=gendesk
pkgver=0.6.2
pkgrel=1
pkgdesc="Utility to generate .desktop files and download icons"
arch=('x86_64')
url="http://roboticoverlords.org/gendesk/"
license=('MIT')
depends=('glibc')
makedepends=('go' 'mercurial')
source=("http://roboticoverlords.org/$pkgname/$pkgname-$pkgver.tar.xz"
        "http://roboticoverlords.org/images/default.png")
md5sums=('b8633c55c930cfe481d6143a472a148f'
         '76e9160caf563badd49c13f34cd137cf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export GOPATH="$srcdir"
  go get code.google.com/p/goconf/conf
  go get github.com/xyproto/textgui

  msg2 "Compiling..."
  GOPATH="$srcdir" go build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 "$pkgname-$pkgver" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/default.png" "$pkgdir/usr/share/pixmaps/default.png"
  install -Dm644 "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
