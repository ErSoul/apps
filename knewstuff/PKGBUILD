
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=knewstuff
pkgver=${_kf5ver}
pkgrel=2
pkgdesc='KNewStuff'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/knewstuff'
license=('LGPL')
depends=('kf5-attica' 'kconfigwidgets' 'kio')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/KDE/knewstuff/commit/e98a2e4b6f0042c10a31023ef37a3534832135b6.diff"
        "https://github.com/KDE/knewstuff/commit/7b59f50ad99dc988fa60a3949ac6d458c42945ba.diff")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '0e3613beae4e63183afec70a0616b4af'
         '88d8660c1a17d5edfc50b9c087024754')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix ghns crashes with 5.36
  patch -p1 -i ${srcdir}/e98a2e4b6f0042c10a31023ef37a3534832135b6.diff
  patch -p1 -i ${srcdir}/7b59f50ad99dc988fa60a3949ac6d458c42945ba.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
