pkgname=mixxx
pkgver=2.0.0
_pkgver=release-2.0.0
pkgrel=10
pkgdesc="Free, open source software for digital DJ'ing."
arch=('x86_64')
url='http://www.mixxx.org'
license=('GPL')
depends=('faad2' 'fftw' 'libid3tag' 'libmad' 'libmodplug' 'libogg' 'libshout' 'libsndfile' 'libusb' 
         'portaudio' 'portmidi' 'protobuf' 'qt5-svg' 'qt5-script' 'qtwebkit-tp' 'rubberband'
         'taglib' 'wavpack' 'opusfile' 'chromaprint')
makedepends=('scons' 'glu' 'qt5-tools')
source=("https://github.com/mixxxdj/mixxx/archive/${_pkgver}.tar.gz"
        'chromaprint4.2.diff')
md5sums=('784e9aafc133c6a31d13b32904d18d39'
         '2e47cfa56e6501ac9c7c05a31073324f')

build() {
  cd ${pkgname}-${_pkgver}
  # chromaprint 1.4.2 needed patch, from:
  # https://github.com/mixxxdj/mixxx/commit/5b252f36ce091d3a0d56afd10ad4085747c6708f
  patch -p1 -i ${srcdir}/chromaprint4.2.diff
  
  CXXFLAGS="-std=c++11"

  scons qtdir=/usr/lib/qt5 prefix=/usr faad=1 qt5=1 wv=1 modplug=1 opus=1
}

package() {
  cd ${pkgname}-${_pkgver}
  
  CXXFLAGS="-std=c++11"

  scons qtdir=/usr/lib/qt5 prefix=/usr qt5=1 install_root=${pkgdir}/usr install
}
