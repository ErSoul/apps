
pkgname=kdev-php
pkgver=4.90.91
pkgrel=2
pkgdesc="PHP plugin for Kdevelop"
arch=('x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop' 'kdevelop-pg-qt' 'threadweaver' 'ktexteditor' 'ki18n')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
groups=('programming')
options=('!makeflags')
replaces=('kdevelop-php' 'kdevelop-php-docs')
conflicts=('kdevelop-php' 'kdevelop-php-docs')
source=("http://download.kde.org/unstable/kdevelop/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
md5sums=('c6063e86356d687f430c7cb441b57767')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
