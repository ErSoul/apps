
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdesu
pkgver=${_kdever}
pkgrel=2
pkgdesc='Provides functionality for building GUI front ends for (password asking) console mode programs'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kdesu'
license=('LGPL')
depends=('kservice' 'kpty' 'libx11')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  # http://kde.6490.n7.nabble.com/KDESU-again-td1394699.html
  chown :nobody "${pkgdir}"/usr/lib/libexec/kf5/kdesud
  chmod g+s "${pkgdir}"/usr/lib/libexec/kf5/kdesud
}
