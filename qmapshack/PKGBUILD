
pkgname=qmapshack
pkgver=1.11.1
pkgrel=2
pkgdesc="Plan your next outdoor trip or to visualize and archive all the GPS recordings, next generation of the QLandkarte GT application"
arch=('x86_64')
url="https://bitbucket.org/maproom/qmapshack/wiki/Home"
license=('GPL')
depends=('gdal' 'gpsd' 'libdmtx' 'libexif' 'libmariadbclient' 'mesa' 'postgresql-libs'
         'shared-mime-info' 'unixodbc' 'glu' 'qt5-webkit' 'proj' 'qt5-script' 
         'routino' 'quazip')
makedepends=('cmake' 'extra-cmake-modules' 'qt5-tools')
groups=('utils')
source=("https://bitbucket.org/maproom/qmapshack/downloads/${pkgname}-${pkgver}.tar.gz"
        'qnetworkreply.diff')
md5sums=('ec970e65c3cd951ae9fd2c7a54b54f37'
         '44ee3225ac367080af003bde480a98d5')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/qnetworkreply.diff
}

build() {
  mkdir -p build
  cd build
 
  cmake ../${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release 
      
  make
}

package(){
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
