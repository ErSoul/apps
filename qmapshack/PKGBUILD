
pkgname=qmapshack
pkgver=1.13.0
pkgrel=1
pkgdesc="Plan your next outdoor trip or to visualize and archive all the GPS recordings, next generation of the QLandkarte GT application"
arch=('x86_64')
url="https://bitbucket.org/maproom/qmapshack/wiki/Home"
license=('GPL')
depends=('gdal' 'gpsd' 'libdmtx' 'libexif' 'mariadb' 'mesa' 'postgresql-libs'
         'shared-mime-info' 'unixodbc' 'glu' 'qtwebengine' 'proj' 'qt5-script' 
         'routino' 'quazip')
makedepends=('cmake' 'extra-cmake-modules' 'qt5-tools')
groups=('utils')
source=("https://bitbucket.org/maproom/qmapshack/downloads/${pkgname}-${pkgver}.tar.gz"
        'proj.patch'
        'quazip.patch')
md5sums=('68eacea307dd95562ca160a7c721da72'
         '1a2192dfa8b63fce503a59d6917918a5'
         'cbdc25ae8121f347ddfedc10501d589e')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/proj.patch
  patch -p1 -i ${srcdir}/quazip.patch
  #sed -i 's|PROJ4|PROJ|' CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
 
  cmake ../${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release 
      
  make
}

package(){
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
