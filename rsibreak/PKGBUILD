
pkgname=rsibreak
pkgver=0.12.3
_pkgver=0.12
pkgrel=1
pkgdesc="RSIBreak helps in preventing Repetitive Strain Injury by reminding you to take a break."
url="https://userbase.kde.org/RSIBreak"
arch=('x86_64')
license=('GPL')
depends=('kconfig' 'kconfigwidgets' 'kcrash' 'kdbusaddons' 'kiconthemes' 'ki18n' 'kidletime'
         'knotifications' 'knotifyconfig' 'ktextwidgets' 'kxmlgui' 'kwindowsystem')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/stable/rsibreak/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('a27ef8361c50c90efa05578a947563a8')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
