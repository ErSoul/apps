
pkgbase=thunderbird-i18n
pkgver=24.0b1
pkgrel=1
pkgdesc="Language pack for Thunderbird"
arch=('x86_64')
url="http://www.mozilla.com/"
license=('MPL' 'GPL')
depends=("thunderbird>=$pkgver")

_languages=(
  'es-ES  "Spanish (Spain)"'
  'nl     "Dutch"'
  'pt-BR  "Brazilian Portuguese"'
)

pkgname=()
source=()
md5sums=('35ba541bb1a33228f52670dcd31f5b2e'
         'ab736c0a30c6dcb58032fb3189795268'
         '1780d27237f4f7a2fea96d37fa8a9c46')
_url=https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/xpi

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=thunderbird-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("thunderbird-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Thunderbird"
  install -Dm644 thunderbird-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/thunderbird/extensions/langpack-$1@thunderbird.mozilla.org.xpi"
}
