
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kde-cli-tools
pkgver=${_kdever}
pkgrel=3
pkgdesc='Tools based on KDE Frameworks 5 to better interact with the system'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kde-cli-tools'
license=('LGPL')
depends=('kdesu' 'kdelibs4support' 'kcmutils' 'kemoticons' 'kinit' 'kitemmodels')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kf5.md5 | cut -d" " -f1`)
#source=("git://anongit.kde.org/kde-cli-tools.git")
#md5sums=('SKIP')

build() {
  mkdir -p build
  sed -e "s/PO_FILES //" -i ${srcdir}/$pkgname-$pkgver/po/*/CMakeLists.txt
  cd build
  
  cmake ../${pkgname}-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
  
  # place kdesu in bin too, use libkde-runtime for kdesu, until kf5 kdesu actually works
  ln -sf /usr/lib/libexec/kdesu ${pkgdir}/usr/bin/
}

