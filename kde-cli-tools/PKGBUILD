
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kde-cli-tools
pkgver=4.97.0
pkgrel=3
pkgdesc='Tools based on KDE Frameworks 5 to better interact with the system'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kde-cli-tools'
license=('LGPL')
depends=('kdesu' 'kdelibs4support' 'kcmutils' 'kemoticons' 'kinit' 'kitemmodels')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kde-runtime')
replaces=('kde-runtime')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('24cce6770b976dc306f4f4dba4ee33dc')
#source=("git://anongit.kde.org/kde-cli-tools.git")
#md5sums=('SKIP')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
  
  # place kdesu in bin too
  ln -sf /usr/lib/libexec/kdesu ${pkgdir}/usr/bin/
}

