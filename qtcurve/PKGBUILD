
pkgname=qtcurve
pkgver=1.8.18.17
_commit=a249871e95d4d64a0f2a7c95020d54b1435a4e93
pkgrel=1
arch=('x86_64')
license=('GPL')
pkgdesc='The QtCurve style engine for Qt5 and Plasma 5.'
url='https://projects.kde.org/projects/playground/base/qtcurve'
depends=('karchive' 'kconfig' 'kconfigwidgets' 'frameworkintegration' 'ki18n' 'kdelibs4support'
         'kio' 'kwidgetsaddons' 'kwindowsystem' 'kxmlgui' 'kguiaddons' 'kiconthemes' 
         'libx11' 'qt5-x11extras' 'qt5-svg')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("https://github.com/KDE/qtcurve/archive/${_commit}.zip"
        "https://raw.githubusercontent.com/RJVB/macstrop/master/kde/QtCurve/files/patch-qt5-dbus-fixes-by-debian.diff"
        'DBus_crashing.diff')
md5sums=('d2408319e89c446908cefe759625abdb'
         '0dfe16fe9ce0340b2a7b56f37067fec4'
         '5f5d3393e02c59933494edd0b5e28184')

prepare() {
  cd ${pkgname}-${_commit}
  
  patch -p0 -i ${srcdir}/DBus_crashing.diff
}

build() {
    mkdir -p build
    cd build
    
    cmake ../${pkgname}-${_commit} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_GTK2=false \
        -DQTC_QT5_ENABLE_KDE=true \
        -DQTC_QT4_ENABLE_KDE=false
    make
}

package() {
    cd build
	
    make DESTDIR=${pkgdir} install
}
