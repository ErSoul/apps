
pkgname=obs-studio
pkgver=24.0.4
pkgrel=1
pkgdesc="Free and open source software for video recording and live streaming."
arch=('x86_64')
url="https://obsproject.com/"
license=('GPL2')
depends=('ffmpeg' 'jansson' 'libxinerama' 'libxkbcommon' 'qt5-x11extras'
         'libxcomposite' 'x264' 'pulseaudio' 'vlc' 'jack')
makedepends=('cmake')
source=("https://github.com/obsproject/obs-studio/archive/${pkgver}.tar.gz")
md5sums=('7ae6f9221395894190f149ba623aeab8')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUNIX_STRUCTURE=1 \
    -DOBS_VERSION_OVERRIDE=${pkgver}
  make 
}

package() {
  cd build

  make install DESTDIR=${pkgdir}
}
