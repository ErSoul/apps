
pkgname=gimp
pkgver=2.9.2
_pkgver=2.9
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
arch=('x86_64')
url="http://www.gimp.org/"
license=('GPL3' 'LGPL3')
depends=('pygtk' 'lcms' 'libxpm' 'libwmf' 'librsvg' 'libmng' 'libgudev' 'dbus-glib' 
         'jasper' 'libexif' 'gegl' 'desktop-file-utils' 'hicolor-icon-theme' 'gexiv2')
makedepends=('intltool' 'webkitgtk2' 'poppler-glib' 'alsa-lib' 'iso-codes' 'curl' 'ghostscript')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'webkitgtk2: for the help browser or web-page plug-ins'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support')
options=('!libtool' '!makeflags')
install=gimp.install
source=("http://download.gimp.org/pub/gimp/v${_pkgver}/gimp-${pkgver}.tar.bz2"
        'linux.gpl')
md5sums=('aa8a846a497e3328c5b7d2fd33f5cf63'
         'bb27bc214261d36484093e857f015f38')

build() {
  cd "${srcdir}/gimp-${pkgver}"
  #sed -e "s|gegl-0.2 >= 0.2.0|gegl-0.3 >= 0.3.0|" -i ${srcdir}/gimp-${pkgver}/configure
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-mp \
    --enable-gimp-console \
    --enable-python \
    --without-aa 
  make
}

package() {
  cd "${srcdir}/gimp-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  sed -i 's|#![ ]*/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/lib/gimp/2.0/plug-ins/*.py
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  ln -sf gimp-console-${pkgver}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -sf gimprc-${pkgver}.5.gz "${pkgdir}/usr/share/man/man5/gimprc.5.gz"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
  ln -s gimp-${pkgver}.1.gz "${pkgdir}/usr/share/man/man1/gimp.1.gz"
}
