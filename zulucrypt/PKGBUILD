
pkgname=zulucrypt
_pkgname=zuluCrypt
pkgver=5.6.0
pkgrel=1
pkgdesc="Simple,feature rich and powerful solution for hard drives encryption."
url="http://mhogomchungu.github.io/zuluCrypt/"
arch=('x86_64')
license=('GPL') 
depends=('cryptsetup' 'libpwquality' 'libsecret' 'libxkbcommon' 'qt5-base' 'kwalletmanager')
makedepends=('cmake')
source=("https://github.com/mhogomchungu/zuluCrypt/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.xz") 
sha256sums=('80eca8681d12a8760aa7f1af455c393b19dcb02c7f58cf8ed555a074db094f1e')

build() {
  mkdir -p build
  cd build
  
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DCMAKE_BUILD_TYPE=release \
    -DLIB_SUFFIX=lib \
    -DNOGUI=OFF \
    -DQT5=ON \
    -DHOMEMOUNTPREFIX=OFF \
    -DNOKDE=OFF 
  make
}

package() {
  cd build

  make DESTDIR=$pkgdir install
}

