
pkgname=ktorrent
pkgver=4.3.76
pkgrel=1
ktorver=4.3.1
pkgdesc="A BitTorrent program for KDE"
arch=('x86_64')
url="http://ktorrent.pwsp.net/"
license=('GPL2')
depends=('kdelibs4support' 'ki18n' 'kio' 'kxmlgui' 'knotifications' 'knotifyconfig' 'solid'
         'kconfig' 'kcoreaddons' 'kross' 'kcmutils' 'sonnet' 'kdbusaddons' 'kcrash' 
         'taglib' 'libktorrent' 'plasma-workspace')
makedepends=('extra-cmake-modules' 'boost' 'kdoctools' 'git')
install=ktorrent.install
groups=('network')
options=('libtool')
#source=("http://ktorrent.pwsp.net/downloads/${ktorver}/ktorrent-${ktorver}.tar.bz2"
source=('git://anongit.kde.org/ktorrent#branch=frameworks')
md5sums=('SKIP')

prepare() {
  cd ${pkgname}
  sed -i 's|Exec=ktorrent %i -caption %c %u|Exec=ktorrent -qwindowtitle %c %u|g' ktorrent/org.kde.ktorrent.desktop
  sed -i 's|ui.rc DESTINATION ${DATA_INSTALL_DIR}|ui.rc DESTINATION ${KXMLGUI_INSTALL_DIR}|g' plugins/*/CMakeLists.txt
  sed -i 's|TARGETS ktorrent_app ${INSTALL_TARGETS_DEFAULT_ARGS}|TARGETS ktorrent_app ${KDE_INSTALL_TARGETS_DEFAULT_ARGS}|g' ktorrent/CMakeLists.txt
}

build() {
  mkdir build
  cd build
  
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
