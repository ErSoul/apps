
pkgname=htop
pkgver=1.0.3
pkgrel=2
pkgdesc="Interactive process viewer"
arch=('x86_64')
url="http://hisham.hm/htop/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
groups=('utils')
source=("http://hisham.hm/htop/releases/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('e768b9b55c033d9c1dffda72db3a6ac7')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	sed -i 's|ncursesw/curses.h|curses.h|' RichString.h RichString.c configure 
	sed -i 's|python|python2|' scripts/MakeHeader.py
	
	./configure --prefix=/usr \
	       --enable-unicode \
	       --enable-openvz \
	       --enable-vserver 
	make 
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	make DESTDIR=${pkgdir} install 
}
