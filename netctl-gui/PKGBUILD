
pkgname=netctl-gui
pkgver=1.4.6
pkgrel=1
pkgdesc="Qt5 graphical interface for netctl, including a widget"
arch=('x86_64')
url="http://arcanis.name/projects/netctl-gui"
license=('GPL3')
depends=('plasma-framework' 'netctl' 'wpa_supplicant')
makedepends=('extra-cmake-modules' 'qt5-tools')
install=netctl-gui.install
source=("https://github.com/arcan1s/netctl-gui/releases/download/V.${pkgver}/${pkgname}-${pkgver}-src.tar.xz"
        'language.diff')
md5sums=('aed674e4c465d849c2044f9464c2671c'
         '6e3af42b7fe95c0505dd274d226f7e42')

prepare() {
  cd ${srcdir}/${pkgname}
  
  sed -i 's|WPASUP_PATH=/usr/bin/wpa_supplicant|WPASUP_PATH=/usr/sbin/wpa_supplicant|' ${srcdir}/${pkgname}/helper/netctl-gui.conf
  sed -i 's|WPACLI_PATH=/usr/bin/wpa_cli|WPACLI_PATH=/usr/sbin/wpa_cli|' ${srcdir}/${pkgname}/helper/netctl-gui.conf
  patch -p1 -i $srcdir/language.diff
}

build() {
  cd ${srcdir}
  mkdir build
  cd build
  
  cmake ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_GUI:BOOL=1 \
    -DBUILD_LIBRARY:BOOL=1 \
    -DBUILD_HELPER:BOOL=1 \
    -DBUILD_DATAENGINE:BOOL=1 \
    -DBUILD_PLASMOID:BOOL=1 \
    -DUSE_CAPABILITIES:BOOL=1 
  make
}


package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}

