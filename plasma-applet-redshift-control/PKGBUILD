
pkgname=plasma-applet-redshift-control
pkgver=1.0.11
pkgrel=1
_commit=b45d454a6d0e8db022dd963cc50a59ccbda7f1c7
pkgdesc='Plasmoid for enabling and disabling redshift process, providing simple settings and mouse-wheel screen temperature control.'
arch=('x86_64')
url='http://kde-apps.org/content/show.php/Redshift+Control?content=170746'
license=('GPL')
depends=('plasma-desktop' 'redshift')
makedepends=('extra-cmake-modules')
conflicts=('redshift-plasmoid')
replaces=('redshift-plasmoid')
source=("https://github.com/kotelnik/plasma-applet-redshift-control/archive/v${pkgver}.tar.gz")
md5sums=('665312d82a3bc40655a582454f7c5cbb')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  make
}

package() {
  cd build
  
  make DESTDIR="$pkgdir" install
}
