
pkgname=plasma-applet-redshift-control
pkgver=1.0.4
pkgrel=1
_commit=59e8355e220329fc17ded0cbb104e3f13d42eaf2
pkgdesc='Plasmoid for enabling and disabling redshift process, providing simple settings and mouse-wheel screen temperature control.'
arch=('x86_64')
url='http://kde-apps.org/content/show.php/Redshift+Control?content=170746'
license=('GPL')
depends=('plasma-desktop' 'redshift')
makedepends=('extra-cmake-modules')
conflicts=('redshift-plasmoid')
replaces=('redshift-plasmoid')
source=("https://github.com/kotelnik/plasma-applet-redshift-control/archive/${_commit}.zip")
md5sums=('8299ea700a7bce6c73b06d13edfa5c4c')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  make
}

package() {
  cd build
  
  make DESTDIR="$pkgdir" install
}
