
pkgname=dnscrypt-proxy
pkgver=1.4.3
pkgrel=1
pkgdesc="Tool for securing communications between a client and a DNS resolver"
arch=('x86_64')
url="http://dnscrypt.org/"
license=('custom:ISC')
depends=('libsodium')
backup=(etc/conf.d/dnscrypt-proxy)
source=("http://download.dnscrypt.org/$pkgname/$pkgname-$pkgver.tar.gz" 
	'conf.d.file' 
	'dnscrypt-proxy.service')
md5sums=('54d172236a8f321fb5689ff81767f1ba'
         '2a6dbb39c63c5089ef4fa80e81bb2bd3'
         'ed1fa560eac727d7ae0c568b66aa8c4e')

build() {
  cd $pkgname-$pkgver
  
  CFLAGS="$CFLAGS -fPIC" ./configure --prefix=/usr 
  make -j2
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir"/{usr/share/{licenses,doc}/$pkgname,etc/conf.d,usr/lib/systemd/system}
  install -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 AUTHORS NEWS README README.markdown "$pkgdir"/usr/share/doc/$pkgname
  install -m 644 "$srcdir"/conf.d.file "$pkgdir"/etc/conf.d/$pkgname
  install -m 644 "$srcdir"/dnscrypt-proxy.service "$pkgdir"/usr/lib/systemd/system
}

