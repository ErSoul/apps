
pkgname=dnscrypt-proxy
pkgver=1.5.0
pkgrel=1
pkgdesc="Tool for securing communications between a client and a DNS resolver"
arch=('x86_64')
url="http://dnscrypt.org/"
license=('custom:ISC')
depends=('libsodium')
backup=(etc/conf.d/dnscrypt-proxy)
source=("http://download.dnscrypt.org/$pkgname/$pkgname-$pkgver.tar.gz" 
	'conf.d.file' 
	'dnscrypt-proxy.service'
	"https://github.com/jedisct1/dnscrypt-proxy/commit/a66823e1ad8d8eb6d36a5d2e050b20765089754d.patch")
md5sums=('a54491e89e0f88c6531a5643686d910e'
         '2a6dbb39c63c5089ef4fa80e81bb2bd3'
         'ed1fa560eac727d7ae0c568b66aa8c4e'
         '9a10c56662b9b973cced91d803c84a00')

build() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/a66823e1ad8d8eb6d36a5d2e050b20765089754d.patch
  
  ./configure --prefix=/usr --with-systemd 
  make -j2
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir"/{usr/share/{licenses,doc}/$pkgname,etc/conf.d,usr/lib/systemd/system}
  install -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 AUTHORS NEWS README README.markdown "$pkgdir"/usr/share/doc/$pkgname
  install -m 644 "$srcdir"/conf.d.file "$pkgdir"/etc/conf.d/$pkgname
  install -m 644 "$srcdir"/dnscrypt-proxy.service "$pkgdir"/usr/lib/systemd/system
}

