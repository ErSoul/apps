
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=khotkeys
pkgver=${_plasmaver}
pkgrel=3
pkgdesc='KHotKeys'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/khotkeys'
license=('LGPL')
depends=('plasma-workspace')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('plasma5')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"
        'dbus_action.diff')
md5sums=(`grep ${pkgname}-$_plasmaver.tar.xz ../kf5.md5 | cut -d" " -f1`
         'b72371a922acbf0b812d92cd509595a9')
#source=("git://anongit.kde.org/khotkeys.git")
#md5sums=('SKIP')

prepare() {
  cd ${pkgname}-$pkgver
  
  patch -p1 -i $srcdir/dbus_action.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
