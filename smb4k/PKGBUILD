
pkgname=smb4k
pkgver=2.0.0
_commit=3056bb01733ec009d789e831636e4fe487d79598
pkgrel=1
pkgdesc='Advanced network neighborhood browser and Samba share mounting utility for KDE.'
url='https://sourceforge.net/p/smb4k/home/Home/'
arch=('x86_64')
license=('GPL')
depends=('kconfig' 'kauth' 'kiconthemes' 'solid' 'kwidgetsaddons' 'ki18n' 'kcompletion' 'kcoreaddons'
         'kio' 'knotifications' 'kxmlgui' 'kjobwidgets' 'kwallet' 'kdbusaddons' 'kparts' 'kconfigwidgets'
         'kwindowsystem' 'plasma-framework' 'samba')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kdenetwork' 'kde-uninstall' 'network')
source=("https://sourceforge.net/projects/smb4k/files/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'po.patch')
md5sums=('7252df36cb3695daac416689a836741a'
         '6a95a47f6dddb16d63a0768c92b7d675')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/po.patch
  #sed -i -e 's|Unknown|UnknownNetworkItem|' core/smb4kbasicnetworkitem.h
}

build() {
  mkdir -p build 
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
