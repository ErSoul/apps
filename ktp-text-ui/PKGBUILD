
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ktp-text-ui
pkgver=${_kdever}
pkgrel=1
pkgdesc="Telepathy text chat handler"
arch=('x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('ktp-common-internals' 'qtwebengine')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'boost')
groups=('kde-telepathy')
options=('debug')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        '0001-Port-to-QWebEngine.patch')
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'ceb8d4d058efa368a531757985a90785')
         
prepare() {
  cd $pkgname-${pkgver}
  
  patch -p1 -i $srcdir/0001-Port-to-QWebEngine.patch
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
