

pkgname=konversation
pkgver=1.7.5
_pkgver=1.7.5
pkgrel=1
pkgdesc="A user friendly IRC client for KDE"
arch=('x86_64')
url="http://konversation.kde.org"
license=('GPL2' 'FDL')
depends=('kfilemetadata' 'kidletime' 'kcmutils' 'krunner' 'kemoticons' 
         'knotifyconfig' 'phonon-qt5' 'baloo-widgets' 'qca-qt5')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('communication')
source=("https://download.kde.org/stable/${pkgname}/${_pkgver}/src/${pkgname}-${_pkgver}.tar.xz")
sha256sums=('60bf7533062b5fc63a37105461b4776437f4e24859e8ddaed1d48c4ba1470940')

prepare() {
  cd ${pkgname}-${_pkgver}
  # set default channel to KaOS
  sed -i -e 's|setName(QStringLiteral("#konversation")|setName(QStringLiteral("#kaosx")|' src/config/preferences.cpp
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
