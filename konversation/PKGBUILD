
pkgname=konversation
pkgver=1.5rc2
_pkgver=1.5-rc2
pkgrel=2
pkgdesc="A user friendly IRC client for KDE"
arch=('x86_64')
url="http://konversation.kde.org"
depends=('kde-runtime' 'kdepimlibs' 'python2')
makedepends=('automoc4' 'cmake' 'pkg-config' 'docbook-xsl')
license=('GPL2' 'FDL')
groups=('communication')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgname}/${_pkgver}/src/${pkgname}-${_pkgver}.tar.xz")
md5sums=('58348a42bd7463f4f215308289a8d8fc')

build() {
  cd "${srcdir}"
  mkdir build
  
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  
  make DESTDIR="${pkgdir}" install
}
