pkgname=docker
pkgver=1.8.2
pkgrel=1
pkgdesc='Pack, ship and run any application as a lightweight container'
arch=('x86_64')
url='http://www.docker.io/'
license=('Apache')
depends=('bridge-utils' 'iproute2' 'device-mapper' 'sqlite' 'systemd')
makedepends=('git' 'go' 'btrfs-progs' 'go-md2man')
optdepends=('btrfs-progs: btrfs backend support')
options=('!strip')
install=$pkgname.install
source=("https://github.com/docker/docker/archive/v$pkgver.tar.gz"
        'docker.sysusers'
        "https://github.com/docker/docker/commit/6f89a8ee1ba6b598dc1f76e829d6db6f830cdb8e.patch")
md5sums=('4faf25b356900f3e7599783ad4565e69'
         '4324edeb9adc210a2c22f44eb4cb8a74'
         '3eff94e59391015f020686d57ffb3c06')

prepare() {
    cd $pkgname-$pkgver
    # fix build with go 1.5
    patch -p1 -i $srcdir/6f89a8ee1ba6b598dc1f76e829d6db6f830cdb8e.patch
}
         
build() {
  cd $pkgname-$pkgver
  export AUTO_GOPATH=1
  
  hack/make.sh dynbinary

  man/md2man-all.sh
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 "bundles/$pkgver/dynbinary/docker-$pkgver" "$pkgdir/usr/bin/docker"
  install -Dm755 "bundles/$pkgver/dynbinary/dockerinit-$pkgver" "$pkgdir/usr/lib/docker/dockerinit"

  install -Dm644 'contrib/completion/bash/docker' "$pkgdir/usr/share/bash-completion/completions/docker"
  install -Dm644 'contrib/completion/zsh/_docker' "$pkgdir/usr/share/zsh/site-functions/_docker"
  install -Dm644 'contrib/completion/fish/docker.fish' "$pkgdir/usr/share/fish/completions/docker.fish"

  install -Dm644 'contrib/init/systemd/docker.service' "$pkgdir/usr/lib/systemd/system/docker.service"
  install -Dm644 'contrib/init/systemd/docker.socket' "$pkgdir/usr/lib/systemd/system/docker.socket"
  install -Dm644 "$srcdir/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  install -Dm644 'contrib/syntax/vim/syntax/dockerfile.vim' "$pkgdir/usr/share/vim/vimfiles/syntax/dockerfile.vim"
  install -Dm644 'contrib/syntax/vim/ftdetect/dockerfile.vim' "$pkgdir/usr/share/vim/vimfiles/ftdetect/dockerfile.vim"

  install -dm755 "$pkgdir/usr/share/man"
  mv man/man* "$pkgdir/usr/share/man"
}

