
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=wacomtablet
pkgver=2.9.81
#_pkgver=3.0.0~beta1
_commit=753615b14ae3c0e670360185c83cc9e525ffdee5
pkgrel=1
pkgdesc="Implements a GUI for the Wacom Linux Drivers and extends it with profile support to handle 
         different button / pen layouts per profile."
arch=('x86_64')
url='https://github.com/KDE/wacomtablet'
license=('GPL' 'LGPL' 'FDL')
depends=('kcoreaddons' 'ki18n' 'kglobalaccel' 'kconfig' 'kxmlgui' 'kwidgetsaddons' 'kwindowsystem' 'knotifications'
         'kdbusaddons' 'plasma-framework' 'xf86-input-wacom')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kdegraphics' 'kde-uninstall')
#source=("https://dl.opendesktop.org/api/files/download/id/1466630714/114856-wacomtablet-${_pkgver}.tar.xz")
source=("https://github.com/KDE/wacomtablet/archive/${_commit}.zip")
md5sums=('2640d7ba655c29778bdf35a89e3d6ea9')

prepare() {
   cd ${pkgname}-${_commit}
   #patch -p1 -i ${srcdir}/CMakeLists.diff
   sed -i -e 's|MacroOptionalAddSubdirectory|ECMOptionalAddSubdirectory|' CMakeLists.txt
   sed -i -e 's|macro_optional_add_subdirectory|ecm_optional_add_subdirectory|' CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
