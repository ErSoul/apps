
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kde-baseapps
pkgver=16.07.70
_commit=13cfde0ec413c4a44e7c8d19e119f2861bf8fd59
pkgrel=1
pkgdesc="Collection of applications used for file and Internet browsing"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/applications/kde-baseapps'
license=('LGPL')
depends=('karchive' 'kbookmarks' 'kcoreaddons' 'kconfig' 'kconfigwidgets' 'kdbusaddons' 'kio' 'kparts' 'kcmutils'
         'kdelibs4support' 'kdesu' 'kactivities' 'kded' 'khtml' 'knewstuff' 'kcrash' 'ki18n'
         'tidyhtml' 'dolphin') # 'qtspeech')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('kf5-kde-baseapps' 
           'kde-baseapps-dolphin' 
           'kde-baseapps-konqueror' 
           'kde-baseapps-konq-plugins' 
           'kde-baseapps-kwrite')
replaces=('kf5-kde-baseapps' 
          'kde-baseapps-dolphin' 
          'kde-baseapps-konqueror' 
          'kde-baseapps-konq-plugins' 
          'kde-baseapps-kwrite')
provides=('kf5-kde-baseapps' 
          'kde-baseapps-dolphin' 
          'kde-baseapps-konqueror' 
          'kde-baseapps-konq-plugins' 
          'kde-baseapps-kwrite')
#source=("http://download.kde.org/unstable/plasma/$pkgver/src/${_pkgname}-$pkgver.tar.xz")
source=("https://github.com/KDE/kde-baseapps/archive/${_commit}.zip")
md5sums=('82d645c046bfe3e832596cdc7de11562')

prepare() {
  cd $pkgname-${_commit}
  sed -i -e 's|#include <buffio.h>|#include <tidybuffio.h>|' konq-plugins/validators/tidy_validator.cpp
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
  
  # konqueror kf5 is not usable
  rm -r ${pkgdir}/usr/share/applications/konquerorsu.desktop
  rm -r ${pkgdir}/usr/share/applications/konqbrowser.desktop
  rm -r ${pkgdir}/usr/bin/konqueror
}
