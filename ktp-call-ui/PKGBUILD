
pkgname=ktp-call-ui
pkgver=0.9.91
pkgrel=1
pkgdesc="Telepathy call handler "
arch=('x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kconfig' 'kdeclarative' 'kiconthemes' 'ki18n' 'knotifications' 'kxmlgui' 'kcmutils'
         'ktp-common-internals' 'telepathy-farstream'
         'telepathy-mission-control' 'telepathy-gabble' 'gst-plugins-base'
         'gst-plugins-good' 'qt5-gstreamer' 'gst-libav')
makedepends=('boost' 'extra-cmake-modules' 'kdoctools' 'git')
groups=('kde-telepathy' 'communication')
#source=("ftp://mirrors.mit.edu/kde/stable/kde-telepathy/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
source=("git://anongit.kde.org/ktp-call-ui.git")
md5sums=('SKIP')

prepare() {
  cd $pkgname
  sed -i -e 's|#include <phonon/Global>|#include <phonon4qt5/phonon/Global>|' ${srcdir}/${pkgname}/libktpcall/private/phonon-integration.h
  sed -i -e 's|#include <phonon/ObjectDescription>|#include <phonon4qt5/phonon/ObjectDescription>|' ${srcdir}/${pkgname}/libktpcall/private/phonon-integration.h
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
