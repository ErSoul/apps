
pkgname=ktp-common-internals
pkgver=0.8.80
pkgrel=1
pkgdesc="Common libraries for telepathy-kde"
arch=('x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kdelibs' 'telepathy-qt' 'telepathy-mission-control' 'kdeutils-kwalletmanager'
         'telepathy-logger-qt' 'libkpeople' 'libotr')
makedepends=('cmake' 'automoc4' 'boost' 'doxygen')
groups=('kde-telepathy')
source=("ftp://mirrors.mit.edu/kde/unstable/kde-telepathy/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('e654c9fda412921c2e5f2a515ce62a20')

build() {
  cd "${srcdir}"
  mkdir build
  
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  
  make DESTDIR="${pkgdir}" install
}
