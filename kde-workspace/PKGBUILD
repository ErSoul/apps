
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

# Default wallpaper included in kde-workspace pkg
_default_wp="Elarun"

pkgname="kde-workspace"
arch=('x86_64')
pkgver=4.11.6
pkgrel=2
pkgdesc="KDE Workspace"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
install=${pkgname}.install
depends=('polkit-kde-agent' "kdepimlibs>=${_kdever}" "kde-runtime>=${_kdever}" 'qimageblitz' 'mesa'
         "kactivities>=${_kdever}" 'libxcomposite' 'xcb-util-image' 'xcb-util-renderutil'
         'libxss' 'lm_sensors' 'libxklavier' 'libxft' 'oxygen-icons' 'xkit' 'libxdamage' 'xorg-utils'
         'libxrandr' 'libxres' 'libdmtx' 'libqalculate' 'prison' 'qrencode' "kdepim-runtime>=${_kdever}"
         "kde-base-artwork>=${_kdever}" 'xcb-util-keysyms' 'xcb-util-wm' 'pciutils' 'about-distro')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'networkmanager' 'bluez' "kdebindings-pykde4>=${_kdever}"
	    'oxygen-icons' "kde-runtime>=${_kdever}" 'gpsd' 'samba' 'xf86-input-wacom'
	    'libraw1394' 'libdmtx' 'docbook-xsl' 'prison' 'qrencode' 'boost')  
optdepends=("appmenu-qt:         Global menu support"
            "kdebindings-pykde4: Python Support for Plasma"
	    "kdeedu-marble:      Marble Wallpaper Plugin"
	    "gpsd:               Plasma geolocation support"
	    "ntp:                Time/date sync support"
	    "opentp:             Optional time/date sync support"
	    "xf86-input-wacom:   Wacom tablet support"
	    "kscreen:            Multiple monitors support")
provides=('kcm_tablet=1.1.3')
conflicts=('kcm_tablet')
groups=("kde" "kde-uninstall" "kde-minimal")
backup=('usr/share/config/kdm/kdmrc'
        'etc/pam.d/kscreensaver')


source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
	"$_mirror/kde-wallpapers-$_kdever.tar.xz"
	'mishaaq-kcm_touchpad-00370b5.tar.gz'
        "http://kde-apps.org/CONTENT/content-files/114856-wacomtablet-2.0.tar.bz2"
        "http://kde-apps.org/CONTENT/content-files/117639-containmentaction-favorites-0.2.tar.gz"
        'kde.pam'
        'kde-np.pam'
        'kscreensaver.pam'
	'kdm.service'
	'os-branding.tar.xz'

        # core patches
        '01_kdm_zsh_profile.patch'
        #02_sane_env_and_shutdown_path.patch
        '03_plasma_menubutton_branding.patch'
        #04_plasma_kickoff_url.patch
	#06_kickoff_default_favourites.patch
	'07_always_show_kickoff_subtext.patch'
	'09_enable_start-shutdown-scripts.patch'

	# restart xserver on logout
	'fix_terminate-server.patch'
	# "fix" some sytemsettings modules that need root access
	'fix_root-only-kcms.patch')

md5sums=('13bbcd739cddd3311d5695d80eed04e6'
         `grep kde-wallpapers-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1` # kde-wallpapers
         'f355a658d2e9267fdf4e8d8f88038bcf'
         '23dd14baa3ebac8fc3010ea8a5782073'
         'bbd19ad516d7ca3181a181e7c7f2366a'
         '6589fb28bd20d5ec1b4a7b9db9fc4209'
         '5a035421a9bfaa353caf04250c2b285c'
         '367a3538f54db71f108b34cfa31088ac'
         'c4b5f1b342b3dad38642318ca077daee'
         'd90514c76354e09f9250593a72e939c9'
         '721e97031b62aee8914e8617e86f9235'
         '848ea65d39a0fa5e519e9cdba7a720d3'
         '89d96455c6a446ef59b0620d1b8606af'
         '3ee0002192f6f0a2e1d15c813f520f81'
         '814350c52c135d6f7bdada1e29223d38'
         '5f963f80a026f0600edae1b1c70411e5')


build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	msg "applying main patchset ..."
	patch -p0 -i ${srcdir}/01_kdm_zsh_profile.patch
	# 02 is merged in 09, Jan 2013
	#patch -p0 -i ${srcdir}/02_sane_env_and_shutdown_path.patch
	patch -p1 -i ${srcdir}/03_plasma_menubutton_branding.patch
        #patch -p0 -i ${srcdir}/04_plasma_kickoff_url.patch
	#patch -p1 -i ${srcdir}/06_kickoff_default_favourites.patch
	patch -p1 -i ${srcdir}/07_always_show_kickoff_subtext.patch
	patch -p0 -i ${srcdir}/09_enable_start-shutdown-scripts.patch

	msg "applying fixes ..."
	patch -p0 -i ${srcdir}/fix_terminate-server.patch
        patch -p1 -i ${srcdir}/fix_root-only-kcms.patch
        # autoload kscreen before disabling krandr:
        #sed -i "s/add_subdirectory(randr)//g" ./kcontrol/CMakeLists.txt

	msg "starting workspace build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DWITH_Xmms=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DWITH_CkConnector=OFF \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
	make

        msg "starting kcm_touchpad build ..."
	cd ${srcdir}/mishaaq-kcm_touchpad-00370b5
        # Fix desktop to the new layout
	sed -i -e "s~keyboard-and-mouse~input-devices~g" touchpad.desktop
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	msg "starting kcm_tablet build ..."
	cd ${srcdir}/wacomtablet-2.0
        # Fix desktop to the new layout
        sed -i -e "s~keyboard-and-mouse~input-devices~g" ${srcdir}/wacomtablet-2.0/src/kcmodule/kcm_wacomtablet.desktop
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	msg "starting favorites launcher build ..."
	cd ${srcdir}/containmentaction-favorites-0.2
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make
}


package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install

	cd ${srcdir}/mishaaq-kcm_touchpad-00370b5
	make DESTDIR=${pkgdir} install

	cd ${srcdir}/wacomtablet-2.0
	make DESTDIR=${pkgdir} install

	cd ${srcdir}/containmentaction-favorites-0.2
	make DESTDIR=${pkgdir} install

	# install pam configuration
	install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
	install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
	install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver

	# sane path for env and shutdown dir
	install -d -m755 ${pkgdir}/etc/kde/{env,shutdown}

	# install session
	install -D -m 644 ${srcdir}/${pkgname}-${pkgver}/kdm/kfrontend/sessions/kde-plasma.desktop \
	${pkgdir}/etc/X11/sessions/kde-plasma.desktop
	install -D -m 644 ${srcdir}/${pkgname}-${pkgver}/kdm/kfrontend/sessions/kde-plasma-safe.desktop \
	${pkgdir}/etc/X11/sessions/kde-plasma-safe.desktop

	# also install kdm clean default config
	cd ${srcdir}/${pkgname}-${pkgver}/kdm 
	make DESTDIR=${pkgdir} GENKDMCONF_FLAGS="--no-old --no-backup --no-in-notice" install
	
	# edit kdmrc
	sed -i -e s,#GUIStyle=.*,GUIStyle=Oxygen,g ${pkgdir}/usr/share/config/kdm/kdmrc
	sed -i -e s,#ColorScheme=.*,ColorScheme=Oxygen,g ${pkgdir}/usr/share/config/kdm/kdmrc
	sed -i -e s,Theme=/usr.*,Theme=/usr/share/apps/kdm/themes/elarun,g ${pkgdir}/usr/share/config/kdm/kdmrc
	sed -i -e s,MinShowUID=.*,MinShowUID=1000,g ${pkgdir}/usr/share/config/kdm/kdmrc
	sed -i -e 's/halt/poweroff/' ${pkgdir}/usr/share/config/kdm/kdmrc
	
	# Add KaOS kickoff icon
	mkdir -p ${pkgdir}/usr/share/icons/oxygen/scalable/places/
	
	cp -f ${srcdir}/os-branding/start-here-os.svgz ${pkgdir}/usr/share/icons/oxygen/scalable/places/start-here-os.svgz
	cp -f ${srcdir}/os-branding/branding-icon.png ${pkgdir}/usr/share/apps/kdm/themes/oxygen/branding-icon.png
	
	cd ${srcdir}/os-branding/
	for i in 256 128 64 48 32 22 16; do
		mkdir -p ${pkgdir}/usr/share/icons/oxygen/${i}x${i}/places/
		install -D -m644 start-here-os-${i}.png ${pkgdir}/usr/share/icons/oxygen/${i}x${i}/places/start-here-os.png
	done

	# WORKAROUND -> put ggl stuff into ggl package
	rm -rf ${pkgdir}/usr/share/apps/plasma/plasmoids/ggl_photos
	rm -rf ${pkgdir}/usr/share/apps/plasma/plasmoids/ggl_rss
	rm -rf ${pkgdir}/usr/share/kde4/services/plasma-applet-ggl-photos.desktop
	rm -rf ${pkgdir}/usr/share/kde4/services/plasma-applet-ggl-rss.desktop

	# Put a wallpaper into the package
	mkdir -p ${pkgdir}/usr/share/wallpapers
	cp -rv ${srcdir}/kde-wallpapers-${_kdever}/${_default_wp} ${pkgdir}/usr/share/wallpapers

	# Adding powerdevil option files
	mkdir -p ${pkgdir}/usr/include/powerdevil
	cp -rv ${srcdir}/${pkgname}-${pkgver}/powerdevil/daemon/powerdevilaction.h ${pkgdir}/usr/include/powerdevil
	cp -rv ${srcdir}/${pkgname}-${pkgver}/powerdevil/daemon/powerdevilactionconfig.h ${pkgdir}/usr/include/powerdevil
	cp -rv ${srcdir}/${pkgname}-${pkgver}/powerdevil/daemon/powerdevilpolicyagent.h ${pkgdir}/usr/include/powerdevil
	
	# Systemd .service file
	install -D -m644 "${srcdir}"/kdm.service "${pkgdir}"/usr/lib/systemd/system/kdm.service
}
