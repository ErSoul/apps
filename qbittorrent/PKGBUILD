# convert to cmake build with 3.4, 3.3 not ready yet
pkgname=qbittorrent
pkgver=3.3.10
_commit=46e2e5342bb5b6f7a479f9ec7746e58d28ff783e
pkgrel=1
pkgdesc="Bittorrent client programmed in C++ / Qt that uses libtorrent (sometimes called libtorrent-rasterbar)"
screenshot="http://sourceforge.net/dbimage.php?id=242189"
arch=('x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt5-base' 'libtorrent-rasterbar' 'xdg-utils' 'boost-libs' 'python2')
makedepends=('boost' 'which' 'qt5-tools')
groups=('network')
source=("http://sourceforge.net/projects/qbittorrent/files/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/qbittorrent/qBittorrent/commit/729c80f9104a3d01ec724351bd6910b4b8d14c6c.diff")
#source=("https://github.com/qbittorrent/qBittorrent/archive/${_commit}.zip")
md5sums=('47497bf0fb27ad54efde2bcd5c549e5b'
         '4775c7af320aea4c7e103a3591690806')

prepare() {
  cd qbittorrent-${pkgver}
  # revert commit, needs unreleased libtorrent-rasterbar
  patch -Rp1 -i ${srcdir}/729c80f9104a3d01ec724351bd6910b4b8d14c6c.diff
}

build() {
    cd qbittorrent-${pkgver}
    
    ./configure --prefix=/usr 
    make
}

package(){
    cd qbittorrent-${pkgver}
    
    make INSTALL_ROOT=${pkgdir} install
}
