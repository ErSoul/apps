
pkgname=fingerprint-gui
pkgver=1.08
pkgrel=1
pkgdesc="Use fingerprint devices with Linux"
arch=('x86_64')
url="http://www.ullrich-online.cc/fingerprint/"
license=('GPL')
depends=('qca-qt5' 'libfprint' 'libfakekey' 'libusb')
makedepends=('polkit-qt5')
source=("http://ullrich-online.cc/nview/Appliance/fingerprint/download/${pkgname}-${pkgver}.tar.gz")
install=${pkgname}.install
md5sums=('5f4163acc051eff1d438199cecc125ec')

build() {
  cd ${pkgname}-${pkgver}
  
  qmake-qt5 PREFIX=/usr LIB=/usr/lib 
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make -j1 INSTALL_ROOT=${pkgdir}/ install 
  
  install -D -m644 ./CHANGELOG ./COPYING ${pkgdir}/usr/share/doc/${pkgname}/
  install -d -m770 ${pkgdir}/var/upek_data
  
  install -d ${pkgdir}/etc
  install -m644 upek/upek.cfg ${pkgdir}/etc/

  install -d ${pkgdir}/etc/udev/rules.d
  install -m644 upek/91-fingerprint-gui-upek.rules ${pkgdir}/etc/udev/rules.d/
  
  install -d ${pkgdir}/usr/lib  
  install -m644 upek/lib64/libbsapi.so.* ${pkgdir}/usr/lib/
}
