
pkgname=kdev-python
pkgver=1.5.2
pkgrel=1
pkgdesc='Python plugin for KDevelop - stable version'
arch=('x86_64')
url="https://projects.kde.org/projects/playground/devtools/plugins/kdev-python"
license=('GPL')
groups=('kde' 'kdevelop-plugins')
depends=('kdevelop')
makedepends=('kdevelop-pg-qt' 'cmake' 'automoc4')
source=("http://download.kde.org/stable/kdevelop/$pkgname/$pkgver/src/$pkgname-v$pkgver.tar.xz")
md5sums=('1acb6ec3f3ef1fb56d75b2d51a6249fe')

build() {
	cd "$srcdir/$pkgname-v$pkgver"
	
	mkdir -p "$srcdir/build"
	cd "$srcdir/build"
	
	cmake "../$pkgname-v$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
                -DCMAKE_BUILD_TYPE=Release
	
	make parser
	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR="$pkgdir" install
	
	sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
        "${pkgdir}"/usr/share/apps/kdevpythonsupport/encodings/encodings/rot_13.py
}
