
pkgname=qtcreator
pkgver=3.0.1
pkgrel=2
#_qtver=4.8.5
pkgdesc="Lightweight, cross-platform integrated development environment."
arch=('x86_64')
url="http://qt.digia.com/"
license=('LGPL')
depends=('qt5')
makedepends=('git')
optdepends=('qt-docs: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project suppport'
            'ksshaskpass: for ssh support'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support')
groups=('programming')
options=('docs')
source=("http://download.qt-project.org/official_releases/${pkgname}/3.0/${pkgver}/qt-creator-opensource-src-${pkgver}.tar.gz"
        'qtcreator.desktop')
md5sums=('1d736f4b3c25c25cf070c400a92e50c4'
         '5aa242e0262fa22f75c3b59d3a4ec198')
         

build() {
  cd "${srcdir}"

  mkdir build
  cd build

  /usr/lib/qt5/bin/qmake "${srcdir}/qt-creator-opensource-src-${pkgver}/qtcreator.pro"
  make 
  make docs -j1
}

package() {
  cd "${srcdir}/build"

  make INSTALL_ROOT="${pkgdir}/usr" install

  install -Dm644 "${srcdir}/qtcreator.desktop" \
    "${pkgdir}/usr/share/applications/qtcreator.desktop"
  install -Dm644 "${srcdir}/qt-creator-opensource-src-${pkgver}/LGPL_EXCEPTION.TXT" \
    "${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT"
}
