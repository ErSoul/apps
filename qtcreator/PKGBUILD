
pkgname=qtcreator
pkgver=3.6.0
pkgrel=2
pkgdesc="Lightweight, cross-platform integrated development environment."
arch=('x86_64')
url="http://qt.digia.com/"
license=('LGPL')
depends=('qt5-tools' 'qt5-quick1')
makedepends=('git')
optdepends=('qt5-docs: for the integrated Qt documentation'
            'qt5-webkit-examples: to work with webkit examples'
            'gdb: for the debugger'
            'cmake: for cmake project suppport'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support')
groups=('programming')
source=("http://download.qt-project.org/official_releases/${pkgname}/3.6/${pkgver}/qt-creator-opensource-src-${pkgver}.tar.gz"
        'qtcreator.desktop')
md5sums=('6b0052a6d671318fca540a190cacd97d'
         '5162b73dd63f457bdb02bd9c8b1cc66b')

prepare() {
  cd qt-creator-opensource-src-${pkgver}

  sed -i 's|$$IDE_OUTPUT_PATH/libexec/qtcreator|$$IDE_OUTPUT_PATH/lib/qtcreator|' qtcreator.pri
  sed -i 's|$$QTC_PREFIX/libexec/qtcreator|$$QTC_PREFIX/lib/qtcreator|' qtcreator.pri
}         

build() {
  cd "${srcdir}"

  mkdir build
  cd build

  /usr/lib/qt5/bin/qmake "${srcdir}/qt-creator-opensource-src-${pkgver}/qtcreator.pro"
  make 
  make docs -j1
}

package() {
  cd "${srcdir}/build"

  make INSTALL_ROOT="${pkgdir}/usr" install
  make INSTALL_ROOT="${pkgdir}/usr/" install_docs

  install -Dm644 "${srcdir}/qtcreator.desktop" \
    "${pkgdir}/usr/share/applications/qtcreator.desktop"
  install -Dm644 "${srcdir}/qt-creator-opensource-src-${pkgver}/LGPL_EXCEPTION.TXT" \
    "${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT"
}
