
pkgname=atom
pkgver=1.10.2
pkgrel=1
pkgdesc="Hackable text editor from the 21st Century"
arch=('x86_64')
url="https://github.com/atom/atom"
license=('MIT')
depends=('alsa-lib' 'gconf' 'gtk2' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs'
         'nss' 'python2')
makedepends=('git')
conflicts=('atom-editor')
replaces=('atom-editor')
source=("https://github.com/atom/atom/archive/v${pkgver}.tar.gz")
md5sums=('ea511e7a2f8c9fac6bc2062e52435cd9')

build() {
  cd ${pkgname}-${pkgver}

  script/build --build-dir ${srcdir}/atom-build
}

package() {
  cd ${pkgname}-${pkgver}

  script/grunt install --build-dir ${srcdir}/atom-build --install-dir ${pkgdir}/usr
  
  sed -e "s|<%= appName %>|Atom|" \
    -e "s|<%= description %>|$pkgdesc|" \
    -e "s|<%= installDir %>/share/<%= appFileName %>|/usr/bin|"\
    -e "s|<%= iconPath %>|atom|"\
    -e "s|GNOME;GTK;Utility;TextEditor;Development;|Utility;TextEditor;Development;|"\
    resources/linux/atom.desktop.in > resources/linux/Atom.desktop

  install -Dm644 resources/linux/Atom.desktop ${pkgdir}/usr/share/applications/atom.desktop
  install -Dm644 resources/app-icons/stable/png/128.png ${pkgdir}/usr/share/pixmaps/atom.png
  install -Dm644 LICENSE.md ${pkgdir}/usr/share/licenses/$pkgname/LICENSE.md
}
