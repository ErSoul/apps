
pkgname=falkon
pkgver=3.0.0
_pkgver=3.0
pkgrel=1
arch=('x86_64')
pkgdesc="Open source browser based on QtWebengine, written in Qt Framework"
url="https://github.com/KDE/falkon/"
license=('GPL3')
depends=('qtwebengine' 'qt5-webchannel' 'qt5-x11extras' 'kwallet')
makedepends=('extra-cmake-modules' 'qt5-tools')
conflicts=('qupzilla')
source=("https://download.kde.org/stable/falkon/${_pkgver}/src/falkon-${pkgver}.tar.xz")
md5sums=('0c8abc3dbfc29c2e90096e289d9d738b')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../${pkgname}-${pkgver}/linux/completion/_falkon ${pkgdir}/usr/share/zsh/site-functions/_falkon
}

