
pkgname=oxygen-gtk2
pkgver=1.4.6
pkgrel=2
pkgdesc="The Oxygen engine for GTK2, for better Qt/GKT+ integration."
arch=('x86_64')
url=("https://projects.kde.org/projects/playground/artwork/oxygen-gtk")
license=('LGPL3' 'GPL')
depends=('cairo')
makedepends=('gcc' 'cmake' 'gtk2')
provides=('oxygen-gtk')
conflicts=('oxygen-gtk')
groups=('gtk-integration')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kdeglobals')
md5sums=('493892fc36302bfe862604f667a6c04e'
         'feadfd1d24392778da98b992cf3b186a')

build() {
	cd $srcdir/$pkgname-$pkgver
	rm -rf build
	mkdir build
	
	cd build
	cmake -DCMAKE_BUILD_TYPE=Release \
	      -DCMAKE_SKIP_RPATH=ON \
	      -DCMAKE_INSTALL_PREFIX=/usr .. 
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver/build
	make DESTDIR="${pkgdir}" install
	# kf5 colors/theme are not picked up, use our own kdeglobals for now
	rm -r ${pkgdir}/usr/share/themes/oxygen-gtk/gtk-2.0/kdeglobals
	install -D -m644 ${srcdir}/kdeglobals ${pkgdir}/usr/share/themes/oxygen-gtk/gtk-2.0/
}
