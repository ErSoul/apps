
pkgname=frescobaldi
pkgver=2.18
pkgrel=1
pkgdesc="A LilyPond sheet music editor. It aims to be powerful, yet lightweight and easy to use."
arch=('x86_64')
url="http://www.frescobaldi.org/"
license=("GPL")
depends=('lilypond' 'python3-poppler-qt' 'pyqt-python3' 'python3-ly')
optdepends=('lilypond-doc')
makedepends=('automoc4' 'cmake' 'docbook-xsl' 'python3')
install=${pkgname}.install
source=("https://github.com/wbsoft/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('f166dfea54cbfb0c14d85a772f04178c')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    #Prevent windows script to execute
    sed -i '1,4d' setup.cfg
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python3 setup.py install --root="$pkgdir/" --optimize=1

    find "$pkgdir" -name '*.py' -exec sed -i -e 's/^#!\/usr\/bin\/env python$/#!\/usr\/bin\/env python3/' \
                                             -e 's/^#!\/usr\/bin\/python$/#!\/usr\/bin\/python3/' {} \;
}
