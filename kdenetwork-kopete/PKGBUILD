
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdenetwork-kopete
_pkgname=kopete
pkgver=${_kdever}
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork' 'kde-uninstall' 'network')
depends=("kde-runtime>=${_kdever}" "kdepimlibs>=${_kdever}" 'qca-ossl' 'libotr' 'libmsn' 'libidn'
         'qimageblitz' 'libgadu' 'libxss' 'v4l-utils' 'mediastreamer' 'jsoncpp')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("$_mirror/${_pkgname}-$_kdever.tar.xz"
        'libotr3.patch'
        'mediastreamer29.patch')
md5sums=(`grep ${_pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'da40c93298d01b496fa063106a2d213a'
         '019aa75eb686ebc2674aa3a33314f2e2')

prepare() {
   cd kopete-${pkgver}
   patch -p2 -i "${srcdir}"/libotr3.patch
   patch -p1 -i "${srcdir}"/mediastreamer29.patch
}
         
build() {
  mkdir build
  cd build
  
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd build
  
  make DESTDIR=$pkgdir install
}
