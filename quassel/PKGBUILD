
pkgname=quassel
pkgver=0.12.3
pkgrel=1
pkgdesc="Qt irc client with a separated core"
url="http://quassel-irc.org"
arch=('x86_64')
license=('GPL')
depends=('qt5-script' 'qt5-webkit' 'hicolor-icon-theme' 'inxi' 'libdbusmenu-qt5' 'phonon-qt5'
         'kconfigwidgets' 'kcoreaddons' 'knotifications' 'knotifyconfig' 'ktextwidgets' 'kwidgetsaddons'
         'kxmlgui' 'sonnet' 'qca-qt5')
makedepends=('extra-cmake-modules' 'qt5-tools')
conflicts=('kf5-quassel')
replaces=('kf5-quassel')
groups=('communication')
source=("http://quassel-irc.org/pub/quassel-${pkgver}.tar.bz2"
#source=("git://github.com/quassel/quassel.git"
	'quassel.service'
        'quassel.conf'
        'irc-channel-active.svgz'
        'irc-channel-inactive.svgz'
        'quassel.png'
        "https://github.com/sandsmark/quassel/commit/56e1d275c2f8a59eaf9d19ba76aca5f1d3163683.diff")
install=quassel.install
backup=(etc/conf.d/quassel.conf)
md5sums=('a1eec91b56df7d538dfc7cafa8dfabae'
         '7f56b8d2d54a1395ed8bada023ec710c'
         '479229556ba0dd94e3ce28e31e5deea9'
         'd22180823cda2ac7bedb238463714c15'
         '79f0e1c5261fefeb10cd53c1b01e64ef'
         '55a01e2e1a15bf4328638f8f27b7383f'
         'c746703719f94932cef4285812c0277f')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # spellchecking through sonnet
  patch -p1 -i ${srcdir}/56e1d275c2f8a59eaf9d19ba76aca5f1d3163683.diff
}
         
build() {
  mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DUSE_QT5=ON \
    -DWITH_WEBKIT=ON \
    -DWITH_KDE=ON \
    -DCMAKE_BUILD_TYPE="Release" \
    ../${pkgname}-${pkgver} \
    -Wno-dev

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/$pkgname.service ${pkgdir}/usr/lib/systemd/system/$pkgname.service
  install -D -m644 ${srcdir}/$pkgname.conf ${pkgdir}/etc/conf.d/$pkgname
  
  #rm ${pkgdir}/usr/share/icons/hicolor/scalable/status/irc*
  rm ${pkgdir}/usr/share/pixmaps/quassel.png
  install -m 644 -p ${srcdir}/irc-channel* ${pkgdir}/usr/share/icons/hicolor/scalable/status/
  #install -D ${srcdir}/irc-channel-inactive.svgz ${pkgdir}/usr/share/apps/quassel/icons/oxygen/scalable/irc*
  install -m 644 -p ${srcdir}/quassel.png ${pkgdir}/usr/share/pixmaps/
}

