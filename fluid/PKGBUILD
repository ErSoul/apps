
pkgname=fluid
pkgver=0.9.0
_commit=145bef0559667c83d421993a7f9a1c7754965443
pkgrel=1
arch=('x86_64')
url='https://github.com/lirios/fluid'
pkgdesc="Modules for fluid and dynamic applications development with QtQuick."
license=('LGPL')
depends=('qt5-quickcontrols2' 'qt5-graphicaleffects' 'qt5-svg')
makedepends=('extra-cmake-modules' 'gdb')
source=("https://github.com/lirios/fluid/archive/${_commit}.zip") 
md5sums=('845aada2d6240469eab1c4f76af14dfe')

prepare() {
  cd ${pkgname}-${_commit}
  
  ./scripts/fetch_icons.sh
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
