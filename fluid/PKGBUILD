
pkgname=fluid
pkgver=0.8.2
_commit=ec5a220e0519c0ab67ce5dec5e5c48c79b2112d0
pkgrel=1
arch=('x86_64')
url='https://github.com/lirios/fluid'
pkgdesc="Modules for fluid and dynamic applications development with QtQuick."
license=('LGPL')
depends=('qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-graphicaleffects' 'qt5-svg')
makedepends=('extra-cmake-modules' 'gdb')
source=("https://github.com/lirios/fluid/archive/${_commit}.zip") 
md5sums=('41a4c5288b961cf341e31cd3700fbcc2')

prepare() {
  cd ${pkgname}-${_commit}
  
  ./scripts/fetch_icons.sh
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_QMLDIR=lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
