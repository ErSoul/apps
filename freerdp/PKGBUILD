
pkgname=freerdp
_pkgname=FreeRDP
pkgver=1.2.0beta
_pkgver=1.2.0-beta1-android9
_commit=11d113872fe254a2472e99a40f8be7237d5a82d3
pkgrel=5
pkgdesc="Free RDP client"
arch=('x86_64')
url="http://www.freerdp.com/"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
	 'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'pulseaudio' 'libxrender' 
	 'libxfixes' 'gst-plugins-base')
makedepends=('krb5' 'cmake' 'damageproto' 'xmlto' 'docbook-xsl')
source=("https://github.com/FreeRDP/FreeRDP/archive/${_commit}.zip")
md5sums=('f7bbd310e483f4ce296291121be4444c')

build() {
  mkdir build
  cd build
  
  cmake ../${_pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_GSTREAMER_1_0=ON \
    -DWITH_PULSE=ON \
    -DWITH_CUPS=ON
  make
}

package() {
  cd build
  
  make DESTDIR=$pkgdir install
}
