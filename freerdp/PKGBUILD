
pkgname=freerdp
_pkgname=FreeRDP
pkgver=2.0.0beta
#_pkgver=1.2.0-beta1-android9
_commit=5b2455f3147aca395a441fc65b602938600f0788
pkgrel=2
pkgdesc="Free RDP client"
arch=('x86_64')
url="http://www.freerdp.com/"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
        'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'pulseaudio' 'libxrender' 
        'libxfixes' 'gst-plugins-base' 'libxkbcommon' 'libxrandr') 
makedepends=('krb5' 'cmake' 'damageproto' 'xmlto' 'docbook-xsl')
source=("https://github.com/FreeRDP/FreeRDP/archive/${_commit}.zip")
md5sums=('02b6b125b31950a536ab61e1775fbe1c')

build() {
  mkdir build
  cd build
  
  cmake ../${_pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_GSTREAMER_1_0=ON \
    -DWITH_PULSE=ON \
    -DWITH_CUPS=ON
  make
}

package() {
  cd build
  
  make DESTDIR=$pkgdir install
}
