 
pkgname=chromium-ffmpeg-codecs
pkgver=52.0.2743.116
pkgrel=1
pkgdesc="Extra ffmpeg codecs from the Chromium Browser for use in other browsers"
arch=('x86_64')
url="https://ffmpeg.org/"
license=('LGPL2.1')
depends=('gtk2' 'libexif' 'pulseaudio' 'libxss')
makedepends=('ninja' 'nss' 'pciutils' 'python2' 'xdg-utils')
options=('!strip')
source=("https://commondatastorage.googleapis.com/chromium-browser-official/chromium-$pkgver.tar.xz")
md5sums=('9c1c9aefe72876865800952c20b25d5e')

build() {
  cd chromium-${pkgver}
  touch chrome/test/data/webui/i18n_process_css_test.html

  build/gyp_chromium \
    --depth=. \
    -Dclang=0 \
    -Duse_gnome_keyring=0 \
    -Duse_gconf=0 \
    -Dlinux_use_bundled_binutils=0 \
    -Dlinux_use_bundled_gold=0 \
    -Dlinux_use_gold_flags=0 \
    -Dsysroot='' \
    -Dcomponent=shared_library \
    -Dffmpeg_branding=ChromeOS

  ninja -C out/Release ffmpeg
}

package() {
  cd chromium-${pkgver}

  install -Dm644 out/Release/lib/libffmpeg.so ${pkgdir}/usr/lib/chromium/libs/libffmpeg.so
}
