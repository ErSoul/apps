 
pkgname=chromium-ffmpeg-codecs
pkgver=65.0.3325.181
pkgrel=1
pkgdesc="Extra ffmpeg codecs from the Chromium Browser for use in other browsers"
arch=('x86_64')
url="https://ffmpeg.org/"
license=('LGPL2.1')
depends=('gtk3' 'libexif' 'pulseaudio' 'libxss' 'gdk-pixbuf2')
makedepends=('ninja' 'nss' 'pciutils' 'python2' 'xdg-utils' 'mesa' 'clang')
options=('!strip')
groups=('network-web' 'multimedia')
source=("https://commondatastorage.googleapis.com/chromium-browser-official/chromium-${pkgver}.tar.xz"
        'gn.patch'
        'ffmpeg_clang.patch'
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/www-client/chromium/files/chromium-FORTIFY_SOURCE-r2.patch"
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/www-client/chromium/files/chromium-memcpy-r0.patch"
        "https://raw.githubusercontent.com/gentoo/gentoo/master/www-client/chromium/files/chromium-clang-r2.patch")
md5sums=('717988367527423b897712ec0e25a5dd'
         '3354997dd0bfde3443499133a6e16a85'
         '767e3e82f89de949909eb990204185cd'
         'f66dc007f17fffe350ffb037175fcc39'
         'cb40c4153943f9eb5e38e90ba2f8b382'
         '27dbd1479d469346d1ffe27c7e7c0011')

build() {
  cd chromium-${pkgver}
  #touch chrome/test/data/webui/i18n_process_css_test.html
  #sed -i "s|base/callback_internal.cc|base/callback_helpers.cc|g" tools/gn/bootstrap/bootstrap.py
  # from https://github.com/gentoo/gentoo/tree/master/www-client/chromium/files
  patch -p1 -i ${srcdir}/ffmpeg_clang.patch
  patch -p1 -i ${srcdir}/chromium-FORTIFY_SOURCE-r2.patch
  patch -p1 -i ${srcdir}/chromium-clang-r2.patch
  
  export CC="/usr/bin/clang"
  export CXX="/usr/bin/clang++"
  
  #python2 tools/clang/scripts/update.py --without-android

  local _flags=("ffmpeg_branding=\"ChromeOS\""
                'proprietary_codecs=true'
                'enable_hevc_demuxing=true'
                'use_gio=false'
                'use_gnome_keyring=false'
                'use_kerberos=false'
                'use_cups=false'
                'use_sysroot=false'
                'use_gold=false'
                'linux_use_bundled_binutils=false'
                'use_allocator="none"'
                'is_clang=true'
                'clang_use_chrome_plugins=true'
                'fatal_linker_warnings=false'
                'treat_warnings_as_errors=false'
                'enable_nacl=false' 
                'enable_nacl_nonsfi=false'
                'is_component_build=true'
                'is_debug=false'
                'symbol_level=0'
                'use_custom_libcxx=false' 
                'use_lld=false'
                'use_jumbo_build=false')
  
  python2 tools/gn/bootstrap/bootstrap.py -v -s
  out/Release/gn gen out/Release -v --args="${_flags[*]}" --script-executable=/usr/bin/python2

  ninja -C out/Release -v media/ffmpeg
}

package() {
  cd chromium-${pkgver}

  install -Dm644 out/Release/libffmpeg.so ${pkgdir}/usr/lib/chromium/libs/libffmpeg.so
}
