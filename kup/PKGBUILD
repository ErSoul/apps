
pkgname=kup
pkgver=0.7.0
pkgrel=1
pkgdesc="Kup is created for helping people to keep up-to-date backups of their personal files."
arch=('x86_64')
url="https://github.com/spersson/Kup"
license=('GPL')
depends=('solid' 'kio' 'kidletime' 'ki18n' 'knotifications' 'kcoreaddons' 'kiconthemes'
         'kdbusaddons' 'kconfig' 'kinit' 'plasma-framework')
makedepends=('extra-cmake-modules')
source=("https://github.com/spersson/Kup/archive/${pkgname}-${pkgver}.tar.gz")
md5sums=('91d5879a1282a8e0e8898ae6f2c3ecbe')

build() {
  mkdir -p build
  cd build
  
  cmake ../Kup-${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
