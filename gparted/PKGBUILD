
pkgname=gparted
pkgver=0.19.1
pkgrel=2
pkgdesc="A Partition Magic clone, frontend to GNU Parted."
arch=('x86_64')
url="http://gparted.sourceforge.net"
license=('GPL')
depends=('parted' 'gtkmm' 'hicolor-icon-theme')
makedepends=('intltool' 'pkg-config' 'gnome-doc-utils')
optdepends=('dosfstools: for FAT16 and FAT32 partitions'
            'jfsutils: for jfs partitions'
            'ntfs-3g: for ntfs partitions'
            'reiserfsprogs: for reiser partitions'
            'xfsprogs: for xfs partitions')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'gparted.desktop')
md5sums=('9aff8cef2c46e5ca4adaab43588c9e64'
         '76b565bf67ffb527786070825f613aa7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  rm "$pkgdir/usr/share/applications/gparted.desktop"
  install -m 644 -p ${srcdir}/gparted.desktop ${pkgdir}/usr/share/applications/
}
