
pkgname=gparted
pkgver=0.28.1
pkgrel=1
pkgdesc="A Partition Magic clone, frontend to GNU Parted."
arch=('x86_64')
url="http://gparted.sourceforge.net"
license=('GPL')
depends=('parted' 'gtkmm' 'hicolor-icon-theme')
makedepends=('intltool' 'pkg-config' 'gnome-doc-utils')
optdepends=('dosfstools: for FAT16 and FAT32 partitions'
            'jfsutils: for jfs partitions'
            'ntfs-3g: for ntfs partitions'
            'reiserfsprogs: for reiser partitions'
            'xfsprogs: for xfs partitions')
source=("http://downloads.sourceforge.net/project/gparted/gparted/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'gparted.desktop')
md5sums=('ab72464a3ab2137664d9df8462befd3f'
         '76b565bf67ffb527786070825f613aa7')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  rm ${pkgdir}/usr/share/applications/gparted.desktop
  install -m 644 -p ${srcdir}/gparted.desktop ${pkgdir}/usr/share/applications/
}
