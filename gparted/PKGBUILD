
pkgname=gparted
pkgver=0.17.0
pkgrel=1
pkgdesc="A Partition Magic clone, frontend to GNU Parted."
arch=('x86_64')
url="http://gparted.sourceforge.net"
license=('GPL')
depends=('parted' 'gtkmm' 'hicolor-icon-theme')
makedepends=('intltool' 'pkg-config' 'gnome-doc-utils')
optdepends=('dosfstools: for FAT16 and FAT32 partitions'
            'jfsutils: for jfs partitions'
            'ntfs-3g: for ntfs partitions'
            'reiserfsprogs: for reiser partitions'
            'xfsprogs: for xfs partitions')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'gparted.policy')
md5sums=('ecd66491bb2061c6aa6077cc24823b9e'
         'aa0fd8f1aa548871600e629a8ebe6ba4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Install policy file
  install -Dm644 "${srcdir}/gparted.policy" "${pkgdir}/usr/share/polkit-1/actions/gparted.policy"

  mv "${pkgdir}/usr/sbin/gparted" "${pkgdir}/usr/sbin/gparted.elf"

  # Install launcher script
  echo '#!/bin/sh' > ${pkgdir}/usr/sbin/gparted
  echo 'pkexec --disable-internal-agent "/usr/sbin/gparted.elf" "$@"' >> ${pkgdir}/usr/sbin/gparted
  chmod 755 ${pkgdir}/usr/sbin/gparted
}
