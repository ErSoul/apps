
pkgname=skrooge
pkgver=2.3.0
pkgrel=2
pkgdesc="Allows you to manage your personal finances in KDE"
arch=('x86_64')
url="http://skrooge.org"
license=('GPL')
depends=('grantlee' 'karchive' 'kcoreaddons' 'kconfig' 'ki18n' 'kitemviews' 'kwidgetsaddons' 
         'kwindowsystem' 'kcompletion' 'kjobwidgets' 'kconfigwidgets' 'kdesignerplugin' 'kiconthemes' 'kio'
         'khtml' 'knewstuff' 'kparts' 'kwallet' 'kxmlgui' 'kdelibs4support' 'plasma-workspace' # 'aqbanking' 
         'boost-libs' 'libofx' 'qca-qt5' 'kactivities' 'sqlcipher' 'qt5-webkit')
makedepends=('extra-cmake-modules' 'kdoctools' 'docbook-xsl' 'boost' 'clang')
groups=('office')
#source=("git://anongit.kde.org/skrooge.git")
source=("http://download.kde.org/stable/skrooge/${pkgname}-${pkgver}.tar.xz"
        'isnan.diff')
md5sums=('667c634c976bad737e0cfa1915db7566'
         '3f5bba63a8672cb4edcb8f213369fa54')

prepare() {
  cd $pkgname-${pkgver}
  #sed -e "s/FIND_PACKAGE(QCA2 2.0.0 REQUIRED)/FIND_PACKAGE(Qca 2.0.0 REQUIRED)/" -i ${srcdir}/$pkgname/skgbasemodeler/CMakeLists.txt
  #sed -e "s/FIND_PACKAGE(KActivities)/FIND_PACKAGE(KF5Activities)/" -i ${srcdir}/$pkgname/skgbasegui/CMakeLists.txt
  patch -p1 -i ${srcdir}/isnan.diff
}
         
build() {
  mkdir -p build
  cd build
  
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSKG_BUILD_TEST=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
