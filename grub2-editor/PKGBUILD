
pkgname=grub2-editor
pkgver=0.7.0
pkgrel=3
_commit=1d24fd0df0f7733240b051fb89535433cbd2bdc1
pkgdesc="A KDE control module for configuring the GRUB2 bootloader."
arch=('x86_64')
url="https://github.com/maz-1/grub2-editor"
license=('GPL3')
depends=('kcoreaddons' 'ki18n' 'kauth' 'kconfigwidgets' 'kio' 'solid'
         'imagemagick' 'hwinfo' 'librsvg' 'grub' 'os-prober')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('system')
source=("https://github.com/maz-1/grub2-editor/archive/${_commit}.zip")
md5sums=('491a56e13552475309fb4245dc4289d6')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}

