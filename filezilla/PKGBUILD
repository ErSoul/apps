
pkgname=filezilla
_pkgname=FileZilla
pkgver=3.13.1
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client."
arch=('x86_64')
url="http://filezilla-project.org/"
license=('GPL2')
depends=('dbus-core' 'hicolor-icon-theme' 'libidn' 'libjpeg-turbo' 'xdg-utils' 'wxgtk2.9' 'sqlite')
source=("http://downloads.sourceforge.net/project/filezilla/${_pkgname}_Client/$pkgver/${_pkgname}_${pkgver}_src.tar.bz2")
md5sums=('48e1e32813cfda37ec4688336fed7408')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --with-pugixml=builtin
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install
}
