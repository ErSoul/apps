
pkgname=filezilla
_pkgname=FileZilla
pkgver=3.46.3
_pkgver=3.46.3
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client."
arch=('x86_64')
url="https://filezilla-project.org/"
license=('GPL2')
depends=('dbus' 'hicolor-icon-theme' 'libidn' 'libjpeg-turbo' 'xdg-utils' 'wxgtk' 'sqlite'
         'libfilezilla' 'pugixml' 'gnutls' 'nettle')
source=("https://download.filezilla-project.org/client/${_pkgname}_${_pkgver}_src.tar.bz2"
        'filezilla.desktop')
sha512sums=('35b61c3bfd1d40848e7846b4533da8844df0e4873cb89286b90330cbc29dffeff96fcab10aec908a08d9129198bb2d80df8f223d8ebc1a0863a4083cb06abffc'
            'a9d36573bc30a476e75f33f04f8aef990bdb03a151e7a4b72cdc69c351fc7313da8a84fc2e43383d3945b78a510d01b31a820c3615a185ead3343606285c893b')

build() {
  cd ${pkgname}-${_pkgver}

  ./configure --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --with-pugixml=system
  make
}

package() {
  cd ${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install
  
  rm ${pkgdir}/usr/share/applications/filezilla.desktop
  install -D -m644 ${srcdir}/filezilla.desktop ${pkgdir}/usr/share/applications/filezilla.desktop
}
