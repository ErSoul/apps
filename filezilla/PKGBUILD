
pkgname=filezilla
_pkgname=FileZilla
pkgver=3.37.3
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client."
arch=('x86_64')
url="https://filezilla-project.org/"
license=('GPL2')
depends=('dbus' 'hicolor-icon-theme' 'libidn' 'libjpeg-turbo' 'xdg-utils' 'wxgtk' 'sqlite'
         'libfilezilla' 'pugixml' 'gnutls')
source=("https://download.filezilla-project.org/client/${_pkgname}_${pkgver}_src.tar.bz2"
        'filezilla.desktop')
sha512sums=('cda45be8075946fb865462ee0bfdb026a3a9edc886ecaf2d6cf2186f8f58c0aacf0c2f1c86930e871b56e55bcd9a593d19c8d60084343cb7cc7db134ab779e66'
            'a9d36573bc30a476e75f33f04f8aef990bdb03a151e7a4b72cdc69c351fc7313da8a84fc2e43383d3945b78a510d01b31a820c3615a185ead3343606285c893b')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --with-pugixml=system
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  rm ${pkgdir}/usr/share/applications/filezilla.desktop
  install -D -m644 ${srcdir}/filezilla.desktop ${pkgdir}/usr/share/applications/filezilla.desktop
}
