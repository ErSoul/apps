
pkgname=filezilla
_pkgname=FileZilla
pkgver=3.44.1
_pkgver=3.44.1
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client."
arch=('x86_64')
url="https://filezilla-project.org/"
license=('GPL2')
depends=('dbus' 'hicolor-icon-theme' 'libidn' 'libjpeg-turbo' 'xdg-utils' 'wxgtk' 'sqlite'
         'libfilezilla' 'pugixml' 'gnutls' 'nettle')
source=("https://download.filezilla-project.org/client/${_pkgname}_${_pkgver}_src.tar.bz2"
        'filezilla.desktop')
sha512sums=('fa2e8c65621c7c6003e45f99f5153177e507ca34dde49d86d6f59e8b985abdeb80b779d71e554fc6acb891a695a0ef4c275bf0950af1b4cb67ff5ddb4c51e3b7'
            'a9d36573bc30a476e75f33f04f8aef990bdb03a151e7a4b72cdc69c351fc7313da8a84fc2e43383d3945b78a510d01b31a820c3615a185ead3343606285c893b')

build() {
  cd ${pkgname}-${_pkgver}

  ./configure --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --with-pugixml=system
  make
}

package() {
  cd ${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install
  
  rm ${pkgdir}/usr/share/applications/filezilla.desktop
  install -D -m644 ${srcdir}/filezilla.desktop ${pkgdir}/usr/share/applications/filezilla.desktop
}
