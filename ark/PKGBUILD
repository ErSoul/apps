
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ark
arch=('x86_64')
pkgver=${_kdever}
pkgrel=2
pkgdesc="Archiver for various compression formats"
url="https://github.com/KDE/ark"
license=('GPL' 'LGPL' 'FDL')
depends=('karchive' 'kconfig' 'kpty' 'khtml' 'kdbusaddons' 'ki18n' 'kiconthemes'
         'kio' 'kservice' 'kwidgetsaddons' 'libarchive' 'p7zip' 'zip' 'unzip' 'unrar' 'kde-baseapps')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kdeutils' 'kde-uninstall' 'utils')
conflicts=('kdeutils-ark')
replaces=('kdeutils-ark')
source=($_mirror/${pkgname}-$_kdever.tar.xz)
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i ${srcdir}/132743e572fb7550cbfbbe8db848f2a28a0001cb.diff
  
  sed -i 's|application/x-rar|application/vnd.rar|' plugins/clirarplugin/CMakeLists.txt
  sed -i 's|application/x-rar|application/vnd.rar|' plugins/clirarplugin/kerfuffle_clirar.json.cmake
}

build() {  
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
