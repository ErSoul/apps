
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ark
arch=('x86_64')
pkgver=${_kdever}
#pkgver=15.03.79
pkgrel=2
pkgdesc="Archiver for various compression formats"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
depends=('karchive' 'kconfig' 'kpty' 'khtml' 'kdbusaddons' 'ki18n' 'kiconthemes'
         'kio' 'kservice' 'kwidgetsaddons' 'libarchive' 'p7zip' 'zip' 'unzip' 'unrar' 'kde-baseapps')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kdeutils' 'kde-uninstall' 'utils')
conflicts=('kdeutils-ark')
replaces=('kdeutils-ark')
source=($_mirror/${pkgname}-$_kdever.tar.xz
       "https://github.com/KDE/ark/commit/0eb4f86e5d6e2860bfc908a0ccf805470aea8675.diff")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '00b7577a7fd46e4f319dd947ab001499')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix opening .deb files
  patch -p1 -i ${srcdir}/0eb4f86e5d6e2860bfc908a0ccf805470aea8675.diff
}

build() {  
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
