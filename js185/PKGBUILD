
pkgname=js185
pkgver=1.0.0
pkgrel=1
pkgdesc="JavaScript interpreter and libraries"
arch=('x86_64')
url="https://developer.mozilla.org/En/SpiderMonkey/1.8.5"
license=('GPL2')
depends=('nspr' 'gcc-libs')
makedepends=('python2' 'zip')
replaces=('spidermonkey' 'js')
conflicts=('spidermonkey' 'js')
provides=('spidermonkey' 'js')
source=("http://ftp.mozilla.org/pub/mozilla.org/js/$pkgname-$pkgver.tar.gz")
md5sums=('a4574365938222adca0a6bd33329cb32')

build() {
  cd "$srcdir/js-1.8.5/js/src"

  unset CPPFLAGS

  ./configure --prefix=/usr --with-system-nspr \
     --enable-threadsafe

  make
}

package() {
  cd "$srcdir/js-1.8.5/js/src"
  make DESTDIR="$pkgdir" install

  install -Dm755 shell/js "$pkgdir/usr/bin/js"
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -x {} +

  cd "$pkgdir/usr/lib"
  ln -sf libmozjs185.so.1.0.0 libmozjs185.so.1.0
  ln -sf libmozjs185.so.1.0 libmozjs185.so
}

