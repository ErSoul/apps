
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kompare
pkgver=${_kdever}
pkgrel=1
pkgdesc='A KDE visual diff viewer'
url='http://kde.org/applications/development/kompare/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kcoreaddons' 'kcodecs' 'kiconthemes' 'kjobwidgets' 'kconfig' 'kparts' 'ktexteditor'
         'kwidgetsaddons' 'libkomparediff2')
makedepends=('extra-cmake-modules' 'kdoctools')
install=${pkgname}.install
groups=('kde' 'kdesdk' 'kde-uninstall' 'programming')
conflicts=('kdesdk-cervisia'
         'kdesdk-kcachegrind' 
         'kdesdk-kioslaves'
         'kdesdk-kompare'
         'kdesdk-poxml'
         'kdesdk-dev-scripts'
         'kdesdk-strigi-analyzers')
replaces=('kdesdk-cervisia'
         'kdesdk-kcachegrind' 
         'kdesdk-kioslaves'
         'kdesdk-kompare'
         'kdesdk-poxml'
         'kdesdk-dev-scripts'
         'kdesdk-strigi-analyzers')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        'save.diff'
        'qt55.diff'
        'preferences.diff')
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '56e39b6d53fa8ab08e752d2199c0a3f4'
         '8f74d080a7d45376cd1d8e465e97fff3'
         '3af06b0ca1f8a0e8618d7a7a521d6b4e')

build() {
  cd ${srcdir}
  patch -p0 -i ${srcdir}/save.diff
  patch -p0 -i ${srcdir}/qt55.diff
  patch -p0 -i ${srcdir}/preferences.diff
  
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
