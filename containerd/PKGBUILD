
# stay with _commit builds, keep in sync with what docker ships
pkgname=containerd
pkgver=0.2.4
_commit=665e84e6c28653a9c29a6db601636a92d46896f3
pkgrel=6
arch=('x86_64')
pkgdesc="Daemon to control runC, built for performance and density."
url="https://containerd.tools/"
license=('APACHE')
depends=('runc')
makedepends=('go')
#source=("https://github.com/docker/containerd/archive/v${pkgver}.tar.gz")
source=("https://github.com/docker/containerd/archive/${_commit}.zip")
md5sums=('b5ec3463395705df32d0bcc83226e947')

prepare() {
    cd ${srcdir}
    
    install -d src/github.com/docker
    mv ${pkgname}-${_commit} src/github.com/docker/${pkgname}
}

build() {
    cd src/github.com/docker/${pkgname}
    
    LDFLAGS="" GOPATH="$srcdir:$srcdir/${pkgname}/vendor" make
}

package() {
    cd src/github.com/docker/${pkgname}/bin
    
    install -Dm755 containerd ${pkgdir}/usr/bin/containerd
    install -Dm755 containerd-shim ${pkgdir}/usr/bin/containerd-shim
    install -Dm755 ctr ${pkgdir}/usr/bin/ctr
}
