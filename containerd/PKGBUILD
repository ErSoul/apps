
# stay with _commit builds, keep in sync with what docker ships
pkgname=containerd
pkgver=0.2.4
_commit=03e5862ec0d8d3b3f750e19fca3ee367e13c090e
pkgrel=4
arch=('x86_64')
pkgdesc="Daemon to control runC, built for performance and density."
url="https://containerd.tools/"
license=('APACHE')
depends=('runc')
makedepends=('go')
#source=("https://github.com/docker/containerd/archive/v${pkgver}.tar.gz")
source=("https://github.com/docker/containerd/archive/${_commit}.zip")
md5sums=('d3d091e1c9ccf8b5668a8694f8cdc1fb')

prepare() {
    cd ${srcdir}
    
    install -d src/github.com/docker
    mv ${pkgname}-${_commit} src/github.com/docker/${pkgname}
}

build() {
    cd src/github.com/docker/${pkgname}
    
    LDFLAGS="" GOPATH="$srcdir:$srcdir/${pkgname}/vendor" make
}

package() {
    cd src/github.com/docker/${pkgname}/bin
    
    install -Dm755 containerd ${pkgdir}/usr/bin/containerd
    install -Dm755 containerd-shim ${pkgdir}/usr/bin/containerd-shim
    install -Dm755 ctr ${pkgdir}/usr/bin/ctr
}
