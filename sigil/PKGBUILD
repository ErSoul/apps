
pkgname=sigil
_pkgname=Sigil
_pagever=0.7.1
_commit=adef8f662a1cd85675bed7f45b156baaf20c85e2
pkgver=0.9.16
pkgrel=2
pkgdesc="A multi-platform WYSIWYG ebook editor. It is designed to edit books in ePub format. "
arch=('x86_64')
url="https://github.com/Sigil-Ebook/Sigil"
license=('GPL3')
depends=('qtwebengine' 'qt5-svg' 'qt5-webchannel' 'qt5-xmlpatterns' 'boost-libs' 'xerces-c' 'python3-lxml'
         'python3-pillow' 'python3-six' 'pyqt5-python3' 'hunspell')
makedepends=('cmake' 'boost' 'qt5-tools' 'clang')
#options=('debug')
groups=('office')
source=("https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.tar.gz"
        "https://github.com/Sigil-Ebook/PageEdit/archive/${_pagever}.tar.gz")
#source=("https://github.com/Sigil-Ebook/Sigil/archive/${_commit}.zip")
sha256sums=('c28ec5230b9eb708fae795b69777e32eceee94ac26e3ea2838c74ef1e041f638'
            '0d6641e8e3a7791026c067a3bea44e7e0171938de08841f9b32ec81b8c981ca1')

prepare() {
  cd ${_pkgname}-${pkgver}

  # set external editor
  sed -i -e 's|QString new_xeditor_path = ""|QString new_xeditor_path = "/usr/bin/pageedit"|' src/Dialogs/PreferenceWidgets/GeneralSettingsWidget.cpp
}

build() {
  cd ${_pkgname}-${pkgver}
  mkdir -p build 
  cd build
  
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_SYSTEM_LIBS=1 \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON ..
  make
  
  cd ../..
  cd PageEdit-${_pagever}
  mkdir -p build
  cd build
  
  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON ..
  make
}

package() {
  cd ${_pkgname}-${pkgver}/build
  make install DESTDIR=${pkgdir}

  cd ../..
  
  cd PageEdit-${_pagever}/build
  make install DESTDIR=${pkgdir}
}
