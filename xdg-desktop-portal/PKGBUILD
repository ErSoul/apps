
# 1.x odd numbers are dev, not stable
pkgname=xdg-desktop-portal
pkgver=1.4.2
pkgrel=1
pkgdesc="Portal frontend service for Flatpak and possibly other desktop containment frameworks."
url="https://github.com/flatpak/xdg-desktop-portal"
arch=("x86_64")
license=('LGPL2.1')
depends=('fuse' 'glib2' 'pipewire')
makedepends=('python3' 'flatpak')
source=("https://github.com/flatpak/xdg-desktop-portal/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('10f887b8b84aedc12fa9b01cc65bc6e0c8a6674460dd72c09f0cd1985ac5c7ed')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-pipewire \
    --disable-geoclue
  make 
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
