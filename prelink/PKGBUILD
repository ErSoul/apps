
pkgname=prelink
pkgver=20130503
pkgrel=1
pkgdesc='ELF prelinking utility to speed up dynamic linking'
arch=('x86_64')
url='http://people.redhat.com/jakub/prelink/'
license=('GPL')
depends=('elfutils')
backup=('etc/prelink.conf')
source=("http://people.redhat.com/jakub/$pkgname/$pkgname-$pkgver.tar.bz2"
        'prelink.conf')
md5sums=('4cab1571718a9b25665bd025069a02c7'
         '5302bde3442792bd792d7d264976443c')

build() {
  cd "$pkgname"

  ./configure --prefix=/usr --mandir=/usr/share/man 
  make
}

check() {
  make -C "$pkgname" check
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install

  install -Dm644 prelink.conf "$pkgdir/etc/prelink.conf"
}
