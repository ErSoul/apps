
pkgname=openimageio
_pkgname=OpenImageIO
pkgver=1.4.15
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications."
arch=('x86_64')
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'qt')
makedepends=('cmake' 'python2' 'boost' 'mesa')
optdepends=('python2: bindings support')
source=("https://github.com/OpenImageIO/oiio/archive/Release-${pkgver}.tar.gz"
        'freetype.patch')
md5sums=('8911cf6103e9fe3638912908bb4faa19'
         'b1a27daf910718d7a055daf5b0c4b534')

build() {
  cd $srcdir/oiio-Release-$pkgver
  patch -p1 -i ${srcdir}/freetype.patch

  mkdir build 
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DOIIO_BUILD_TESTS=OFF \
      ..
  make
}

package() {
  cd $srcdir/oiio-Release-$pkgver/build

  make DESTDIR=$pkgdir install

  # license
  cd ..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}