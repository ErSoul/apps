
pkgname=fcitx-gtk2
_pkgname=fcitx
pkgver=4.2.9.7
pkgrel=1
pkgdesc="A Flexible Input Method Framework"
arch=('x86_64')
license=('GPL')
url="https://code.google.com/p/fcitx"
depends=('fcitx' 'gtk2')
makedepends=('extra-cmake-modules' 'doxygen' 'gobject-introspection')
groups=('localization')
install=${pkgname}.install
source=("https://download.fcitx-im.org/$_pkgname/$_pkgname-${pkgver}_dict.tar.xz")
md5sums=('59dccaaf4e6fe4c9dc511ddbc84a084a')

build() {
  mkdir -p build
  cd build
  
  cmake ../${_pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DENABLE_QT=OFF
  make
}

package(){
    cd build/src/frontend/gtk2
    
    make DESTDIR=$pkgdir install
}

