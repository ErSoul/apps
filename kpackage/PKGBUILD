
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kpackage
pkgver=${_kf5ver}
pkgrel=3
pkgdesc='Framework that lets applications manage user installable packages of non-binary assets'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kpackage'
license=('LGPL')
depends=('karchive' 'ki18n' 'kcoreaddons' 'kconfig')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${pkgname}-$_kf5ver.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
