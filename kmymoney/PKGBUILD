
pkgname=kmymoney
pkgver=5.0.2
pkgrel=1
pkgdesc="An finance manager for KDE with HBCI support"
arch=('x86_64')
url="http://www.kmymoney.org/"
license=('GPL')
depends=('karchive' 'kcoreaddons' 'kconfig' 'kconfigwidgets' 'ki18n' 'kcompletion' 'kcmutils' 'kitemmodels' 'kitemviews'
         'kservice' 'kwallet' 'kiconthemes' 'kxmlgui' 'ktextwidgets' 'knotifications' 'kio' 'kholidays' 
         'gpgme' 'kcontacts' 'akonadi-contacts' 'akonadi' 'kidentitymanagement' 'kross'
         'shared-mime-info' 'boost-libs' 'pulseaudio' 'libofx' 'libical' 'alkimia' 'kdiagram'
         'kactivities' 'aqbanking' 'kdewebkit') # not finding sqlite 'sqlcipher')
makedepends=('extra-cmake-modules' 'kdoctools' 'docbook-xsl' 'boost')  
groups=('office')
source=("https://download.kde.org/stable/kmymoney/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('57f321ebdb1e8bc04e41531a7d0ce947c65101d7768c2aa45c65370b5d67a593')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DENABLE_SQLCIPHER=OFF \
    -DENABLE_WEBENGINE=OFF # build fails when using qtwebengine looking for KWebView
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
