
pkgname=rosegarden
pkgver=17.12.1
_pkgver=17.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('x86_64')
url="https://www.rosegardenmusic.com/"
license=('GPL')
depends=('liblrdf' 'dssi' 'fftw' 'perl' 'qt5-base' 'shared-mime-info' 'lilypond')
makedepends=('cmake' 'qt5-tools')
optdepends=('cups: printing support'
            'okular: print preview, or any other PDF viewer'
            'flac'
            'wavpack')
groups=('multimedia')
source=("https://sourceforge.net/projects/rosegarden/files/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('13022c54af94024f5304927d7c40e797')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_QT5=TRUE
  make
}

package() {
  cd build 
  
  make DESTDIR="${pkgdir}" install
}
