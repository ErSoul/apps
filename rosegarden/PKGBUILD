
pkgname=rosegarden
pkgver=15.11.80
_commit=14415
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('x86_64')
url="http://www.rosegardenmusic.com/"
license=('GPL')
depends=('liblrdf' 'dssi' 'fftw' 'perl' 'qt5-base' 'shared-mime-info' 'lilypond')
makedepends=('cmake' 'qt5-tools')
optdepends=('cups: printing support'
            'okular: print preview, or any other PDF viewer'
            'flac'
            'wavpack')
groups=('multimedia')
install=${pkgname}.install
source=("http://sourceforge.net/code-snapshots/svn/r/ro/rosegarden/code/${pkgname}-code-${_commit}-trunk.zip")
md5sums=('61866b3e34563947b35ce28e72c834e0')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-code-${_commit}-trunk/${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_QT5=TRUE
  make
}

package() {
  cd build 
  
  make DESTDIR="${pkgdir}" install
}
