
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-integration
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='The plugin KDEPlatformTheme provides KDE integration for QPlatformThemePlugin.'
arch=('x86_64')
url='https://github.com/KDE/plasma-integration'
license=('LGPL')
depends=('kconfig' 'kconfigwidgets' 'ki18n' 'kiconthemes' 'kio' 'knotifications' 'kwidgetsaddons'
         'kwayland' 'noto-fonts' 'oxygen-fonts')
makedepends=('extra-cmake-modules')
groups=('plasma5')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/KDE/plasma-integration/commit/4240c588aa3c2643d8d00a0da562197f1784cad6.diff")
md5sums=(`grep ${pkgname}-$_plasmaver.tar.xz ../kf5.md5 | cut -d" " -f1`
         'ce12e54584b1f54dd41a768520f0dc61')

prepare () {
  cd ${pkgname}-${pkgver}
  # patch by frugalware to fix reading from default lnf package
  patch -p1 -i ${srcdir}/4240c588aa3c2643d8d00a0da562197f1784cad6.diff
}
  
build () {  
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package () {
  cd build
  
  make DESTDIR=${pkgdir} install
}

