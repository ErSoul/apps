
_extramodules=extramodules-4.7-next
_kernver="$(cat /lib/modules/$_extramodules/version)"
#_kernver=4.7.0-1-next  # hardcode in case system not running linux-next

pkgname=virtualbox-modules-next
pkgver=5.1.4
_pkgver=5.1.4
pkgrel=2
pkgdesc="Kernel modules for VirtualBox"
url="http://www.virtualbox.org"
arch=('x86_64')
license=('GPL-2')
depends=('linux-next>=4.7RC7' 'linux-next<4.8')
makedepends=('linux-next-headers')
provides=('virtualbox-modules')
source=("https://sourceforge.net/projects/kaosx/files/sources/virtualbox-modules/vbox-kernel-module-src-${_pkgver}.tar.xz"
        'virtualbox-modules-depmod.patch'
        'vbox-modules-next.conf')
install=${pkgname}.install
md5sums=('bc762c95a7868657afa5286f1be2ccf0'
         'c9f84fb084a79275d269c542ff53ba4d'
         'f122361a8d2075e4dc819757b6ba34cc')

prepare() {
    cd vbox-kernel-module-src-${_pkgver}
    
    patch -p0 -i ${srcdir}/virtualbox-modules-depmod.patch
}

build() {
    cd vbox-kernel-module-src-${_pkgver}
    mkdir -p lib/modules/${_kernver}/misc
    
    make KERN_DIR=/lib/modules/${_kernver}/build MODULE_DIR_TST=${pkgdir}/lib/modules/${_kernver} \
        MODULE_DIR=${pkgdir}/lib/modules/${_kernver}/misc
}

package() {
    cd vbox-kernel-module-src-${_pkgver}
    
    make KERN_DIR=/lib/modules/${_kernver}/build MODULE_DIR_TST=${pkgdir}/lib/modules/${_kernver} \
        MODULE_DIR=${pkgdir}/lib/modules/${_kernver}/misc INSTALL_MOD_PATH=${pkgdir} install

    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/virtualbox-modules-next.install"
    
    install -D -m644 ../vbox-modules-next.conf ${pkgdir}/etc/modules-load.d/vbox-modules-next.conf
}
