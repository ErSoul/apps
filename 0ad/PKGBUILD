
pkgname=0ad
pkgver=a15
_pkgver=0.0.15-alpha
pkgrel=3
pkgdesc="Cross-platform, 3D and historically-based real-time strategy game"
arch=('x86_64')
url="http://play0ad.com/"
license=('GPL2' 'CCPL')
depends=('binutils' 'boost-libs' 'curl' 'enet>=1.3' 'libogg' 'libpng' 'libvorbis' 'libxml2'
         'openal' 'sdl' 'zlib' 'libgl' '0ad-data' 'glu' 'gamin' 'libxcursor' 'mesa'
         'libjpeg' 'libtiff' 'openexr' 'gloox' 'miniupnpc')
makedepends=('boost' 'cmake' 'zip' 'python2')
source=("http://releases.wildfiregames.com/$pkgname-$_pkgver-unix-build.tar.xz")
md5sums=('a651d81eb9b31da01e376ddaa597e954')

build() {
  cd "$srcdir/$pkgname-$_pkgver/build/workspaces"

  unset CPPFLAGS 

  ./update-workspaces.sh \
      --with-system-enet \
      --with-system-miniupnpc \
      --bindir=/usr/bin \
      --libdir=/usr/lib/0ad \
      --disable-atlas \
      --datadir=/usr/share/${pkgname}/data

  cd "$srcdir/$pkgname-$_pkgver/build/workspaces/gcc"

  make CONFIG=Release
}

package() {
  install -d "${pkgdir}"/usr/{bin,lib/0ad}
  install -Dm755 "${srcdir}"/${pkgname}-${_pkgver}/binaries/system/pyrogenesis "${pkgdir}/usr/bin"
  install -Dm755 "${srcdir}"/${pkgname}-${_pkgver}/binaries/system/*.so{,.1.0} "${pkgdir}/usr/lib/0ad"

  install -Dm755 "${srcdir}/${pkgname}-${_pkgver}/build/resources/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/$pkgname-$_pkgver/build/resources/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${srcdir}/$pkgname-$_pkgver/build/resources/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
