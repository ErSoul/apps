
pkgname=0ad
pkgver=a21
_pkgver=0.0.21-alpha
pkgrel=2
pkgdesc="Cross-platform, 3D and historically-based real-time strategy game"
arch=('x86_64')
url="http://play0ad.com/"
license=('GPL2' 'CCPL')
depends=('binutils' 'boost-libs' 'curl' 'enet>=1.3' 'libogg' 'libpng' 'libvorbis' 'libxml2'
         'openal' 'sdl2' 'zlib' 'libgl' '0ad-data' 'glu' 'gamin' 'libxcursor' 'mesa'
         'libjpeg' 'libtiff' 'openexr' 'gloox')
makedepends=('boost' 'cmake' 'zip' 'python2')
source=("http://releases.wildfiregames.com/${pkgname}-${_pkgver}-unix-build.tar.xz")
md5sums=('095eade8c9b3deaf25d0d7fa423ff860')

build() {
  cd ${pkgname}-${_pkgver}/build/workspaces

  unset CPPFLAGS 

  ./update-workspaces.sh \
      --without-miniupnpc \
      --bindir=/usr/bin \
      --libdir=/usr/lib/0ad \
      --disable-atlas \
      --datadir=/usr/share/${pkgname}/data

  cd gcc

  make 
}

package() {
  install -d ${pkgdir}/usr/{bin,lib/0ad,share/${pkgname}/data}

  install -Dm755 ${srcdir}/${pkgname}-${_pkgver}/binaries/system/pyrogenesis ${pkgdir}/usr/bin
  install -Dm755 ${srcdir}/${pkgname}-${_pkgver}/binaries/system/*.so* ${pkgdir}/usr/lib/0ad
  
  cp -r ${srcdir}/${pkgname}-${_pkgver}/binaries/data/l10n/ ${pkgdir}/usr/share/${pkgname}/data/

  install -Dm755 ${srcdir}/${pkgname}-${_pkgver}/build/resources/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
  install -Dm644 ${srcdir}/$pkgname-$_pkgver/build/resources/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${srcdir}/$pkgname-$_pkgver/build/resources/${pkgname}.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
}
