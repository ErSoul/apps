
pkgname=kdevelop
pkgver=5.1.2
_pkgver=5.1.2
pkgrel=1
pkgdesc="A C/C++ development environment for KDE"
arch=('x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevplatform' 'kconfig' 'kdeclarative' 'kiconthemes' 'ki18n' 'kitemmodels' 'kitemviews'
         'kjobwidgets' 'kcmutils' 'kio' 'knewstuff' 'knotifyconfig' 'kparts' 'kservice' 'ktexteditor' 
         'threadweaver' 'kxmlgui' 'kwindowsystem' 'krunner' 'libksysguard' 'plasma-framework' 'okteta' 
         'kdevelop-pg-qt' 'clang' 'llvm' 'heaptrack')
makedepends=('extra-cmake-modules' 'kdoctools' 'perl' 'docbook-xsl')
conflicts=('kf5-kdevelop')
replaces=('kf5-kdevelop')
groups=('programming')
source=("https://download.kde.org/stable/kdevelop/${_pkgver}/src/kdevelop-${pkgver}.tar.xz")      
md5sums=('b38e55c5b8656d7fab597d8d2b873683')

prepare() {
  cd ${pkgname}-${pkgver}
  
  #patch -p1 -i $srcdir/qt55.diff
  sed -i -e 's|qdbus|qdbus-qt5|g' app/kdevelop!
}

build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
