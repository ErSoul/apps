
pkgname=js
pkgver=52.7.3
_pkgver=52_7_3esr_RELEASE
pkgrel=1
pkgdesc="JavaScript interpreter and libraries"
arch=('x86_64')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/SpiderMonkey/Releases/52"
license=('GPL2')
depends=('nspr' 'gcc-libs' 'libffi' 'readline')
makedepends=('python2' 'zip' 'autoconf2.13')
source=("https://hg.mozilla.org/mozilla-unified/archive/FIREFOX_${_pkgver}.tar.bz2")
md5sums=('ad245fdd0bc4f3cdd0716913d4bf8c06')

build() {
  cd mozilla-unified-FIREFOX_${_pkgver}/js/src
  # http://www.linuxfromscratch.org/blfs/view/svn/general/JS2.html needed for newer perl
  #sed -i 's/(defined\((@TEMPLATE_FILE)\))/\1/' config/milestone.pl

  unset CPPFLAGS

  autoconf-2.13
  ./configure --prefix=/usr \
       --disable-debug \
       --disable-debug-symbols \
       --disable-optimize \
       --disable-tests \
       --with-system-nspr \
       --with-system-zlib \
       --without-system-icu \
       --enable-readline

  make
}

package() {
  cd mozilla-unified-FIREFOX_${_pkgver}/js/src
  make DESTDIR=${pkgdir} install
  
  rm ${pkgdir}/usr/lib/libjs_static.ajs

  find ${pkgdir}/usr/{lib/pkgconfig,include} -type f -exec chmod -x {} +
}

