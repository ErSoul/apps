
pkgname=pidgin
pkgver=2.10.11
pkgrel=2
pkgdesc="Multi-protocol instant messaging client"
arch=('x86_64')
url="http://pidgin.im/"
license=('GPL')
depends=("libpurple=$pkgver" 'startup-notification' 'gtkspell' 'libxss' 'hicolor-icon-theme'
         'libsm' 'libidn' 'avahi' 'networkmanager')
makedepends=('nss' 'libsasl' 'python2' 'tk' 'ca-certificates' 'intltool' )
options=('!libtool')
install=pidgin.install
source=("http://downloads.sourceforge.net/pidgin/${pkgname}-${pkgver}.tar.bz2")
md5sums=('7c8bb6f997e05e7929104439872c2844')

build() {
  cd "$srcdir/${pkgname}-$pkgver"

    # Use Python 2
    sed -i "s#env python#env python2#" */plugins/*.py
    sed -i "s#env python#env python2#" libpurple/purple-{remote,notifications-example,url-handler}
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --disable-schemas-install \
                --disable-meanwhile \
                --disable-gnutls \
                --disable-gstreamer \
                --disable-vv \
                --enable-cyrus-sasl \
                --enable-nm \
                --with-python=/usr/bin/python \
                --with-system-ssl-certs=/etc/ssl/certs
    make
}

package(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    #for linking
    make -C libpurple DESTDIR="${pkgdir}" install-libLTLIBRARIES

    make -C pidgin DESTDIR="${pkgdir}" install
    make -C doc DESTDIR="${pkgdir}" install

    #clean up libpurple
    make -C libpurple DESTDIR="${pkgdir}" uninstall-libLTLIBRARIES

    install -D -m 0644 pidgin/data/pidgin.desktop "${pkgdir}"/usr/share/applications/pidgin.desktop

    rm -f "${pkgdir}"/usr/share/man/man1/finch.1
}


