
# perl issue on server, build locally only
pkgname=pidgin
pkgver=2.10.12
pkgrel=1
pkgdesc="Multi-protocol instant messaging client"
arch=('x86_64')
url="http://pidgin.im/"
license=('GPL')
depends=("libpurple=$pkgver" 'startup-notification' 'gtkspell' 'libxss' 'hicolor-icon-theme'
         'libsm' 'libidn' 'avahi' 'networkmanager' 'gst-plugins-good' 'farstream')
makedepends=('nss' 'libsasl' 'python2' 'tk' 'ca-certificates' 'intltool' )
options=('!libtool')
install=pidgin.install
source=("http://downloads.sourceforge.net/pidgin/${pkgname}-${pkgver}.tar.bz2")
md5sums=('14e0f5cfb2ed065e4dc80391a806ac76')

build() {
  cd ${pkgname}-$pkgver

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --disable-schemas-install \
                --disable-meanwhile \
                --disable-gnutls \
                --enable-cyrus-sasl \
                --enable-nm \
                --with-system-ssl-certs=/etc/ssl/certs
    make
}

package(){
    cd ${pkgbase}-${pkgver}
    #for linking
    make -C libpurple DESTDIR="${pkgdir}" install-libLTLIBRARIES

    make -C pidgin DESTDIR="${pkgdir}" install
    make -C doc DESTDIR="${pkgdir}" install

    #clean up libpurple
    make -C libpurple DESTDIR="${pkgdir}" uninstall-libLTLIBRARIES

    install -D -m 0644 pidgin/data/pidgin.desktop "${pkgdir}"/usr/share/applications/pidgin.desktop

    rm -f "${pkgdir}"/usr/share/man/man1/finch.1
}


