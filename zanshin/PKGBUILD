
pkgname=zanshin
pkgver=0.5.0
pkgrel=1
pkgdesc="A Getting Things Done application which aims at getting your mind like water."
url="https://zanshin.kde.org/"
arch=('x86_64')
license=('GPL')
depends=('akonadi-calendar' 'akonadi-notes' 'akonadi-search' 'kidentitymanagement' 'kontactinterface'
         'kldap' 'krunner' 'kwallet' 'kmime' 'kcalcore' 'kparts' 'kcoreaddons')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('office')
source=("https://download.kde.org/stable/zanshin/${pkgname}-${pkgver}.tar.xz")
md5sums=('1a01f7f34a51619b221ec51673d0c12b')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
