
pkgname=zanshin
pkgver=0.4.1
pkgrel=2
pkgdesc="A Getting Things Done application which aims at getting your mind like water."
url="https://zanshin.kde.org/"
arch=('x86_64')
license=('GPL')
depends=('akonadi-calendar' 'akonadi-notes' 'akonadi-search' 'kidentitymanagement' 'kontactinterface'
         'kldap' 'krunner' 'kwallet' 'kmime' 'kcalcore' 'kparts' 'kcoreaddons')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('office')
source=("http://files.kde.org/zanshin/${pkgname}-${pkgver}.tar.bz2")
md5sums=('0da13182cf3ace42b65500e40edbbe05')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
