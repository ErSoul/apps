
pkgbase=digikam
pkgname=('digikam' 'kipi-plugins')
pkgver=5.8.0
_pkgver=5.8.0
pkgrel=2
url="https://www.digikam.org/"
arch=('x86_64')
license=('GPL')
pkgdesc="Digital photo management application for kde"
makedepends=('knotifyconfig' 'knotifications' 'kio' 'ktextwidgets' 'kcoreaddons' 'kwindowsystem'
         'solid' 'kiconthemes' 'kfilemetadata' 'libksane' 'ki18n' 'kitemmodels' 'lensfun' 
         'marble' 'qt5-x11extras' 'jasper' 'libgphoto2' 'liblqr' 'opencv'
         'lapack' 'libgpod' 'eigen3' 'mariadb' 'akonadi-contacts'
         'kparts' 'kconfig' 'kxmlgui' 'kservice' 'threadweaver' 'karchive' 'libkexiv2' 
         'libkdcraw' 'libkipi' 'libkgeomap' 'libkvkontakte' 'libmediawiki' 'libtiff'
         'kcalcore' 'kwallet' 'phonon-qt5' 
         'pkgconfig' 'extra-cmake-modules' 'kdoctools' 'doxygen' 'boost' 'imagemagick' 'clang' 'ruby')
source=("http://download.kde.org/stable/digikam/${pkgbase}-${_pkgver}.tar.xz"
        "https://github.com/KDE/digikam/commit/855ba5b7d4bc6337234720a72ea824ddd3b32e5b.diff")
md5sums=('39ae21083ab47acbb8ca89f73716fecb'
         '9441364a642e3645f91b83aa86cbc726')

prepare() {
  cd ${pkgbase}-${_pkgver}
  sed -i -e 's|add_subdirectory( sv )|#add_subdirectory( sv )|' doc-translated/CMakeLists.txt
  # Qt 5.9.3 fix
  #cd core
  #patch -p1 -i ${srcdir}/855ba5b7d4bc6337234720a72ea824ddd3b32e5b.diff
}

build() {
   cd ${srcdir}
 
   mkdir -p build
   cd build
   cmake ../${pkgbase}-${_pkgver} \
     -DCMAKE_BUILD_TYPE=Release \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DKDE_INSTALL_LIBDIR=lib \
     -DCMAKE_SKIP_RPATH=ON \
     -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
     -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
     -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
     -DBUILD_TESTING=OFF \
     -DENABLE_AKONADICONTACTSUPPORT=ON \
     -DENABLE_MYSQLSUPPORT=ON \
     -DENABLE_KFILEMETADATASUPPORT=ON \
     -DENABLE_MEDIAPLAYER=OFF \
     -DENABLE_OPENCV3=ON \
     -DCMAKE_C_COMPILER=clang \
     -DDIGIKAMSC_CHECKOUT_DOC=ON \
     -DENABLE_APPSTYLES=ON
   make 
}

package_digikam() {
  pkgdesc="Digital photo management application for kde"
  depends=('knotifyconfig' 'knotifications' 'kio' 'ktextwidgets' 'kcoreaddons' 'kwindowsystem'
           'solid' 'kiconthemes' 'kfilemetadata' 'kitemmodels' 'libksane' 'ki18n' 'lensfun' 
           'marble' 'qt5-x11extras' 'jasper' 'libgphoto2' 'liblqr' 'opencv'
           'kipi-plugins' 'lapack' 'libgpod' 'eigen3' 'mariadb' 'kdepimlibs')
  groups=('graphics')
  
  cd ${srcdir}/build/core
  make DESTDIR=${pkgdir}/ install
  
  cd ${srcdir}/build/doc
  make DESTDIR=${pkgdir}/ 
}

package_kipi-plugins () {
  pkgdesc="Collection of plugins extending the KDE graphics and image applications as digiKam, Gwenview, and KPhotoalbum."
  depends=('kparts' 'kconfig' 'kiconthemes' 'kwindowsystem' 'kxmlgui' 'kio' 'kservice'
           'threadweaver' 'karchive' 'ki18n' 'libkexiv2' 'libkdcraw' 'libkipi' 'libkgeomap' 
           'libkvkontakte' 'libmediawiki' 'libtiff'
           'kcalcore' 'kwallet' 'phonon-qt5') # 'libkface') 
  groups=('kde' 'kde-graphics' 'kde-uninstall')
  
  cd ${srcdir}/build/extra/kipi-plugins
  make DESTDIR=${pkgdir}/ install
  
  cd ${srcdir}/build/po
  make DESTDIR=${pkgdir}/ install
}

