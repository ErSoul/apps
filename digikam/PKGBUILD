
pkgname=digikam
pkgver=6.4.0
_pkgver=6.4.0
pkgrel=3
url="https://www.digikam.org/"
arch=('x86_64')
license=('GPL')
pkgdesc="Digital photo management application for kde"
depends=('knotifyconfig' 'knotifications' 'kio' 'ktextwidgets' 'kcoreaddons' 'kwindowsystem'
         'solid' 'kiconthemes' 'kfilemetadata' 'libksane' 'ki18n' 'kitemmodels' 'lensfun' 
         'marble' 'qt5-xmlpatterns' 'qt5-x11extras' 'qtwebengine' 'libgphoto2' 'liblqr' 'opencv'
         'lapack' 'eigen3' 'mariadb' 'akonadi-contacts'
         'kparts' 'kconfig' 'kxmlgui' 'kservice' 'threadweaver' 'karchive' 'libkexiv2' 
         'libkdcraw' 'libkgeomap' 'libkvkontakte' 'libmediawiki' 'libtiff'
         'kcalendarcore' 'kwallet' 'phonon-qt5' 'kdepimlibs' 'imagemagick')
makedepends=('eigen3' 'mariadb'  'pkgconfig' 'extra-cmake-modules' 'kdoctools' 'doxygen' 'boost' 'ruby') # 'clang')
groups=('kde-graphics')
source=("https://download.kde.org/stable/digikam/${_pkgver}/${pkgname}-${_pkgver}.tar.xz"
        "https://github.com/KDE/digikam/commit/855ba5b7d4bc6337234720a72ea824ddd3b32e5b.diff")
sha256sums=('775012ff515bdd25a0b894c95bf5d33e7122b523da9c0f3af260ec3eff498d6f'
            '0fd1bc8aa9b89bc16e20cc59b928630c42bc290784e0024546eb546f165a100a')

prepare() {
  cd ${pkgname}-${_pkgver}
  sed -i -e 's|QString module = QLatin1String(inf->module);|QString module = QLatin1String(inf->magick_module);|' core/tests/dimg/magickloader.cpp
  # Qt 5.9.3 fix
  #cd core
  #patch -p1 -i ${srcdir}/855ba5b7d4bc6337234720a72ea824ddd3b32e5b.diff
}

build() {
   mkdir -p build
   cd build
   
   cmake ../${pkgname}-${_pkgver} \
     -DCMAKE_BUILD_TYPE=Release \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DKDE_INSTALL_LIBDIR=lib \
     -DCMAKE_SKIP_RPATH=ON \
     -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
     -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
     -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
     -DBUILD_TESTING=OFF \
     -DENABLE_AKONADICONTACTSUPPORT=ON \
     -DENABLE_MYSQLSUPPORT=ON \
     -DENABLE_KFILEMETADATASUPPORT=ON \
     -DENABLE_MEDIAPLAYER=OFF \
     -DCMAKE_C_COMPILER=gcc \
     -DDIGIKAMSC_CHECKOUT_DOC=ON \
     -DENABLE_APPSTYLES=ON \
     -DENABLE_QWEBENGINE=ON
   make 
}

package() {
  cd build
  make DESTDIR=${pkgdir}/ install
  
  #cd ../doc
  #make DESTDIR=${pkgdir}/ 
}
