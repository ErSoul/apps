
pkgname=toxcore
pkgver=0.2.9
pkgrel=2
pkgdesc='Easy to use, all-in-one communication platform that ensures full privacy and secure message delivery.'
arch=('x86_64')
url='https://tox.chat'
license=('GPL3')
depends=('systemd' 'libconfig' 'libsndfile' 'libsodium' 'libvpx' 'opus' 'opencv' 'portaudio')
makedepends=('check' 'msgpack-c')
options=('!staticlibs')
source=("https://github.com/TokTok/c-toxcore/archive/v${pkgver}.tar.gz")
md5sums=('4e52ca6b771229f2a5fce74f652fa38a')

build() {
  cd c-${pkgname}-${pkgver}
  
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-tests \
    --enable-daemon
  make
}

check() {
  cd c-${pkgname}-${pkgver}
  # tox_test 
  make check 
}

package() {
  cd c-${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

