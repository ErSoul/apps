
pkgname=zenlib
_pkgname=ZenLib
pkgver=0.4.37
pkgrel=2
pkgdesc="Small C++ derivate classes to have an easier life"
arch=('x86_64')
url="https://github.com/MediaArea/ZenLib"
license=('BSD')
depends=('gcc-libs')
makedepends=('libtool')
conflicts=('libzen')
replaces=('libzen')
source=("https://github.com/MediaArea/ZenLib/archive/v${pkgver}.tar.gz")
md5sums=('31bd9badb2587e8a087dd0889e19b609')

build() {
  cd ${_pkgname}-${pkgver}/Project/GNU/Library
  
  ./autogen.sh
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ${_pkgname}-${pkgver}/Project/GNU/Library
  make DESTDIR=${pkgdir} install
  
  cd ../../..
  install -D -m 644 License.txt ${pkgdir}/usr/share/licenses/zenlib/License.txt
}
