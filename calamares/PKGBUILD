
pkgname=calamares
pkgver=2.3.93
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('x86_64')
url='http://calamares.io/'
license=('LGPL')
depends=('qt5-svg' 'qt5-webkit' 'kconfig' 'ki18n' 'kcoreaddons' 'solid' 'yaml-cpp'
         'parted' 'libatasmart' 'udisks2' 'polkit-qt5' 'kpmcore' 'kparts')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools') # 'clang')
options=('debug' '!staticlibs')
#source=("git://github.com/KaOSx/calamares#commit=8d1145719acb6e3cc11d22f4ab4c61a1dfb9cd81"
source=("git://github.com/KaOSx/calamares"
#source=("git://github.com/demmm/calamares"
        'calamares.desktop'
        'installer.svg'
        'kdm_sddm.conf'
        'settings.conf'
        'unpackfs.conf'
        'launch-calamares.sh'
        'license.pdf'
        'release_notes.html')
md5sums=('SKIP'
         '663fce16ac08b3149516e6a0d2797f65'
         'd5c65f43e057054e9728810530c4a030'
         '184d5b6886fc7a6156d4b790657f77f4'
         '4de3b3695b8169a536c2c574fba450fe'
         '8b69a3a86923e2ee67a5a0725acb93e6'
         'fc92a5a0a1276a8e8f1a3327a0e567ee'
         'bb33b54d289cfe39ebfd78f172382880'
         '585df49a0daf0b7e9186dd12016d41a2')

prepare () {
  cd ${srcdir}/${pkgname}
  
  git submodule init
  git submodule update
  
  sed -i 's|VERSION_LESS 5.6|VERSION_LESS 5.8|' src/modules/webview/CMakeLists.txt
}

build() {
  #export CC=/usr/bin/clang
  #export CXX=/usr/bin/clang++
  
  mkdir -p build
  cd build
  
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_CRASHREPORTER=ON
  make
}

package() {
  cd build 
  make DESTDIR="${pkgdir}" install
  
  install -Dm755 "${srcdir}/launch-calamares.sh" "${pkgdir}/usr/bin/launch-calamares.sh"
  rm -rf "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "${srcdir}/installer.svg" "${pkgdir}/usr/share/pixmaps/installer.svg"
  install -Dm644 "$srcdir/license.pdf" "$pkgdir/usr/share/calamares/license.pdf"
  install -Dm644 "$srcdir/release_notes.html" "$pkgdir/usr/share/calamares/release_notes.html"
}
