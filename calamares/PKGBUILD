
pkgname=calamares
pkgver=0.7.1b
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('x86_64')
url='https://github.com/calamares/calamares'
license=('LGPL')
depends=('qt5-svg' 'kconfig' 'ki18n' 'kcoreaddons' 'solid' 'yaml-cpp'
         'parted' 'libatasmart' 'udisks2')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
source=("git://github.com/KaOSx/calamares"
#source=("calamares.tar.xz"
        'calamares.desktop'
        'displaymanager.conf'
        'grubcfg.conf'
        'locale.conf'
        'prepare.conf'
        'settings.conf'
        'unpackfs.conf'
        'launch-calamares.sh'
        'installer.svg'
        'GreetingPage.diff'
        'CalamaresStyle.diff'
        'kf5.diff'
        'UEFI.diff'
        'ProgressTreeDelegate.diff')
md5sums=('SKIP'
         '8c263c9faeec86a33c02e743d9b083f7'
         '27f8ad3ce42bff7158fa0cb9b252e144'
         'e1cfaec54b0b1bddad582eebdfca9bc2'
         '6b793a81d051d5d0b00a6031ceb08308'
         'ebc235d4b2f9a425bf2161d7a252dce3'
         'd8fd5954016d93c5fd13f1a9f1653d3b'
         '8b69a3a86923e2ee67a5a0725acb93e6'
         '2437e44479a54376ad9244d120369f6c'
         'd5c65f43e057054e9728810530c4a030'
         '7e60cf6d43c37a924bb6e0cfa0bf60fc'
         '68245f2d061ac06d19fc0bab3765eca6'
         '75c7823c5e551f7591b5d32f2ca2aa47'
         'f7a20dfcd71feeabc3d92f29d7504c02'
         'b329f553eb3d4b72f17e0c4656471c60')

prepare () {
  cd ${srcdir}/${pkgname}
  
  git submodule init
  git submodule update
  sed -i 's|Ext4|Xfs|' ${srcdir}/${pkgname}/src/modules/partition/tests/PartitionJobTests.cpp
  sed -i 's|Ext4|Xfs|' ${srcdir}/${pkgname}/src/modules/partition/gui/EraseDiskPage.cpp
  patch -p1 -i ${srcdir}/GreetingPage.diff
  patch -p1 -i ${srcdir}/CalamaresStyle.diff
  # switch on/off for kde4/kf5 builds:
  patch -p1 -i ${srcdir}/kf5.diff
  patch -p1 -i ${srcdir}/UEFI.diff
  patch -p1 -i ${srcdir}/ProgressTreeDelegate.diff
}

build() {
  mkdir -p build
  
  cd build
  
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_PARTITIONMANAGER=1 \
    -DCMAKE_INSTALL_LIBDIR=lib 
  make
}

package() {
  cd build 
  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}/usr/share/calamares/settings.conf"
  install -D -m644 "${srcdir}/settings.conf" "${pkgdir}/usr/share/calamares/settings.conf"
  install -D -m644 "${srcdir}/displaymanager.conf" "${pkgdir}/etc/calamares/modules/displaymanager.conf"
  install -D -m644 "${srcdir}/grubcfg.conf" "${pkgdir}/etc/calamares/modules/grubcfg.conf"
  install -D -m644 "${srcdir}/locale.conf" "${pkgdir}/etc/calamares/modules/locale.conf"
  install -D -m644 "${srcdir}/prepare.conf" "${pkgdir}/etc/calamares/modules/prepare.conf"
  install -D -m644 "${srcdir}/unpackfs.conf" "${pkgdir}/etc/calamares/modules/unpackfs.conf"
  #sed 's|/path/to/another/filesystem.sqfs|/bootmnt/kdeos/x86_64/root-image.sqfs|' -i "${pkgdir}/usr/share/calamares/modules/unpackfs.conf"
  sed 's|linux312|linux|' -i "${pkgdir}/usr/share/calamares/modules/initcpio.conf"
  
  install -Dm755 "${srcdir}/launch-calamares.sh" "${pkgdir}/usr/bin/launch-calamares.sh"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "${srcdir}/installer.svg" "${pkgdir}/usr/share/pixmaps/installer.svg"
}
