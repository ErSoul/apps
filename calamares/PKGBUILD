
pkgname=calamares
pkgver=1.0.2
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('x86_64')
url='https://github.com/calamares/calamares'
license=('LGPL')
depends=('qt5-svg' 'kconfig' 'ki18n' 'kcoreaddons' 'solid' 'yaml-cpp'
         'parted' 'libatasmart' 'udisks2' 'polkit-qt5')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
source=("git://github.com/KaOSx/calamares"
        'calamares.desktop'
        'installer.svg'
        'kdm_sddm.conf'
        'settings.conf'
        'launch-calamares.sh'
        'installer.svg')
md5sums=('SKIP'
         '8c263c9faeec86a33c02e743d9b083f7'
         'd5c65f43e057054e9728810530c4a030'
         '184d5b6886fc7a6156d4b790657f77f4'
         'aa8b7ef22d13a936833e966f2946f68c'
         '2437e44479a54376ad9244d120369f6c'
         'd5c65f43e057054e9728810530c4a030')

prepare () {
  cd ${srcdir}/${pkgname}
  
  git submodule init
  git submodule update
  sed -i 's|Ext4|Xfs|' ${srcdir}/${pkgname}/src/modules/partition/tests/PartitionJobTests.cpp
  sed -i 's|Ext4|Xfs|' ${srcdir}/${pkgname}/src/modules/partition/gui/EraseDiskPage.cpp
  sed -i 's|Ext4|Xfs|' ${srcdir}/${pkgname}/src/modules/partition/gui/ReplacePage.cpp
  # toggle on/off for kf5/kde 4 builds
  #sed -i 's|KaOS|KaOS-kf5|' ${srcdir}/${pkgname}/src/modules/bootldr/bootldr.conf
}

build() {
  mkdir -p build
  
  cd build
  
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_PARTITIONMANAGER=1 \
    -DCMAKE_INSTALL_LIBDIR=lib 
  make
}

package() {
  cd build 
  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}/usr/share/calamares/settings.conf"
  install -D -m644 "${srcdir}/settings.conf" "${pkgdir}/usr/share/calamares/settings.conf"
  install -D -m644 "${srcdir}/kdm_sddm.conf" "${pkgdir}/etc/calamares/modules/kdm_sddm.conf"
  
  install -Dm755 "${srcdir}/launch-calamares.sh" "${pkgdir}/usr/bin/launch-calamares.sh"
  rm -rf "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "${srcdir}/installer.svg" "${pkgdir}/usr/share/pixmaps/installer.svg"
}
