
pkgname=desmume
pkgver=0.9.10
pkgrel=1
pkgdesc="Nintendo DS emulator"
arch=('x86_64')
url="http://desmume.org/"
license=('GPL')
depends=('desktop-file-utils' 'libpcap' 'soundtouch' 'alsa-lib' 'glu' 'sdl' 'gtk2' 'zziplib')
makedepends=('intltool' 'mesa')
install=desmume.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.tar"
        "glx_3Demu.patch")
md5sums=('a6aedfe5d6437d481aa9ac5fb5aebbea'
         '352ea0ffa109f68da2fb79f30d2c85e2')
         
build() {
  cd "$srcdir/$pkgname-$pkgver"

  # http://sourceforge.net/p/desmume/code/4907/
  patch -p2 -i "$srcdir"/glx_3Demu.patch
  # https://sourceforge.net/tracker/?func=detail&aid=3612768&group_id=164579&atid=832291
  sed -i 's/@GETTEXT_PACKAGE@/desmume/' po/Makefile.in.in

  ./configure --prefix=/usr --enable-wifi
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}


