
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdepim
pkgver=${_kdever}
pkgrel=2
arch=('x86_64')
pkgdesc="KDE's Personal Information Management suite"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
depends=('grantlee' 'phonon-qt5' 'qt5-tools' 'prison' 'libkgapi' 'qt5-webkit' 'qtwebengine' # fails with Qt5.5.1 'qtspeech'
         #frameworks
         'kdelibs4support' 'kwallet' 'knewstuff' 'kcmutils' 'kdewebkit' 'karchive' 'knotifyconfig'
         'kconfig' 'khtml' 'kservice' 'kdbusaddons' 'kauth' 'ktexteditor' 'kdnssd' 'kcodecs' 'kglobalaccel'
         'sonnet' 'kross' 
         #kdepimlibs
         'kcontacts' 'kcalcore' 'kidentitymanagement' 'kldap' 'kmailtransport' 'kcalutils' 'kontactinterface'
         'kholidays' 'ktnef' 'kimap' 'kmbox' 'akonadi-calendar' 'syndication' 'gpgmepp' 'kalarmcal' 'kmime'
         'kxmlrpcclient' 'kblog' 'kdepimlibs' 'akonadi-search' 'kdepim-runtime' 'libgravatar' 'mailimporter'
         'mailcommon' 'kdepim-apps-libs' 'libkleo' 'grantleetheme' 'pimcommon' 'libkdepim' 'incidenceditor'
         'messagelib' 'calendersupport' 'eventviews' 'kdgantt2' 'kmailtransport')
makedepends=('boost' 'extra-cmake-modules' 'kdoctools' 'clang')
groups=('kde' 'kdepim' 'kde-uninstall')
install=kdepim.install
source=($_mirror/${pkgname}-${_kdever}.tar.xz
        'CMakeLists.diff')
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '2218a2df5d6f3fb09c0616a68e4e13f2')

prepare() {
  cd $pkgname-${pkgver}
  #sed -i 's|add_subdirectory(korganizer)|#add_subdirectory(korganizer)|' ${srcdir}/$pkgname/CMakeLists.txt
  patch -p1 -i $srcdir/CMakeLists.diff
}

build() {
  mkdir build
  
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++
  
  cd build 
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=FALSE \
    -DKDEPIM_NO_TEXTTOSPEECH=TRUE \
    -DQTWEBENGINE_EXPERIMENTAL_OPTION=TRUE
  make VERBOSE=1
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
