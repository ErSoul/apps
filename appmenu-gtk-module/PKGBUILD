
pkgname=appmenu-gtk-module
pkgver=0.6.94
pkgrel=1
pkgdesc="Small GTK Module that strips menus from all GTK programs, converts to MenuModel and send to AppMenu."
arch=('x86_64')
url="https://github.com/rilian-la-te/vala-panel-appmenu/tree/master/subprojects/appmenu-gtk-module"
license=('GPL')
depends=('gtk2' 'gtk3')
makedepends=('cmake')
source=("https://github.com/rilian-la-te/vala-panel-appmenu/releases/download/${pkgver}/appmenu-gtk-module.tar.xz"
        '80-appmenu-gtk-module')
md5sums=('26aac3ea932a7a42c37faeca040f3a30'
         'eef92e41f9e4c2ea16c0d706cc511540')

build() {
    mkdir -p build
    cd build

    cmake ../${pkgname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib 
    make 
}

package() {
    cd build
    
    make DESTDIR=${pkgdir} install
    
    install -Dm755 ../80-appmenu-gtk-module ${pkgdir}/etc/X11/xinit/xinitrc.d/80-appmenu-gtk-module
}
