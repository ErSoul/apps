
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=gwenview
pkgver=${_kdever}
pkgrel=1
pkgdesc="A fast and easy to use image viewer for KDE"
arch=('x86_64')
url="http://kde.org/applications/graphics/gwenview/"
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs4support' 'kf5-baloo' 'kf5-kde-baseapps' 'phonon-qt5' 'libpng' 'exiv2' 'lcms2'
         'libjpeg-turbo' 'libkdcraw' 'libkipi')
makedepends=('extra-cmake-modules' 'kdoctools' 'pkg-config')
opdepends=('kamera: to work directly with cameras')
install=${pkgname}.install
groups=('graphics')
conflicts=('kdegraphics-gwenview')
#options=('docs')
source=("$_mirror/${pkgname}-${pkgver}.tar.xz"
        'libkipi1.patch'
        'libkipi.patch')
#md5sums=(`grep ${_pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)
md5sums=('c1970a4cce19f1e5935f8e955a6f9ab8'
         '700f0fce42e9784a749942b940ffbcb2'
         '2327dc25351d528b8207467039a8c8d6')

prepare() {
  cd $pkgname-${pkgver}
  sed -i -e 's/Kdcraw/KF5KDcraw/' ${srcdir}/${pkgname}-${pkgver}/CMakeLists.txt
  patch -p1 -i $srcdir/libkipi1.patch
  patch -p1 -i $srcdir/libkipi.patch
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}

