
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kio
pkgver=${_kf5ver}
pkgrel=2
pkgdesc='Framework that implements almost all the file management functions you will ever need'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kio'
license=('LGPL')
depends=('karchive' 'kconfig' 'kcoreaddons' 'kdbusaddons' 'ki18n' 'kservice' 'solid'
         'kbookmarks' 'kcompletion' 'kconfigwidgets' 'kiconthemes' 'kitemviews' 'kjobwidgets'
         'kwidgetsaddons' 'kwindowsystem' 'kwallet' 'knotifications' 'krb5' 'qt5-script')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/5.20/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/KDE/kio/commit/f9d0cb47cf94e209f6171ac0e8d774e68156a6e4.diff")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '77192bff358a7c26df0b4444cecee5e6')

prepare() {
  cd ${pkgname}-${pkgver}
  # CVE fix
  patch -p1 -i ${srcdir}/f9d0cb47cf94e209f6171ac0e8d774e68156a6e4.diff
  #sed -i -e 's|-caption=|-qwindowtitle|' src/core/desktopexecparser.cpp
}
         
build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR="${pkgdir}" install
}
