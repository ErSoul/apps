
pkgname=system-config-printer
pkgver=1.5.9
pkgrel=1
pkgdesc="A CUPS printer configuration tool and status applet"
url="https://github.com/zdohnal/system-config-printer"
arch=('x86_64')
license=('GPL')
depends=('pycups' 'dbus-python3' 'pysmbc' 'libusbx' 'python3-pycurl' 'systemd'
         'python3-gobject3' 'dbus-glib' 'gtk3' 'python3-requests')
makedepends=('intltool' 'python3' 'xmlto' 'docbook-xsl' 'desktop-file-utils' 'libcups' 'libxml2')
optdepends=('print-manager: for the administration tool in KDE System Settings')
provides=('system-config-printer-common')
source=("https://github.com/zdohnal/system-config-printer/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('8b449c4b00fb170d79a6b781eab75bb3')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "s|cups.socket|org.cups.cupsd.socket|g" udev/configure-printer@.service.in

  ./bootstrap
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udev-rules \
    --with-udevdir=/usr/lib/udev
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  
  #rm ${pkgdir}/usr/share/applications/system-config-printer.desktop
}
