
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdepim-addons
pkgver=${_kdever}
pkgrel=2
arch=('x86_64')
pkgdesc="Addons for kdepim apps (as kmail addons/ kmail theme/ kaddressbook addons etc.)"
url="https://github.com/KDE/kdepim-addons"
license=('GPL' 'LGPL' 'FDL')
depends=('libkgapi' 'qtwebengine' 'boost-libs'
         #frameworks
         'ki18n' 'khtml' 'kconfig' 'kdbusaddons' 'kxmlgui' 
         #kdepimlibs
         'kdepimlibs' 'mailcommon' 'kdepim-apps-libs' 'libkleo' 'grantleetheme'
         'pimcommon' 'libkdepim' 'incidenceeditor'
         'messagelib' 'calendarsupport' 'eventviews' 'akonadi' 'libgravatar' 'ktnef')
makedepends=('extra-cmake-modules' 'kdoctools' 'clang' 'boost')
groups=('kde' 'kdepim' 'kde-uninstall')
install=kdepim-addons.install
source=($_mirror/${pkgname}-${_kdever}.tar.xz
       "https://github.com/KDE/kdepim-addons/commit/ad9b246409bf9fbea70f57a69b4cfef1ca45c2b3.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '1b46c181b1e22e2d381e377c93f735af')

prepare() {
  cd $pkgname-${pkgver}
  #sed -i 's|add_subdirectory(korganizer)|#add_subdirectory(korganizer)|' ${srcdir}/$pkgname/CMakeLists.txt
  # fix license issue
  patch -p1 -i ${srcdir}/ad9b246409bf9fbea70f57a69b4cfef1ca45c2b3.diff
}

build() {
  mkdir build
  
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++
  
  cd build 
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=FALSE \
    -DQTWEBENGINE_EXPERIMENTAL_OPTION=TRUE \
    -DKDEPIMADDONS_BUILD_EXAMPLES=TRUE
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
