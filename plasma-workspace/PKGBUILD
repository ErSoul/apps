
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-workspace
pkgver=4.96.0.1
pkgrel=1
pkgdesc='Provides the interface and basic tools for the KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('frameworkintegration' 'kcmutils' 'knotifyconfig' 'krunner' 'kdesu' 'knewstuff' 'khtml'
         'libdbusmenu-qt5' 'glu' 'kjsembed' 'libxkbfile' 'libxcursor' 'lm_sensors' 'libraw1394'
         'libusb-compat' 'pciutils' 'libbsd' 'kf5-akonadi' 'qt5-webkit' 'kf5-attica'
         'kemoticons' 'libxrandr' 'kded' 'kwin' 'libksysguard' 'ktexteditor' 'kde-cli-tools'
         'khelpcenter' 'kio-extras' 'milou' 'libkscreen2' 'breeze' 'gpsd' 'libqalculate' 'qt5-tools'
         'qimageblitz-qt5' 'oxygen-icons')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('plasma')
conflicts=('kde-workspace' 'kde-runtime')
#source=("http://download.kde.org/unstable/plasma/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
source=("git://anongit.kde.org/plasma-workspace.git"
        'kf5-plasma.desktop'
        'startkde'
        '10-kf5'
        'kabout1.diff'
        'kabout2.diff')
md5sums=('SKIP'
         '277dd50ca93e8136ea40a9f1a10f59be'
         '1c093f5c012fe42bd00e9d7b9189bb15'
         '6d0a507a9f2de77f5bb7c1de9f93e8c4'
         '0748e9ecc97fa34f414d2bff90ce89b0'
         'c7482f960336bbf9f8144d4ed93278b3')

build() {
  cd ${srcdir}/${pkgname}
  patch -p1 -Ri ${srcdir}/kabout1.diff
  patch -p1 -Ri ${srcdir}/kabout2.diff
  cd ..
  
  mkdir -p build
  cd build
  
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  rm "$pkgdir/usr/bin/startkde"
  install -Dm755 "${srcdir}/startkde" "$pkgdir/usr/bin/startkde"
  
  install -Dm755 "${srcdir}/10-kf5" "${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5"
  
  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  install -dm755 "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/kf5-plasma.desktop" "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -sr "${pkgdir}/usr/share/apps/kdm/sessions/kf5-plasma.desktop" -t "${pkgdir}/usr/share/xsessions/"
}