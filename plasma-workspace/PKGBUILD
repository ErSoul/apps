
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-workspace
pkgver=${_plasmaver}
_pkgver=5.5.5
pkgrel=2
pkgdesc='Provides the interface and basic tools for the KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('frameworkintegration' 'kcmutils' 'knotifyconfig' 'krunner' 'kdesu' 'knewstuff' 'khtml'
         'libdbusmenu-qt5' 'glu' 'kjsembed' 'libxkbfile' 'libxcursor' 'lm_sensors' 'libraw1394'
         'libusb' 'pciutils' 'libbsd' 'kf5-attica' 
         'kemoticons' 'libxrandr' 'kded' 'kwin' 'libksysguard' 'ktexteditor' 'kde-cli-tools'
         'khelpcenter' 'kio-extras' 'baloo' 'milou' 'libkscreen' 'breeze' 'gpsd' 'libqalculate' 'qt5-tools'
         'qimageblitz' 'oxygen-icons' 'prison' 'sddm-kcm' 'networkmanager-qt' 'kxmlrpcclient' 'kscreenlocker'
         'kactivitymanagerd' 'iso-codes')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('plasma5')
options=('debug')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        '10-kf5'
        'kde.pam'
        'kde-np.pam'
        'kscreensaver.pam'
        "https://github.com/KDE/plasma-workspace/commit/cc5ee1a8495aaf126abf1e7c7d3163ea5813d279.diff"
        "https://github.com/KDE/plasma-workspace/commit/1c70951abe9f52948b6e1c3ea0dc191997e26b2a.diff")
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         'beba34da9144bced3ef2a225be532109'
         '6589fb28bd20d5ec1b4a7b9db9fc4209'
         '5a035421a9bfaa353caf04250c2b285c'
         '367a3538f54db71f108b34cfa31088ac'
         '5c78b5035ea5d288d4b68839cfa1e79b'
         '1c03cd7e251aa78ff57c2ba8c726246d')

prepare () {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  patch -p1 -i $srcdir/cc5ee1a8495aaf126abf1e7c7d3163ea5813d279.diff
  patch -p1 -i $srcdir/1c70951abe9f52948b6e1c3ea0dc191997e26b2a.diff
  #sed -i 's|find_package(Prison "1.2.0" QUIET CONFIG)|find_package(KF5Prison "1.2.0" QUIET CONFIG)|' ${srcdir}/${pkgname}-${pkgver}/klipper/CMakeLists.txt
}         
         
build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  sed 's|qtpaths|qtpaths-qt5|' -i $pkgdir/usr/bin/startkde
  sed 's|qtpaths|qtpaths-qt5|' -i $pkgdir/usr/bin/startplasmacompositor
  
  install -Dm755 ${srcdir}/10-kf5 ${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5
  
  # install pam configuration
  install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
  install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
  install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver
}
