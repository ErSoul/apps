
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-workspace
pkgver=4.96.0
pkgrel=7
pkgdesc='Provides the interface and basic tools for the KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('frameworkintegration' 'kcmutils' 'knotifyconfig' 'krunner' 'kdesu' 'knewstuff' 'khtml'
         'libdbusmenu-qt5' 'glu' 'kjsembed' 'libxkbfile' 'libxcursor' 'lm_sensors' 'libraw1394'
         'libusb-compat' 'pciutils' 'libbsd' 'kf5-akonadi' 'qt5-webkit' 'kf5-attica'
         'kemoticons' 'libxrandr' 'kded' 'kwin' 'libksysguard' 'ktexteditor' 'kde-cli-tools'
         'khelpcenter' 'kio-extras' 'milou' 'libkscreen2' 'breeze' 'gpsd' 'libqalculate' 'qt5-tools'
         'qimageblitz' 'oxygen-icons')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('plasma')
conflicts=('kde-workspace' 'kde-runtime')
source=("http://download.kde.org/unstable/plasma/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'kf5-plasma.desktop'
        'startkde')
md5sums=('cebd06411902dde2087b747d5a916f11'
         '277dd50ca93e8136ea40a9f1a10f59be'
         '1c093f5c012fe42bd00e9d7b9189bb15')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  rm "$pkgdir/usr/bin/startkde"
  install -Dm755 "${srcdir}/startkde" "$pkgdir/usr/bin/startkde"
  
  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  install -dm755 "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/kf5-plasma.desktop" "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -sr "${pkgdir}/usr/share/apps/kdm/sessions/kf5-plasma.desktop" -t "${pkgdir}/usr/share/xsessions/"
}