
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kinfocenter
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='KInfocenter'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kinfocenter'
license=('LGPL')
depends=('glu' 'kcmutils' 'kdelibs4support' 'kemoticons' 'libraw1394' 'pciutils' 'plasma-framework' 'kwayland')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('plasma5')
conflicts=('kde-workspace' 'about-distro')
replaces=('about-distro')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"
        'kcm-about-distrorc'
        'KaOS.png')
md5sums=(`grep ${pkgname}-$_plasmaver.tar.xz ../kf5.md5 | cut -d" " -f1`
         '598273ffd5442cacfd262b3920516c46'
         '0957d15eee83c30c1351571d3b875bc9')


build() {
  mkdir -p build
  #sed -e "s/PO_FILES //" -i ${srcdir}/$pkgname-$pkgver/po/*/CMakeLists.txt
  cd build
  
  cmake ../${pkgname}-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  install -Dm644 "$srcdir/kcm-about-distrorc" "$pkgdir/etc/xdg/kcm-about-distrorc"
  install -Dm644 "$srcdir/KaOS.png" "$pkgdir/usr/share/$pkgname/KaOS.png"
}

