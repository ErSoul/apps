
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kinfocenter
pkgver=4.98.0
pkgrel=1
pkgdesc='KInfocenter'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kinfocenter'
license=('LGPL')
depends=('glu' 'kcmutils' 'kdelibs4support' 'kemoticons' 'libraw1394' 'pciutils' 'plasma-framework')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kde-workspace')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('bd15429e04a9b5df8b681ed55e09871e')
#source=("git://anongit.kde.org/kinfocenter.git")
#md5sums=('SKIP')

build() {
  mkdir -p build
  sed -e "s/PO_FILES //" -i ${srcdir}/$pkgname-$pkgver/po/*/CMakeLists.txt
  cd build
  
  cmake ../${pkgname}-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

