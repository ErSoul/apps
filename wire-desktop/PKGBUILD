
pkgname=wire-desktop
pkgver=3.4.2879
pkgrel=1
pkgdesc="Cross platform desktop app, wrapping the wire-webapp. Based on Electron"
arch=('x86_64')
url="https://wire.com/en/"
license=('GPL3')
depends=('alsa-lib' 'gconf' 'gtk2' 'libxtst' 'nss')
makedepends=('nodejs' 'yarn')
source=("https://github.com/wireapp/wire-desktop/archive/release/${pkgver}.tar.gz"
        'wire-desktop.desktop')
md5sums=('4a1f1e185fc6d4b19b2c1a43ba78365c'
         '32a998ab2d5cec93ab975e5aff4ad223')

build() {
  cd ${pkgname}-release-${pkgver}
  
  yarn
  yarn build:ts
  npx grunt 'linux-other'
}

package() {
  cd ${pkgname}-release-${pkgver}
  
  install -d ${pkgdir}/usr/lib/${pkgname}
  install -d ${pkgdir}/usr/bin
  
  cp -a wrap/dist/linux-unpacked/* ${pkgdir}/usr/lib/${pkgname}
  ln -s /usr/lib/${pkgname}/${pkgname} ${pkgdir}/usr/bin/${pkgname}

  install -Dm644 ../${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
  install -Dm644 resources/icons/256x256.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
