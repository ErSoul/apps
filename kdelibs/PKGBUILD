
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="kdelibs"
arch=('x86_64')
pkgver=4.14.10
pkgrel=1
pkgdesc="KDE Core Libraries"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
depends=("${_qtpkg}>=${_qtver}" 'shared-mime-info' 'upower' 'udisks<2.0' 'xz' 'enchant' 'jasper'
         'openexr' 'giflib' 'strigi' 'libxtst' 'ca-certificates' 'xdg-utils' 'qca' 'polkit-qt'
         'libxss' 'phonon' 'attica' 'krb5' 'libxcursor' 'libutempter' 'hicolor-icon-theme'
         'libdbusmenu-qt' 'grantlee' 'media-player-info' 'qtwebkit' 'systemd' 'sni-qt' 'qtcurve4')
makedepends=('pkg-config' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'aspell' 'hspell' 'docbook-xsl'
             'docbook-xml' 'bzip2' 'libzip')
optdepends=('phonon-backend-gstreamer: GStreamer Phonon backend (default)'
	    'phonon-backend-vlc:       VLC Phonon backend'
	    'avahi:            Service discovery on your local network'
	    'aspell:           Spell checking'
	    'hspell:           Hebrew spell checking')
groups=("kde" "kde-minimal" "kde-uninstall")
install=${pkgname}.install
source=($_mirror/${pkgname}-${pkgver}.tar.xz
	'02_kde_applications_menu.patch'
	'MergeDir.patch')
md5sums=('68cb7191d8f7ac383cc30bfe69a016bc'
         'e94450ba5430ea9c1e33bad9ae38ca2d'   # 02_kde_applications_menu.patch
         'f442698bae9d4ffb26f6df9457d7fc5a')   # MergeDir.patch
        
build() {
	# main patches
	msg "applying main patchset ..."
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -p1 -i ${srcdir}/02_kde_applications_menu.patch

	# right positioning of applications' entries in kmenu
	patch -p1 -i ${srcdir}/MergeDir.patch

        cd ..
	rm -rf build
	mkdir -p build && cd build

	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DISTRIBUTION_TEXT='KaOS' \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF \
                -DWITH_HUpnp=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DWITH_SOLID_UDISKS2=OFF \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make VERBOSE=1
}

package() {
	cd ${srcdir}/build

	make DESTDIR=${pkgdir} install

	# link cert bundle to the one from ca-certificates
	rm -f ${pkgdir}/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt ${pkgdir}/usr/share/apps/kssl/ca-bundle.crt
}
