
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="kdelibs"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=2
pkgdesc="KDE Core Libraries"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
depends=("${_qtpkg}>=${_qtver}" "kde-common>=${_kdever}" "oxygen-icons>=${_kdever}" 
	    'shared-mime-info' 'upower' 'udisks<2.0' 'xz' 'enchant' 'jasper' 'openexr' 'giflib'
	    'strigi' 'libxtst' 'ca-certificates' 'xdg-utils' 'qca' 'polkit-qt'
	    'libxss' 'phonon' 'attica' 'krb5' 'libxcursor' 'libutempter'
	    'hicolor-icon-theme' 'libdbusmenu-qt' 'grantlee' 'media-player-info' 'qtwebkit' 'systemd')
makedepends=('pkg-config' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'aspell' 'hspell' 'shared-mime-info' 
             'docbook-xsl' 'docbook-xml' 'bzip2' 'libzip')
optdepends=('phonon-backend-gstreamer: GStreamer Phonon backend (default)'
	    'phonon-backend-vlc:       VLC Phonon backend'
	    'avahi:            Service discovery on your local network'
	    'aspell:           Spell checking'
	    'hspell:           Hebrew spell checking')
provides=("${pkgname}=${_kdever}" 'kdelibs-doc')
groups=("kde" "kde-minimal" "kde-uninstall")
conflicts=("kdelibs-doc")
install=${pkgname}.install
source=($_mirror/${pkgname}-$_kdever.tar.xz
	'02_kde_applications_menu.patch'
	'MergeDir.patch'
	'kio_global.diff'
	'polkit.diff')
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'e94450ba5430ea9c1e33bad9ae38ca2d'   # 02_kde_applications_menu.patch
         'f442698bae9d4ffb26f6df9457d7fc5a'   # MergeDir.patch
         '3f7c5f2d3819039d13b25165ba3f4de3'
         'd0b570bdfef0185a4db5ec29fa7995c3')
        
build() {
	# main patches
	msg "applying main patchset ..."
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -p1 -i ${srcdir}/02_kde_applications_menu.patch

	# right positioning of applications' entries in kmenu
	patch -p1 -i ${srcdir}/MergeDir.patch
	# bug found in compiling kradio https://projects.kde.org/projects/kde/kdelibs/repository/revisions/96250267bd7c22b47ca93084112c97e722b18c54
	patch -p1 -i ${srcdir}/kio_global.diff
	# http://seclists.org/oss-sec/2014/q3/227
	# http://quickgit.kde.org/?p=kdelibs.git&a=commit&h=e4e7b53b71e2659adaf52691d4accc3594203b23
	patch -p1 -i ${srcdir}/polkit.diff

        cd ..
	rm -rf build
	mkdir -p build && cd build

	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DISTRIBUTION_TEXT='KaOS' \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF \
                -DWITH_HUpnp=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DWITH_SOLID_UDISKS2=OFF \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make VERBOSE=1
}

package() {
	cd ${srcdir}/build

	make DESTDIR=${pkgdir} install

	# link cert bundle to the one from ca-certificates
	rm -f ${pkgdir}/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt ${pkgdir}/usr/share/apps/kssl/ca-bundle.crt
}
