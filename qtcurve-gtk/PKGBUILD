
pkgname=qtcurve-gtk
_pkgname=qtcurve
pkgver=1.8.18.15
_commit=c0018df44efe55d40a5dda9e937e7b5aa6839b08
pkgrel=2
arch=('x86_64')
license=('GPL')
pkgdesc='The QtCurve style engine for GTK.'
url='https://projects.kde.org/projects/playground/base/qtcurve'
depends=('qtcurve')
makedepends=('extra-cmake-modules' 'gtk2')
source=("https://github.com/KDE/qtcurve/archive/${_commit}.zip"
        'kdeglobals')
md5sums=('55af33127225aac2a1ee215b3df37859'
         '8ca968e7f09201b8d2376d131a8e55df')

build() {
	mkdir -p build
	cd build
	
	cmake ../${_pkgname}-${_commit} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_GTK2=true \
        -DQTC_QT5_ENABLE_KDE=false \
        -DQTC_QT4_ENABLE_KDE=false \
        -DENABLE_QT5=false \
        -DENABLE_QT4=false
	make
}

package() {
	cd build
	make DESTDIR=${pkgdir} install
	
	rm -r ${pkgdir}/usr/lib/libqtcurve-utils.so*
	rm -r ${pkgdir}/usr/share/themes/QtCurve/gtk-2.0/kdeglobals
	install -D -m644 ${srcdir}/kdeglobals ${pkgdir}/usr/share/themes/QtCurve/gtk-2.0/
}
