
pkgname=amarok
pkgver=2.8.82
pkgrel=3
_commit=8c8392e95455bd9bf83fa4ddc910f9835351aa3b
pkgdesc="A media player for KDE"
arch=('x86_64')
url="http://amarok.kde.org"
license=('GPL2' 'LGPL2' 'FDL')
depends=('kcoreaddons' 'solid' 'kdelibs4support' 'kcodecs' 'ki18n' 'threadweaver' 'plasma-framework'
         'kcmutils' 'knewstuff' 'ktexteditor' 'kglobalaccel' 'kio' 'knotifyconfig' 'karchive' 'kdnssd'
         'kconfigwidgets'
         'mariadb' 'taglib-extras' 'ffmpeg' 'libofa' 'mesa' 'loudmouth' 'clamz' 'gst-plugins-bad' 
         'ifuse' 'libgpod' 'libmtp'
         'qca-qt5' 'liblastfm' 'qjson' 'libmygpo-qt' 'qt5-webkit')
makedepends=('pkgconfig' 'extra-cmake-modules' 'kdoctools')
groups=('multimedia')
source=("https://github.com/KDE/amarok/archive/${_commit}.zip"
        "https://github.com/KDE/amarok/commit/745b2e18c5b58cfabcbf6e7863572122d673692e.diff"
        'rb126682.patch')
md5sums=('2bc69fcd0756f787e8862de0249f9022'
         'bcbfd9543ce6768181f0342f89745de6'
         'f9189d509b4f54fa382da09434ac250f')

prepare() {
  cd ${pkgname}-${_commit}
  patch -p1 -i $srcdir/745b2e18c5b58cfabcbf6e7863572122d673692e.diff
  # ffmpeg 3.1.3 needed patch
  patch -p1 -i $srcdir/rb126682.patch
  sed -i -e 's|Qt::Key_Space||' src/ActionClasses.cpp
  sed -i -e 's|setScheme( "http" )|setScheme( "https" )|' src/context/engines/tabs/TabsEngine.cpp
  sed -i -e 's|KUrl( KStandardDirs::locate|KUrl( KStandardDirs::locateLocal|' src/scripting/scriptconsole/ScriptConsole.cpp
  sed -i -e 's|PATH_SUFFIXES lastfm5|PATH_SUFFIXES lastfm|' cmake/modules/FindLibLastFm.cmake
  #sed -i -e 's|KIO NotifyConfig Archive)|KIO NotifyConfig Archive ConfigWidgets)|' ${srcdir}/${pkgname}/CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
