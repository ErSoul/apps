
pkgname=amarok
pkgver=2.8.83
pkgrel=1
_commit=0b2b243b2b70471c32d282157624379da5214051
pkgdesc="A media player for KDE"
arch=('x86_64')
url="http://amarok.kde.org"
license=('GPL2' 'LGPL2' 'FDL')
depends=('kcoreaddons' 'solid' 'kdelibs4support' 'kcodecs' 'ki18n' 'threadweaver' 'plasma-framework'
         'kcmutils' 'knewstuff' 'ktexteditor' 'kglobalaccel' 'kio' 'knotifyconfig' 'karchive' 'kdnssd'
         'kconfigwidgets'
         'mariadb' 'taglib-extras' 'ffmpeg' 'libofa' 'mesa' 'loudmouth' 'clamz' 'gst-plugins-bad' 
         'ifuse' 'libgpod' 'libmtp'
         'qca-qt5' 'liblastfm' 'qjson' 'libmygpo-qt' 'qt5-webkit')
makedepends=('pkgconfig' 'extra-cmake-modules' 'kdoctools')
groups=('multimedia')
source=("https://github.com/KDE/amarok/archive/${_commit}.zip"
        "https://github.com/KDE/amarok/commit/745b2e18c5b58cfabcbf6e7863572122d673692e.diff"
        'rb126682.patch')
md5sums=('700087b74b13a7135696d32c434867b7'
         'bcbfd9543ce6768181f0342f89745de6'
         'f9189d509b4f54fa382da09434ac250f')

prepare() {
  cd ${pkgname}-${_commit}
  #patch -p1 -i $srcdir/745b2e18c5b58cfabcbf6e7863572122d673692e.diff
  # ffmpeg 3.1.3 needed patch
  patch -p1 -i $srcdir/rb126682.patch
  sed -i -e 's|PATH_SUFFIXES lastfm5|PATH_SUFFIXES lastfm|' cmake/modules/FindLibLastFm.cmake
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
