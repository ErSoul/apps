
pkgname=bitcoin
pkgver=0.14.0
pkgrel=1
arch=('x86_64')
pkgdesc="Bitcoin is a peer-to-peer network based digital currency"
url="http://www.bitcoin.org/"
license=('MIT')
depends=('boost-libs' 'qt5-base' 'miniupnpc' 'qrencode' 'protobuf' 'libevent' 'zeromq')
makedepends=('boost' 'qt5-tools')
#source=("git://github.com/bitcoin/bitcoin.git")
source=("https://github.com/bitcoin/bitcoin/archive/v${pkgver}.tar.gz")
md5sums=('d2debd041b1f4941fc82079be8f6af13')

build() {
  cd ${pkgname}-${pkgver}
  #CXXFLAGS="$CXXFLAGS -DBOOST_VARIANT_USE_RELAXED_GET_BY_DEFAULT=1"
  CXXFLAGS="-std=c++11"

  ./autogen.sh
  ./configure --prefix=/usr \
      --with-incompatible-bdb \
      --with-gui=qt5
  make
}


package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 contrib/debian/bitcoin-qt.desktop ${pkgdir}/usr/share/applications/bitcoin.desktop
  install -Dm644 share/pixmaps/bitcoin128.png ${pkgdir}/usr/share/pixmaps/bitcoin128.png

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
