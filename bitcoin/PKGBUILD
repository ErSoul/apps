
pkgname=bitcoin
pkgver=0.9.4
pkgrel=1
arch=('x86_64')
pkgdesc="Bitcoin is a peer-to-peer network based digital currency"
url="http://www.bitcoin.org/"
license=('MIT')
depends=('boost-libs' 'qt5-base' 'miniupnpc' 'qrencode' 'protobuf')
makedepends=('boost' 'qt5-tools')
install=bitcoin.install
#source=("git://github.com/bitcoin/bitcoin.git")
source=("https://github.com/bitcoin/bitcoin/archive/v$pkgver.tar.gz")
md5sums=('8f3c0c071e6c432c0c8b47bcdb8a9046')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
  ./configure --prefix=/usr \
      --with-incompatible-bdb \
      --with-gui=qt5
  make
}


package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  install -Dm644 contrib/debian/bitcoin-qt.desktop "$pkgdir"/usr/share/applications/bitcoin.desktop
  install -Dm644 share/pixmaps/bitcoin128.png "$pkgdir"/usr/share/pixmaps/bitcoin128.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
