
pkgname=bitcoin
pkgver=0.9.2.1
pkgrel=1
arch=('x86_64')
pkgdesc="Bitcoin is a peer-to-peer network based digital currency"
url="http://www.bitcoin.org/"
license=('MIT')
depends=('boost-libs' 'qt' 'miniupnpc' 'qrencode' 'protobuf')
makedepends=('automoc4' 'boost')
install=bitcoin.install
#source=("git://github.com/bitcoin/bitcoin.git")
source=("https://github.com/bitcoin/bitcoin/archive/v$pkgver.tar.gz")
md5sums=('bd3caef17ba5bf9075ff26cd1b29cdae')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
  ./configure --prefix=/usr --with-incompatible-bdb
  make
}


package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  install -Dm644 contrib/debian/bitcoin-qt.desktop "$pkgdir"/usr/share/applications/bitcoin.desktop
  install -Dm644 share/pixmaps/bitcoin128.png "$pkgdir"/usr/share/pixmaps/bitcoin128.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
