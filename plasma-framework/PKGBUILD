
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-framework
pkgver=${_kf5ver}
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('x86_64')
url='https://projects.kde.org/projects/playground/libs/plasma-framework'
license=('LGPL')
depends=('kactivities' 'karchive' 'kconfig' 'kconfigwidgets' 'kcoreaddons' 'kdbusaddons' 'kdeclarative' 'kglobalaccel'
         'kguiaddons' 'ki18n' 'kiconthemes' 'kio' 'kservice' 'kwindowsystem' 'kxmlgui' 'knotifications' 'kpackage'
         'qt5-quickcontrols')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kf5')
options=('debug')
source=("http://download.kde.org/stable/frameworks/5.19/${pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}
  # https://bugs.kde.org/show_bug.cgi?id=359388
  #patch -p1 -i ${srcdir}/iconitem.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR="${pkgdir}" install
}
