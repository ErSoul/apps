
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-framework
pkgver=${_kf5ver}
pkgrel=3
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('x86_64')
url='https://github.com/KDE/plasma-framework'
license=('LGPL')
depends=('kactivities' 'karchive' 'kconfig' 'kconfigwidgets' 'kcoreaddons' 'kdbusaddons' 'kdeclarative' 'kglobalaccel'
         'kguiaddons' 'ki18n' 'kiconthemes' 'kio' 'kservice' 'kwindowsystem' 'kxmlgui' 'knotifications' 'kpackage'
         'kwayland' 'qt5-quickcontrols' 'kirigami')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kf5')
options=('debug')
source=("http://download.kde.org/stable/frameworks/5.19/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/KDE/plasma-framework/commit/d15f0fa8322dc2632f15024457bea41e7dd058d7.diff")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '584f9f38e02c747a02d65efbabf32bcd')

prepare() {
  cd ${pkgname}-${pkgver}
  # https://bugs.kde.org/show_bug.cgi?id=394423
  patch -p1 -i ${srcdir}/d15f0fa8322dc2632f15024457bea41e7dd058d7.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
