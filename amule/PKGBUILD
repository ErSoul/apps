
pkgname=amule
pkgver=2.3.2
pkgrel=2
epoch=2
pkgdesc="An eMule-like client for ed2k p2p network"
arch=('x86_64')
url="http://www.amule.org"
license=('GPL')
depends=('wxgtk' 'gd' 'geoip' 'libupnp' 'crypto++')
#source=("https://github.com/amule-project/amule/archive/${_commit}.zip")
source=("https://sourceforge.net/projects/amule/files/aMule/${pkgver}/aMule-${pkgver}.tar.xz")
md5sums=('4e666c33f2c87874d389d6d45eb82b7a')

build() {
  cd aMule-${pkgver}

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-cas \
              --enable-wxcas \
              --enable-amule-daemon \
              --enable-amulecmd \
              --enable-amule-gui \
              --enable-alc \
              --enable-alcc \
              --enable-webserver \
              --disable-debug \
              --enable-optimize \
              --enable-geoip \
              --enable-upnp \
              --with-wx-config=wx-config \
              --without-boost

  make 
}

package() {
  cd aMule-${pkgver} 
  
  make DESTDIR=${pkgdir}/ install 
  # conflict with glibc
  #rm "${pkgdir}/usr/share/locale/locale.alias"
}

