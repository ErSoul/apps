 
pkgname=spglib
pkgver=1.11.1.2
pkgrel=1
pkgdesc="Library for finding and handling crystal symmetries written in C."
arch=("x86_64")
url="http://atztogo.github.io/spglib/"
license=('custom')
depends=('glibc')
makedepends=('cmake')
source=("https://github.com/atztogo/spglib/archive/v${pkgver}.tar.gz")
md5sums=('9a0c9ae948ae795507ed56c57da5a7f4')

prepare () {
  cd ${pkgname}-${pkgver}
  
  sed -i 's|${CMAKE_INSTALL_PREFIX}/include|${CMAKE_INSTALL_PREFIX}/include/spglib|' CMakeLists.txt
  #sed -i 's|${PROJECT_SOURCE_DIR}/include|${pkgdir}/usr/include/spglib|' CMakeLists.txt
}      

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
